***********************************
* MANTENIMIENTO DE NOTAS DE RECIBOS

*DEBE SER LLAMADO DESDE MAIN (V_MUMAIN.PRG)
*IF NOT V_VALACC()
*  RETURN
*ENDIF  

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U"
  DO V_GETEMP
ENDIF

IF TYPE("USER")="U" OR TYPE("MACHINE")="U"
  PUBLIC USER, MACHINE
  USER   =v_getuin("U") 
  MACHINE=v_getuin("M")
ENDIF

IF .NOT. V_USRAUT(USER,"RECIBOS",1) 
    RETURN
ENDIF

NROLINEAS=10

DO SET_AMBI
DO SET_GRAL
IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF
DO V_ONKEYS

SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA
SELECT V_REC_HD
SET FILTER TO HR_EMPR=EMPRESA
SEEK EMPRESA
DO REC_VER
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_REC
ENDDO

DO FIN

RETURN

*******************************************************************
PROCEDURE SET_AMBI
******************
SET COLOR SET TO plasmar
SET BLINK OFF
SET CLOCK ON
SET CONFIRM ON
SET EXCLU OFF
SET DATE TO DMY
SET DELE ON
SET TALK OFF
SET SAFETY OFF
SET ESCAPE OFF
SET EXACT OFF
RETURN

PROCEDURE SET_GRAL
******************
*MUESTRA PANTALLA INICIAL E INICIALIZA AMBIENTE CON 'SETUP'
SET CURSOR OFF
MENSAJE='ESPERE UN MOMENTO POR FAVOR '+ALLTRIM(USER)+'.'
CLEAR
DO V_REC_CA.SPR
S_INI=SECONDS()
DO SETUP
S_FIN=SECONDS()
IF S_FIN-S_INI<1
  @ 23,0  
  WAIT '' TIMEOUT 2-(S_FIN-S_INI)
ENDIF
SET CURSOR ON
CLEAR
RETURN

PROCEDURE SETUP
***************
* INICIALIZA AMBIENTE
SELECT 1
USE V_REC_HD ORDER CORRE
SELECT 2
USE V_REC_LN ORDER CORRE
****
SET RELATION TO LR_EMPR+LR_NRO INTO V_REC_HD
DO REVI_REC   && REVISION DE CONSISTENCIA ENTRE CABECERAS Y LINEAS, PARA RESOLVER BUG ANTERIOR DEL SISTEMA
SET RELATION TO
****

SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
SELECT 5
SELECT 6
SELECT 7
USE V_CORRE ORDER EMPRESA
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11

SELECT 15
* AREA RESERVADA PARA USO TEMPORAL DE CABEZAS DE REC. 
SELECT 16
* AREA RESERVADA PARA USO TEMPORAL DE LINEAS DE REC. 

SELECT 17
USE V_TC ORDER FECHA

SELECT  V_REC_HD
SET RELATION TO HR_EMPR INTO V_EMPR
SET RELATION TO HR_EMPR+HR_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HR_EMPR+HR_COD_VEN INTO V_VENDOR ADDITIVE

SELECT V_CLIENT

DEFINE POPUP GETCLIEN FROM 8,20 TO 20,60 PROMPT FIELD substr(CL_RAZON,1,30)+'-'+CL_COD_CL ;
 TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
ON SELECTION POPUP GETCLIEN DEACTIVATE POPUP

SELECT V_REC_HD

DEFINE WINDOW W_REC FROM 0,0 TO 24,79 DOUBLE
DEFINE WINDOW REC_hd FROM 0, 0 TO 9,79 TITLE " RECIBO DE CAJA " ;
 NOFLOAT DOUBLE 
DEFINE WINDOW REC_ln FROM 10, 0 TO 21,79 TITLE "D E T A L L E" ;
 NOFLOAT DOUBLE 
DEFINE WINDOW REC_MSG FROM 21, 0 TO 24,79 TITLE "MENSAJES" ;
 NOFLOAT DOUBLE 
DEFINE MENU M_REC 
DEFINE PAD PREV OF M_REC PROMPT '<- \<PREV'   AT 23,01 SKIP FOR BOF("V_REC_HD")
DEFINE PAD NEXT OF M_REC PROMPT '\<SIG ->'    AT 23,11 SKIP FOR EOF("V_REC_HD")
DEFINE PAD OJEAR OF M_REC PROMPT '\<OJEA'     AT 23,20
DEFINE PAD BUSCA OF M_REC PROMPT '\<BUSCA'    AT 23,27
DEFINE PAD MIRA  OF M_REC PROMPT '\<VERIFICA'    AT 23,35
DEFINE PAD ALTA  OF M_REC PROMPT '\<ALTA'     AT 23,46
DEFINE PAD ANULA OF M_REC PROMPT 'AN\<UL'     AT 23,53
DEFINE PAD OPCION OF M_REC PROMPT 'OP\<CIONES'  AT 23,60
DEFINE PAD SALE  OF M_REC PROMPT 'SA\<LIR '    AT 23,71

*          1         2         3         4         5         6         7      
*01234567890123456789012345678901234567890123456789012345678901234567890123456789
*ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป
*บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ ANUL บ OPCIONES บ SALIR  บ
*ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ

ON SELECTION PAD PREV   OF M_REC DO REC_PREV
ON SELECTION PAD NEXT   OF M_REC DO REC_NEXT
ON SELECTION PAD OJEAR  OF M_REC DO REC_OJEAR
ON SELECTION PAD BUSCA  OF M_REC DO REC_BUSCA
ON SELECTION PAD MIRA   OF M_REC DO REC_MIRA
ON SELECTION PAD ALTA   OF M_REC DO REC_ALTA
ON SELECTION PAD ANULA  OF M_REC DO REC_ANULA
ON SELECTION PAD OPCION OF M_REC DO V_REC_OP
ON SELECTION PAD SALE   OF M_REC DO REC_SALE
RETURN

PROCEDURE REC_VER
********************
SELECT V_REC_HD
DO V_REC_V.SPR
ACTIVATE SCREEN
CLEAR
@ 22,0 SAY "ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป"
@ 23,0 SAY "บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ BAJA บ OPCIONES บ SALIR  บ"
@ 24,0 SAY "ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ"
SELECT V_REC_LN

BROWSE KEY V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO  ;
       FIELDS LR_DESCR:H='CONCEPTO',LR_DOC:H='DOCUMENTO',LR_MONTO:H='MONTO Bs.':P='9,999,999.99':W=.F. WINDOW REC_LN  ;
       NOEDIT noclear NOAPPEND nowait
USE
USE V_REC_LN order CORRE
RETURN

PROCEDURE REC_MIRA
*****************
SELECT V_REC_LN
DEFINE WINDOW REC_VERI ;
		FROM 9, 0 ;
		TO 24,79 ;
		DOUBLE 
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO  ;
       FIELDS LR_DESCR:H='CONCEPTO',LR_DOC:H='DOCUMENTO',LR_MONTO:H='MONTO Bs.':P='9,999,999.99':W=.F. ;
       WINDOW REC_VERI  ;
       NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  TITLE 'DETALLE  -  [F10] PARA SALIR'
RELEASE WINDOW REC_VERI
DO V_ONKEYS
DO REC_VER
RETURN

PROCEDURE REC_NEXT
*****************
SELECT V_REC_HD
IF .NOT. EOF()
   SKIP
ENDIF 
DO REC_VER
RETURN

PROCEDURE REC_PREV
*****************
SELECT V_REC_HD
IF .NOT. BOF()
   SKIP -1
ENDIF 
DO REC_VER
RETURN

PROCEDURE REC_OJEAR
******************
SELECT V_REC_HD
DO V_IR_REG.SPR
ON KEY LABEL F10 KEYBOARD CHR(23)
ON KEY LABEL F12 DO V_BEXPOR WITH "HR_NRO, HR_FECHA, HR_COD_CL, HR_TOT_BS, HR_ESTADO, V_CLIENT.CL_RAZON", ".T."
BROWSE KEY EMPRESA FIELDS HR_NRO:H='NRO.',HR_FECHA:H='FECHA',HR_COD_CL:H='COD.CLIE.',HR_TOT_BS:H='MONTO BS.',HR_ESTADO:H='ESTADO',V_CLIENT.CL_RAZON:H="NOMBRE CLIENTE" ;
	   NOEDIT NODELETE WINDOW W_REC ;
       TITLE 'SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO' 
DO V_ONKEYS 
DO REC_VER
RETURN

PROCEDURE REC_ANULA
*******************
IF .NOT. V_USRAUT(USER,"RECIBOS",3) 
    RETURN
ENDIF

SELECT V_REC_HD

DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTE RECIBO ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

IF SINO='S'
  SELECT V_REC_HD
  IF LOCK()
    REPLACE HR_ESTADO WITH 'A', ;
            HR_TOT_BS with 0, ;
            HR_BANCO1 WITH SPACE(10), HR_CHE1 WITH SPACE(10), ;
            HR_BANCO2 WITH SPACE(10), HR_CHE2 WITH SPACE(10), ;
            HR_BANCO3 WITH SPACE(10), HR_CHE3 WITH SPACE(10), ;
            HR_COD_CL WITH SPACE(8) , HR_COD_VEN WITH SPACE(3)
    REPLACE HR_USUARIO WITH USER, HR_MODI WITH DATE()

    SELECT V_REC_LN
    SEEK V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO
    IF .NOT. EOF() .AND. LOCK()
      REPLACE LR_ESTADO WITH 'A', ;
              LR_DESCR  WITH '** ANULADO **' ;
              LR_MONTO  WITH 0 
      SKIP             
      DELE REST WHILE V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO=LR_EMPR+LR_NRO FOR LOCK()
    ENDIF     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! EL RECIBO HA SIDO ANULADO !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
DO REC_VER  
RETURN

PROCEDURE REC_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_REC
RELEASE WINDOW REC_HD
RELEASE WINDOW REC_LN
RELEASE WINDOW REC_MSG
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE REC_ALTA
*****************
IF .NOT. V_USRAUT(USER,"RECIBOS",2) 
    RETURN
ENDIF

SELECT V_REC_HD
REG_ACTUAL=RECNO()

APPEND BLANK
IF LOCK()
  REPLACE HR_EMPR WITH EMPRESA
  REPLACE HR_FECHA WITH DATE()
  REPLACE HR_USUARIO WITH USER
ENDIF
          
TIPO_REC=1
OKCANCEL=1

DO V_REC_M.SPR

IF OKCANCEL=2 .OR. LASTKEY()=27
  REPLACE HR_EMPR WITH 'K'  && Si se cancela
  REPLACE HR_NRO WITH '000000'
  DELETE
  GO REG_ACTUAL
  DO V_REC_V.SPR
  RETURN
ENDIF

REPLACE HR_NRO WITH V_NEXT_N(EMPRESA,'REC')

IF TIPO_REC=1
  REPLACE HR_TIPO WITH 'E'
ELSE
  REPLACE HR_TIPO WITH 'C'
ENDIF

DO V_REC_V.SPR
        
SELECT V_REC_LN
DEFINE WINDOW REC_ADD FROM 9, 0	TO 24,79 DOUBLE 

FOR N=1 TO NROLINEAS
APPEND BLANK
IF LOCK()
 REPLACE LR_EMPR   WITH V_REC_HD.HR_EMPR
 REPLACE LR_NRO    WITH V_REC_HD.HR_NRO
 REPLACE LR_FECHA  WITH V_REC_HD.HR_FECHA
 REPLACE LR_COD_CL WITH V_REC_HD.HR_COD_CL
ENDIF            
ENDFOR
GO TOP
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO  ;
 FIELDS LR_DESCR:H='CONCEPTO',LR_DOC:H='DOCUMENTO',LR_MONTO:H='MONTO Bs.':P='9999999.99' ;
 WINDOW REC_ADD TITLE 'DETALLE  -  [F10] PARA SALIR'

SEEK V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO
DELE REST WHILE .NOT. EOF() .AND. V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO=LR_EMPR+LR_NRO ;
    FOR LR_MONTO=0

SEEK V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO
SUM REST WHILE .NOT. EOF() .AND. V_REC_HD.HR_EMPR+V_REC_HD.HR_NRO=LR_EMPR+LR_NRO ;
    LR_MONTO TO TOTAL_BS

GO TOP
        
SELECT V_REC_HD
IF LOCK()
  REPLACE HR_TOT_BS WITH TOTAL_BS
ENDIF

RELEASE WINDOW REC_ADD
DO V_ONKEYS
DO REC_VER
RETURN

FUNCTION CHK_VENDOR
*******************
PARAMETER CODIGO
SELECT V_VENDOR
SEEK EMPRESA+CODIGO
IF FOUND()
  SELECT V_REC_HD
  RETURN .T.
ELSE
  SELECT V_REC_HD
  RETURN .F.  
ENDIF    

PROCEDURE REC_CORR
******************
CURRENT=SELECT()
SELECT V_CORRE
USE V_CORRE ORDER EMPRESA
SEEK EMPRESA
IF EOF()
  SELECT (CURRENT)
  RETURN 
ENDIF
  
PROXIMO=VAL(CO_REC)+1
PROXIMO=RIGHT('00000'+ALLTRIM(STR(PROXIMO)),6)
OPCION=1

DO WHILE .T.
  DO V_REC_MC.SPR
  DO CASE
    CASE OPCION=1
      EXIT
    CASE OPCION=2
     PROXIMO=VAL(PROXIMO)+1
     PROXIMO=RIGHT('00000'+ALLTRIM(STR(PROXIMO)),6)
    CASE OPCION=3
     PROXIMO=VAL(PROXIMO)-1
     PROXIMO=RIGHT('00000'+ALLTRIM(STR(PROXIMO)),6)
  ENDCASE
  IF OPCION=1
    EXIT
  ENDIF
ENDDO

PROXIMO=VAL(PROXIMO)-1
PROXIMO=RIGHT('00000'+ALLTRIM(STR(PROXIMO)),6)

IF LOCK()
   REPLACE CO_REC WITH PROXIMO
ENDIF
UNLOCK
FLUSH
USE
USE V_CORRE ORDER EMPRESA
SELECT (CURRENT)
RETURN PROXIMO

PROCEDURE REVI_REC
******************
*REVISA CONSISTECIA ENTRE CABECERAS Y LINEAS
SELECT V_REC_LN
GO TOP
DO WHILE .NOT. EOF()
  IF LR_FECHA<>V_REC_HD.HR_FECHA .OR. LR_COD_CL<>V_REC_HD.HR_COD_CL .OR. LR_ESTADO<>V_REC_HD.HR_ESTADO
    REPLACE LR_ESTADO WITH V_REC_HD.HR_ESTADO
    REPLACE LR_FECHA  WITH V_REC_HD.HR_FECHA
    REPLACE LR_COD_CL WITH V_REC_HD.HR_COD_CL 
  ENDIF
  SKIP
ENDDO
GO TOP
RETURN

PROCEDURE REC_BUSCA
******************
DEFINE WINDOW REC_BUSCA FROM 10,10 TO 19,70 DOUBLE TITLE 'BUSQUEDA DE N.R.' 

ACTIVATE WINDOW REC_BUSCA
SELECT V_REC_HD 
@ 1,1 GET choice FUNCTION '*RNV POR \<NRO;POR COD. \<CLIENTE;POR \<RAZON SOCIAL' ;
	SIZE 1,10,1 DEFAULT 1 && COLOR B/G

MI_REC='      '
MI_CLI='        '
MI_RSOC=SPACE(30)
@ 1,22 SAY ':' GET MI_REC  PICTURE '999999'   WHEN CHOICE=1
@ 3,22 SAY ':' GET MI_CLI  PICTURE '99999999' WHEN CHOICE=2
@ 5,22 SAY ':' GET MI_RSOC PICTURE REPLICATE("X",30) WHEN CHOICE=3

@ 7,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G
READ CYCLE

SELECT V_REC_HD
IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'
  ON KEY LABEL F12 DO V_BEXPOR WITH "HR_NRO, HR_FECHA, HR_COD_CL, HR_TOT_BS, HR_ESTADO, V_CLIENT.CL_RAZON", W_FILTRO
  ON KEY LABEL F10 KEYBOARD CHR(23)
  DO CASE
   CASE choice = 1
     W_FILTRO="ALLTRIM(MI_REC)$HR_NRO"
   CASE choice = 2
     W_FILTRO="ALLTRIM(MI_CLI)$HR_COD_CL"
   CASE choice = 3
     W_FILTRO="UPPER(ALLTRIM(MI_RSOC))$UPPER(V_CLIENT.CL_RAZON)"
  ENDCASE

  BROWSE NOEDIT FOR evaluate(w_filtro) ;
     FIELDS HR_NRO:H='NRO.',HR_FECHA:H='FECHA',HR_COD_CL:H='COD.CLIE.',V_CLIENT.CL_RAZON:H='RAZON SOCIAL CLIENTE':P='@S30', HR_TOT_BS:H='MONTO BS.',HR_ESTADO:H='ESTADO' ;
     WINDOW W_REC TITLE W_TITLE
ENDIF
RELEASE WINDOW REC_BUSCA
DO V_ONKEYS
DO REC_VER
RETURN
