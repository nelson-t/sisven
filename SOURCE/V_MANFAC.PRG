***************************************************************************
* MANTENIMIENTO DE FACTURAS

*IF NOT V_VALACC()
*  RETURN
*ENDIF  

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U"
  DO V_GETEMP
ENDIF

IF TYPE("USER")="U" OR TYPE("MACHINE")="U"
  PUBLIC USER, MACHINE
  USER   =v_getuin("U") 
  MACHINE=v_getuin("M")
ENDIF

IF V_GETNDO()=0
  RETURN
ENDIF

*                                          
DO SET_AMBI
DO SET_GRAL
IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF

DO V_ONKEYS

SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA
SELECT V_FAC_HD
SET FILTER TO HF_EMPR=EMPRESA .AND. HF_TIPO$'OR'
SEEK EMPRESA
DO FAC_VER
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_FAC
ENDDO
DO FIN
RETURN

******************
PROCEDURE SET_AMBI
******************
SET BLINK OFF
SET CLOCK ON
SET CONFIRM ON
SET EXCLU OFF
SET DATE TO DMY
SET DELE ON
SET TALK OFF
SET SAFETY OFF
SET ESCAPE OFF
SET EXACT OFF
RETURN

PROCEDURE SET_GRAL
******************
*MUESTRA PANTALLA INICIAL E INICIALIZA AMBIENTE CON 'SETUP'
SET CURSOR OFF
MENSAJE='ESPERE UN MOMENTO POR FAVOR !!'
CLEAR
DO V_FAC_CA.SPR
S_INI=SECONDS()
DO SETUP
S_FIN=SECONDS()
IF S_FIN-S_INI<2
  @ 23,0  
  WAIT '' TIMEOUT 2-(S_FIN-S_INI)
ENDIF
SET CURSOR ON
CLEAR
RETURN

PROCEDURE SETUP
***************
* INICIALIZA AMBIENTE
SELECT 1
USE V_FAC_HD ORDER  CORRE
SELECT 2
USE V_FAC_LN ORDER  CORRE
SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
USE V_ITEMS ORDER PART
SELECT 5
* 
SELECT 6
*
SELECT 7
USE V_CORRE ORDER EMPRESA
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
USE V_DESCTO ORDER PART
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11
USE V_CATEGO ORDER CATEGO

SELECT 15
USE V_DOC_HD ORDER NR
SELECT 16
USE V_DOC_LN ORDER NR

SELECT 17
USE V_DOSIFI ORDER NRO

SELECT V_FAC_LN
SET RELATION TO LF_EMPR+LF_PART INTO V_ITEMS

SELECT  V_FAC_HD
SET RELATION TO HF_EMPR INTO V_EMPR
SET RELATION TO HF_EMPR+HF_COD_CL  INTO V_CLIENT  ADDITIVE
SET RELATION TO HF_EMPR+HF_COD_VEN INTO V_VENDOR  ADDITIVE
SET RELATION TO HF_EMPR+STR(HF_NDOSIF,4,0) INTO V_DOSIFI ADDITIVE

SELECT V_ITEMS

DEFINE POPUP GETITEM FROM 8,15 TO 20,70 ;
	PROMPT FIELD IT_PART+'-'+IT_DESCR ;
	TITLE 'SELECCIONE ITEM CON [ENTER]' 
ON SELECTION POPUP GETITEM DEACTIVATE POPUP

SELECT V_FAC_HD

DEFINE WINDOW W_FAC FROM 0,0 TO 24,79 DOUBLE

DEFINE WINDOW FAC_hd FROM 0,0 TO 8,79 TITLE " F A C T U R A S " NOFLOAT DOUBLE

DEFINE WINDOW FAC_ln FROM 9,0 TO 21,79 TITLE "D E T A L L E" NOFLOAT DOUBLE 
		
DEFINE WINDOW FAC_MSG FROM 21,0 TO 24,79 TITLE "MENSAJES" NOFLOAT DOUBLE
		
DEFINE MENU M_FAC 

DEFINE PAD PREV   OF M_FAC PROMPT '<- \<PREV'   AT 23,01 SKIP FOR BOF("V_FAC_HD")
DEFINE PAD NEXT   OF M_FAC PROMPT '\<SIG ->'    AT 23,11 SKIP FOR EOF("V_FAC_HD")
DEFINE PAD OJEAR  OF M_FAC PROMPT '\<OJEA'      AT 23,20
DEFINE PAD BUSCA  OF M_FAC PROMPT '\<BUSCA'     AT 23,27
DEFINE PAD MIRA   OF M_FAC PROMPT '\<VERIFICA'  AT 23,35
DEFINE PAD ALTA   OF M_FAC PROMPT '\<ALTA'      AT 23,46 
DEFINE PAD ANULA  OF M_FAC PROMPT 'AN\<UL'      AT 23,53
DEFINE PAD OPCION OF M_FAC PROMPT 'OP\<CIONES'  AT 23,60
DEFINE PAD SALE   OF M_FAC PROMPT 'SA\<LIR '    AT 23,71

*          1         2         3         4         5         6         7      
*01234567890123456789012345678901234567890123456789012345678901234567890123456789
*ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป
*บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ ANUL บ OPCIONES บ SALIR  บ
*ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ

ON SELECTION PAD PREV   OF M_FAC DO FAC_PREV
ON SELECTION PAD NEXT   OF M_FAC DO FAC_NEXT
ON SELECTION PAD OJEAR  OF M_FAC DO FAC_OJEAR
ON SELECTION PAD BUSCA  OF M_FAC DO FAC_BUSCA
ON SELECTION PAD MIRA   OF M_FAC DO FAC_MIRA
ON SELECTION PAD ALTA   OF M_FAC DO FAC_ALTA
ON SELECTION PAD ANULA  OF M_FAC DO FAC_ANULA
ON SELECTION PAD OPCION OF M_FAC DO V_FAC_OP
ON SELECTION PAD SALE   OF M_FAC DO FAC_SALE
RETURN

PROCEDURE FAC_VER
********************
ALMACEN =SPACE(20)
SELECT V_FAC_HD
DO GET_ALM    WITH HF_ALM
SELECT V_FAC_HD
DO V_FAC_V.SPR
ACTIVATE SCREEN
@ 22,0 SAY "ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป"
@ 23,0 SAY "บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ BAJA บ OPCIONES บ SALIR  บ"
@ 24,0 SAY "ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ"
SELECT V_FAC_LN
GO TOP
KEYBOARD CHR(27)
BROWSE KEY V_FAC_HD.HF_EMPR+'F'+STR(V_FAC_HD.HF_NDOSIF,4)+V_FAC_HD.HF_NRO  ;
       FIELDS LF_PART:H='PART.#':W=.F.,V_ITEMS.IT_DESCR:H='DESCRIPCION',LF_CANT:H='CANTIDAD':P='999,999.99':W=.F.,V_ITEMS.IT_UNIDAD:H='UNI.',IMPORTE=ROUND(LF_PRECIO*LF_CANT,2):P='9,999,999.99',LF_PRECIO:H='PRECIO':p='999,999.9999',LF_ESTADO:H='ESTADO':W=.F. WINDOW FAC_LN  ;
       NOEDIT NOCLEAR COLOR SCHEME 10

RETURN

PROCEDURE FAC_MIRA
*****************
SELECT V_FAC_LN
DEFINE WINDOW FAC_VERI FROM 9, 0 TO 24,79 DOUBLE 
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_FAC_HD.HF_EMPR+'F'+STR(V_FAC_HD.HF_NDOSIF,4)+V_FAC_HD.HF_NRO  ;
 FIELDS LF_PART:H='PART.#',V_ITEMS.IT_DESCR:H='DESCRIPCION',LF_CANT:H='CANTIDAD':P='999,999.99',V_ITEMS.IT_UNIDAD:H='UNI.',LF_PRECIO:H='PRECIO':P='999,999.9999',IMPORTE=ROUND(LF_PRECIO*LF_CANT,2):P='9,999,999.99',LF_ESTADO:H='ESTADO' WINDOW FAC_VERI  ;
 NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  TITLE 'DETALLE  -  [F10] PARA SALIR' ;
 COLOR SCHEME 10
RELEASE WINDOW FAC_VERI
DO V_ONKEYS
DO FAC_VER
RETURN

PROCEDURE FAC_NEXT
*****************
SELECT V_FAC_HD
TRECNO=RECNO()
SKIP
IF EOF()
   GO TRECNO
ENDIF 
DO FAC_VER
RETURN

PROCEDURE FAC_PREV
*****************
SELECT V_FAC_HD
IF .NOT. BOF()
   SKIP -1
ENDIF 
DO FAC_VER
RETURN

PROCEDURE FAC_MODI
*****************
RETURN

PROCEDURE FAC_ANULA
******************
IF .NOT. V_USRAUT(USER,"FACTURAS",3)
  RETURN
ENDIF

SELECT V_FAC_HD
DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTA FACTURA ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

NRO_PED=SPACE(6)
NRO_EMPR=' '

DO V_FAC_AR  && ANULA REMISION INTERNA / REPONE REMISIONES 

SELECT V_FAC_HD
IF HF_TIPO='O'
  DO V_FAC_AP  WITH NRO_EMPR,NRO_PED && ANULA PEDIDO
ENDIF

SELECT V_FAC_HD
IF SINO='S'
  IF LOCK()
    REPLACE HF_ESTADO WITH 'A', ;
            HF_COD_CL WITH SPACE(8), ;
            HF_NOM_CL WITH SPACE(30), ;
            HF_RUC    WITH SPACE(15), ;
            HF_OBS    WITH '* * * A N U L A D A', ;
            HF_PEDIDO WITH SPACE(6), ;
            HF_NR     WITH SPACE(6), ;
            HF_IMPORTE WITH 0, ;
            HF_DSCTO_P WITH 0, ;
            HF_DSCTO_M WITH 0, ;
            HF_FLETES  WITH 0, ;
            HF_EMBALA  WITH 0, ;
            HF_OTROS   WITH 0, ;
            HF_FACTOR  WITH 0, ;
            HF_USUARIO WITH USER

    SELECT V_FAC_LN
    SEEK V_FAC_HD.HF_EMPR+'F'+STR(V_FAC_HD.HF_NDOSIF,4)+V_FAC_HD.HF_NRO
    IF .NOT. EOF() .AND. LOCK()
      REPLACE LF_ESTADO WITH 'A', ;
              LF_PART WITH SPACE(15), ;
              LF_COD_CL WITH SPACE(8), ;
              LF_CANT WITH 0, ;
              LF_PRECIO WITH 0, ;
              LF_DESCTOS WITH 0
      SKIP
      DO WHILE .NOT. EOF() .AND. V_FAC_HD.HF_TIPO$"RO" .AND. ;
               V_FAC_HD.HF_EMPR+STR(V_FAC_HD.HF_NDOSIF,4)+V_FAC_HD.HF_NRO=LF_EMPR+STR(LF_NDOSIF,4)+LF_NRO             
        IF LOCK()
          DELETE
        ENDIF
        SKIP
      ENDDO 
    ENDIF     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! LA FACTURA HA SIDO ANULADA !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
  DO FAC_VER  
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
RETURN

PROCEDURE FAC_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_FAC
RELEASE WINDOW FAC_HD
RELEASE WINDOW FAC_LN
RELEASE WINDOW FAC_MSG
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

*-----------------*
FUNCTION GET_ALM
*-----------------*
PARAMETER ALM
SELECT 0 
USE V_ALM ORDER ALM
SEEK EMPRESA+ALM
IF FOUND()
  ALMACEN=SUBSTR(AL_DESCR,1,20)
  USE
ELSE
  ALMACEN='ALMACEN NO EXISTE'
  USE
ENDIF    


PROCEDURE FAC_ALTA
*****************
MENSAJE1='LAS FACTURAS SE GENERAN DESDE LAS NOTAS DE REMISION' 
MENSAJE2='PRIMERO EL PEDIDO, LUEGO LA NR Y PROCEDA A FACTURARLA'
DO V_MENSAJ.SPR
RETURN

*-----------------*
FUNCTION CHK_ALM
*-----------------*
PARAMETER ALM
SELECT 0 
USE V_ALM ORDER ALM
SEEK EMPRESA+ALM
IF FOUND()
  ALMACEN=SUBSTR(AL_DESCR,1,20)
  @ 3,14 SAY '-'+SUBSTR(AL_DESCR,1,20)
  USE
  SELECT V_FAC_HD
  RETURN .T.
ELSE
  @ 3,14 SAY 'ALMACEN NO EXISTE'
  USE
  SELECT V_FAC_HD
  RETURN .F.  
ENDIF    

FUNCTION CHK_CLIENT
*******************
PARAMETER CODIGO
CURRENT=SELECT()
SELECT V_CLIENT
SEEK EMPRESA+CODIGO
IF FOUND()
  @ 3,38 SAY substr(CL_RAZON,1,30)
  SELECT (CURRENT)
  RETURN .T.
ELSE
  *-  DEFINIR UN POPUP DE CLIENTES -*
  SELECT V_CLIENT
  SET ORDER TO RAZON
  DEFINE POPUP GETCL FROM 4,25 TO 20,65 ;
	PROMPT FIELD substr(CL_RAZON,1,30)+'-'+CL_COD_CL ;
	TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
  ON SELECTION POPUP GETCL DEACTIVATE POPUP
  ACTIVATE POPUP GETCL
  NOM_CL=SUBSTR(PROMPT(),1,30)
  MI_CLI=SUBSTR(PROMPT(),32,8)
  SET ORDER TO CODIGO

  SELECT (CURRENT)
  @ 3,38 SAY NOM_CL
  RELEASE POPUP GETCL
  RETURN .T.
ENDIF    

PROCEDURE FAC_BUSCA
******************
DEFINE WINDOW FAC_BUSCA FROM 06,5 TO 17,75 DOUBLE TITLE 'BUSQUEDA DE FACTURA' 
ACTIVATE WINDOW FAC_BUSCA
SELECT V_FAC_HD 
@ 1,1 GET choice FUNCTION '*RNV POR \<NRO;POR \<CODIGO;POR \<NIT;POR \<RAZON S.' ;
	SIZE 1,10,1 DEFAULT 1 && COLOR B/G

* VARIBLES PARA BUSQUEDAS                                          
MI_NIT=SPACE(16)
MI_DOSIFI=0
MI_FAC='      '
MI_CLI='        '
MI_RAZON=space(30)

@ 1,25 SAY '#DOSIF.:' GET MI_DOSIFI PICTURE '9999'  WHEN CHOICE=1
@ 1,40 SAY '#FACTURA :' GET MI_FAC PICTURE '999999' WHEN CHOICE=1

@ 3,25 GET MI_CLI   WHEN CHOICE=2 VALID CHK_CLIENT(MI_CLI)
@ 5,25 GET MI_NIT   WHEN CHOICE=3  
@ 7,25 GET MI_RAZON WHEN CHOICE=4
@ 9,25 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G
READ CYCLE

IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'
  DO CASE
    CASE choice = 1
       W_FILTRO="HF_TIPO$'RO' .AND. HF_NDOSIF=MI_DOSIFI .AND. ALLTRIM(MI_FAC)$HF_NRO"
    CASE choice = 2
       W_FILTRO="HF_TIPO$'RO' .AND. ALLTRIM(MI_CLI)$HF_COD_CL" 
    CASE choice = 3
       W_FILTRO="HF_TIPO$'RO' .AND. ALLTRIM(MI_NIT)$V_CLIENT.CL_RUC"
    CASE choice = 4
       W_FILTRO="HF_TIPO$'RO' .AND. (UPPER(ALLTRIM(MI_RAZON))$UPPER(HF_NOM_CL) .OR. UPPER(ALLTRIM(MI_RAZON))$UPPER(HF_OBS))"
  ENDCASE
  
  ON KEY LABEL F10 KEYBOARD CHR(23)
  ON KEY LABEL F12 DO V_BEXPOR WITH "HF_NDOSIF, HF_TIPO, HF_NRO, HF_FECHA, HF_PEDIDO, HF_NR, HF_ALM, HF_COD_CL, HF_OBS, HF_NOM_CL, FLETES_OTROS(), DESCUENTOS(), IMPORTE_BS(), HF_ESTADO", W_FILTRO
  SELECT V_FAC_HD
  BROWSE NOEDIT FOR EVAL(W_FILTRO) ;
     FIELDS HF_NDOSIF:H='#DOSIF',NRO=HF_NRO+' C'+HF_TIPO:H='NRO.',HF_ALM:H='ALM',HF_FECHA:H='FECHA',HF_COD_CL:H='COD.CLIE.',V_CLIENT.CL_RUC:H="NIT":16,HF_NOM_CL:H="NOMBRE FACTURA", HF_OBS:H='OBSERVACIONES':P='@S30',FLE=FLETES_OTROS():13:H='FLETES Y OTROS', DES=DESCUENTOS():13:H='DESCUENTOS', IMP=IMPORTE_BS():13:H='IMPORTE BS', HF_ESTADO:H='ESTADO' ;
     WINDOW W_FAC TITLE W_TITLE
  IF EOF()
    go bott
  ENDIF

ENDIF

RELEASE WINDOW FAC_BUSCA
DO V_ONKEYS
DO FAC_VER
RETURN

PROCEDURE FAC_OJEAR
******************
SELECT V_FAC_HD
DO V_IR_REG.SPR
W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'
W_FILTRO=".T."
ON KEY LABEL F10 KEYBOARD CHR(23)
ON KEY LABEL F12 DO V_BEXPOR WITH "HF_NDOSIF, HF_TIPO, HF_NRO, HF_FECHA, HF_PEDIDO, HF_NR, HF_ALM, HF_COD_CL, HF_OBS, HF_NOM_CL, FLETES_OTROS(), DESCUENTOS(), IMPORTE_BS(), HF_ESTADO", W_FILTRO
BROWSE KEY EMPRESA FIELDS HF_NDOSIF:H="#DOSIF",NRO=HF_NRO+' C'+HF_TIPO:H='NRO.',HF_FECHA:H='FECHA',HF_PEDIDO:H='PEDIDO #', HF_NR:H='N.R.#', HF_ALM:H='ALM', HF_COD_CL:H='COD.CLIE.',HF_OBS:H='OBSERVACIONES':P='@S30',HF_NOM_CL:H='NOMBRE EN LA FACTURA', ;
    FLE=FLETES_OTROS():13:H='FLETES Y OTROS', DES=DESCUENTOS():13:H='DESCUENTOS', IMP=IMPORTE_BS():13:H='IMPORTE BS', ;
	V_DOSIFI.NRO_AUT:H="# AUTORIZ.":15, HF_ESTADO:H='ESTADO' NOEDIT WINDOW W_FAC ;
	TITLE W_TITLE ;
    COLOR SCHEME 10 NODELETE
ON KEY
DO V_ONKEYS
DO FAC_VER
RETURN

FUNCTION IMPORTE_BS
*******************
RETURN v_fac_hd.hf_importe-ROUND((v_fac_hd.hf_importe*v_fac_hd.hf_dscto_p)/100,2)-v_fac_hd.hf_dscto_m+V_FAC_HD.HF_FLETES+V_FAC_HD.HF_EMBALA+V_FAC_HD.HF_OTROS

FUNCTION DESCUENTOS
*******************
RETURN ROUND((v_fac_hd.hf_importe*v_fac_hd.hf_dscto_p)/100,2)-v_fac_hd.hf_dscto_m

FUNCTION FLETES_OTROS
********************
RETURN V_FAC_HD.HF_FLETES+V_FAC_HD.HF_EMBALA+V_FAC_HD.HF_OTROS
