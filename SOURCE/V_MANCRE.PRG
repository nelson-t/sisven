***************************************************************************
* MANTENIMIENTO DE NOTAS DE CREDITO

*DEBE SER LLAMADO DESDE MAIN (V_MUMAIN.PRG)
*IF NOT V_VALACC()
*  RETURN
*ENDIF  

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U"
  DO V_GETEMP
ENDIF

IF TYPE("USER")="U" OR TYPE("MACHINE")="U"
  PUBLIC USER, MACHINE
  USER   =v_getuin("U") 
  MACHINE=v_getuin("M")
ENDIF

IF .NOT. V_USRAUT(USER,"NOTAS_CR",1)  
   RETURN
ENDIF

DO SET_AMBI
DO SET_GRAL
IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF
DO V_ONKEYS

SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA
SELECT V_FAC_HD
SET FILTER TO HF_EMPR=EMPRESA .AND. HF_TIPO$'XY' && NOTA DE CREDITO,DEBITO
SEEK EMPRESA
DO REC_VER
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_REC
ENDDO

DO FIN

RETURN

*******************************************************************************
PROCEDURE SET_GRAL
******************
*MUESTRA PANTALLA INICIAL E INICIALIZA AMBIENTE CON 'SETUP'
SET CURSOR OFF
MENSAJE='ESPERE UN MOMENTO POR FAVOR !!'
CLEAR
DO V_CRE_CA.SPR
S_INI=SECONDS()
DO SETUP
S_FIN=SECONDS()
IF S_FIN-S_INI<2
  @ 23,0  
  WAIT '' TIMEOUT 2-(S_FIN-S_INI)
ENDIF
SET CURSOR ON
CLEAR
RETURN

PROCEDURE SETUP
***************
* INICIALIZA AMBIENTE
SELECT 1
USE V_FAC_HD ORDER CORRE
SELECT 2
USE
SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
SELECT 5
SELECT 6
SELECT 7
USE V_CORRE ORDER EMPRESA
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11

SELECT 15
* AREA RESERVADA PARA USO TEMPORAL DE CABEZAS DE FACT. 
SELECT 16
* AREA RESERVADA PARA USO TEMPORAL DE LINEAS DE FACT. 

SELECT 17
USE V_TC ORDER FECHA

SELECT  V_FAC_HD
SET RELATION TO HF_EMPR INTO V_EMPR
SET RELATION TO HF_EMPR+HF_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HF_EMPR+HF_COD_VEN INTO V_VENDOR ADDITIVE

SELECT V_CLIENT

DEFINE POPUP GETCLIEN FROM 8,20 TO 20,70 ;
	PROMPT FIELD substr(CL_RAZON,1,30)+'-'+CL_COD_CL ;
	TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
ON SELECTION POPUP GETCLIEN DEACTIVATE POPUP

SELECT V_FAC_HD

DEFINE WINDOW W_REC FROM 0,0 TO 24,79 DOUBLE

DEFINE WINDOW CRE_hd ;
		FROM 0, 0 ;
		TO 17,79 ;
		TITLE " NOTAS / DEUDORES " ;
		NOFLOAT ;
		DOUBLE 

DEFINE WINDOW REC_MSG ;
		FROM 21, 0 ;
		TO 24,79 ;
		TITLE "MENSAJES" ;
		NOFLOAT ;
		DOUBLE 

DEFINE MENU M_REC 

DEFINE PAD PREV  OF M_REC PROMPT '<- \<PREV'   AT 23,01 SKIP FOR BOF("V_FAC_HD")
DEFINE PAD NEXT  OF M_REC PROMPT '\<SIG ->'    AT 23,11 SKIP FOR EOF("V_FAC_HD")
DEFINE PAD OJEAR OF M_REC PROMPT '\<OJEA'      AT 23,20
DEFINE PAD BUSCA OF M_REC PROMPT '\<BUSCA'     AT 23,27
DEFINE PAD ALTA  OF M_REC PROMPT '\<ALTA'      AT 23,35
DEFINE PAD ANULA OF M_REC PROMPT 'AN\<ULA'     AT 23,42
DEFINE PAD MODI  OF M_REC PROMPT '\<MODIFICA'  AT 23,50
DEFINE PAD IMPRE OF M_REC PROMPT '\<IMPRIME'   AT 23,61
DEFINE PAD SALE  OF M_REC PROMPT 'SA\<LIR'     AT 23,71

*           1         2         3         4         5         6         7      
*  123456789012345678901234567890123456789012345678901234567890123456789012345678
*ษอออออออออหออออออออหออออออหอออออออหออออออหอออออออหออออออออออหอออออออออหอออออออป
*บ <- PREV บ SIG -> บ OJEA บ BUSCA บ ALTA บ ANULA บ MODIFICA บ IMPRIME บ SALIR บ
*ศอออออออออสออออออออสออออออสอออออออสออออออสอออออออสออออออออออสอออออออออสอออออออผ

ON SELECTION PAD PREV   OF M_REC DO REC_PREV
ON SELECTION PAD NEXT   OF M_REC DO REC_NEXT
ON SELECTION PAD OJEAR  OF M_REC DO REC_OJEAR
ON SELECTION PAD BUSCA  OF M_REC DO REC_BUSCA
ON SELECTION PAD ALTA   OF M_REC DO REC_ALTA
ON SELECTION PAD ANULA  OF M_REC DO REC_ANULA
ON SELECTION PAD MODI   OF M_REC DO REC_MODI
ON SELECTION PAD IMPRE  OF M_REC DO REC_IMPRE
ON SELECTION PAD SALE   OF M_REC DO REC_SALE
RETURN

PROCEDURE REC_VER
********************
SELECT V_FAC_HD
DO V_CRE_V.SPR
ACTIVATE SCREEN
CLEAR
@ 22,0 SAY "ษอออออออออหออออออออหออออออหอออออออหออออออหอออออออหออออออออออหอออออออออหอออออออป"
@ 23,0 SAY "บ <- PREV บ SIG -> บ OJEA บ BUSCA บ ALTA บ ANULA บ MODIFICA บ IMPRIME บ SALIR บ"
@ 24,0 SAY "ศอออออออออสออออออออสออออออสอออออออสออออออสอออออออสออออออออออสอออออออออสอออออออผ"
RETURN

PROCEDURE REC_NEXT
*****************
SELECT V_FAC_HD
IF .NOT. EOF()
   SKIP
ENDIF 
DO REC_VER
RETURN

PROCEDURE REC_PREV
*****************
SELECT V_FAC_HD
IF .NOT. BOF()
   SKIP -1
ENDIF 
DO REC_VER
RETURN

PROCEDURE REC_OJEAR
******************
W_FILTRO=".T."
W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'

ON KEY LABEL F10 KEYBOARD CHR(23)
ON KEY LABEL F12 DO V_BEXPOR WITH "HF_TIPO, HF_NRO, HF_FECHA, HF_COD_CL, V_CLIENT.CL_RAZON, HF_MONEDA, HF_OBS, HF_IMPORTE, HF_ESTADO", W_FILTRO

SELECT V_FAC_HD
BROWSE KEY EMPRESA FIELDS TIPO=IIF(HF_TIPO='X','N.Cr.',IIF(HF_TIPO='Y','N.Db.','C.I.')):H='DOC.', ;
	   HF_NRO:H='NRO.',HF_FECHA:H='FECHA',HF_COD_CL:H='COD.CLIE.',hf_moneda:H="M",HF_IMPORTE:H='CREDITO',;
	   hf_obs:H="Observaciones", HF_ESTADO:H='ESTADO' ;
	   NOEDIT NODELETE WINDOW W_REC TITLE W_TITLE
DO V_ONKEYS 
DO REC_VER
RETURN

PROCEDURE REC_ANULA
*******************
IF .NOT. V_USRAUT(USER,"NOTAS_CR",3)  
   RETURN
ENDIF

SELECT V_FAC_HD

DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTE RECIBO ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

IF SINO='S'
  SELECT V_FAC_HD
  IF LOCK()
    REPLACE HF_ESTADO  WITH 'A', ;
            HF_IMPORTE with 0, ;
            HF_COD_CL WITH SPACE(8) , ;
            HF_OBS WITH "** ANULADO", ;
            HF_USUARIO WITH USER
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! LA NOTA DE CREDITO HA SIDO ANULADA !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
DO REC_VER  
RETURN

PROCEDURE REC_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_REC
RELEASE WINDOW CRE_HD
RELEASE WINDOW REC_MSG
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE REC_MODI
*****************
IF .NOT. V_USRAUT(USER,"NOTAS_CR",2)  
   RETURN
ENDIF

SELECT V_FAC_HD

IF LOCK()
  REPLACE HF_ESTADO  WITH 'M', ;
          HF_USUARIO WITH USER
ENDIF

DO V_CRE_M.SPR

DO V_CRE_V.SPR
        
DO V_ONKEYS
DO REC_VER
RETURN

PROCEDURE REC_ALTA
*****************
IF .NOT. V_USRAUT(USER,"NOTAS_CR",2)  
   RETURN
ENDIF

SELECT V_FAC_HD
REG_ACTUAL=RECNO()
q_moneda="Bolivianos"
TIPO=1

DO V_CRE_O.SPR

IF TIPO=1
  RETURN
ENDI
  
APPEND BLANK
IF LOCK()
  REPLACE HF_EMPR    WITH EMPRESA
  REPLACE HF_FECHA   WITH DATE()
  REPLACE HF_USUARIO WITH USER
ENDIF

OKCANCEL=1          

DO V_CRE_M.SPR

IF OKCANCEL=2 .OR. LASTKEY()=27
  REPLACE HF_EMPR WITH ' '  && Si se cancela
  REPLACE HF_TIPO WITH ' '
  DELETE
  GO TOP
  IF .NOT.EOF()
    GO REG_ACTUAL
  ENDIF
ELSE
 DO CASE
  CASE TIPO=2  && NOTA DE CREDITO
   REPLACE HF_NRO WITH V_NEXT_N(EMPRESA,'NC')
   REPLACE HF_TIPO WITH 'X'
   REPLACE HF_MONEDA WITH 'B'

  CASE TIPO=3  && NOTA DE DEBITO
   REPLACE HF_NRO WITH V_NEXT_N(EMPRESA,'ND')
   REPLACE HF_TIPO WITH 'Y'
   REPLACE HF_MONEDA WITH 'B'
  ENDCASE
ENDIF
DO V_CRE_V.SPR
DO V_ONKEYS
DO REC_VER
RETURN


PROCEDURE REC_BUSCA
******************
DEFINE WINDOW REC_BUSCA FROM 8,10 TO 19,70 DOUBLE TITLE 'BUSQUEDA DE N.R.' 
ACTIVATE WINDOW REC_BUSCA
SELECT V_FAC_HD 
MI_REC='      '
MI_CLI='        '
MI_RSOC=SPACE(30)
@ 1,10 GET TIPO FUNCTION '*RH NOTA CR.;NOTA DB.' DEFAULT 1 SIZE 1,10,1
@ 3,1  GET choice FUNCTION '*RNV POR \<NRO;POR COD \<CLIENTE;POR \<RAZON SOCIAL' ;
	   SIZE 1,10,1 DEFAULT 1 && COLOR B/G
@ 3,21 SAY ':' GET MI_REC  PICTURE '999999'   WHEN CHOICE=1
@ 5,21 SAY ':' GET MI_CLI  PICTURE '99999999' WHEN CHOICE=2
@ 7,21 SAY ':' GET MI_RSOC PICTURE REPLICATE("X",30) WHEN CHOICE=3

@ 9,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G
READ CYCLE
DO CASE
  CASE TIPO=1
    TIPO='X'
  CASE TIPO=2
    TIPO='Y'
ENDCASE  
SELECT V_FAC_HD
IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'
  ON KEY LABEL F12 DO V_BEXPOR WITH "HF_TIPO, HF_NRO, HF_FECHA, HF_COD_CL, V_CLIENT.CL_RAZON, HF_MONEDA, HF_OBS, HF_IMPORTE, HF_ESTADO", W_FILTRO
  ON KEY LABEL F10 KEYBOARD CHR(23)
  DO CASE
   CASE choice = 1
     W_FILTRO="HF_TIPO=TIPO .AND. ALLTRIM(MI_REC)$HF_NRO"
   CASE choice = 2
     W_FILTRO="HF_TIPO=TIPO .AND. ALLTRIM(MI_CLI)$HF_COD_CL"
   CASE choice = 3
     W_FILTRO="HF_TIPO=TIPO .AND. UPPER(ALLTRIM(MI_RSOC))$UPPER(V_CLIENT.CL_RAZON)"
  ENDCASE

  BROWSE NOEDIT FOR evaluate(w_filtro) ;
     FIELDS HF_NRO:H='NRO.',HF_FECHA:H='FECHA',HF_COD_CL:H='COD.CLIE.',V_CLIENT.CL_RAZON:H='RAZON SOCIAL CLIENTE':P='@S30',HF_IMPORTE:H='TOTAL CREDITO',HF_ESTADO:H='ESTADO'  ;
     WINDOW W_REC TITLE W_TITLE
ENDIF
RELEASE WINDOW REC_BUSCA
DO V_ONKEYS
DO REC_VER
RETURN

PROCEDURE SET_AMBI
******************
SET COLOR SET TO plasmar
SET BLINK OFF
SET CLOCK ON
SET CONFIRM ON
SET EXCLU OFF
SET DATE TO DMY
SET DELE ON
SET TALK OFF
SET SAFETY OFF
SET ESCAPE OFF
SET EXACT OFF
RETURN

PROCEDURE REC_IMPRE
*******************
*IMPRIME COPIAS DE LA REMISION

MENSAJE1='SE IMPRIMIRA LA NOTA DE CREDITO/DEBITO' 
MENSAJE2='[ENTER] PARA CONTINUAR, [ESC] PARA CANCELAR IMPRESION'
DO V_MENSAJ.SPR
IF LASTKEY()=27
  RETURN
ENDIF

SELECT V_FAC_HD
MI_REC=RECNO()
TEMP_FILE='..\TMP\'+SYS(3)+'.TXT'
_PADVANCE='LINEFEED'
REPORT FORM V_CRE NOCONSOLE NEXT 1 TO FILE (TEMP_FILE)
_PADVANCE='FORMFEED'
GO MI_REC

ACTIVATE SCREEN
SAVE SCREEN TO SC1
! COPY &TEMP_FILE lpt1:
RESTORE SCREEN FROM SC1
GO TOP

*modi comm (TEMP_FILE)
MENSAJE1='EL DOCUMENTO HA SIDO IMPRESO'
MENSAJE2=''
DO V_MENSAJ.SPR
DELETE FILE (TEMP_FILE)
RETURN
