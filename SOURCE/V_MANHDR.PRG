********************************
* MANTENIMIENTO DE HOJAS DE RUTA

NROLINEAS=V_GETPAR("HDR_NROLINEA")
IF NROLINEAS="*"
   NROLINEAS=35
ELSE
   NROLINEAS=val(NROLINEAS)
ENDIF    

DO V_INIMAN WITH "HOJAS_DR", 1

* SOLO PARA HDR
CLEAR
CODCAT=ALLTRIM(V_GETPAR("HDR_CODCHOF"))
IF CODCAT='*'
  WAIT "NO SE PUEDE CONTINUAR, DEBE INICIALIZAR COD. CHOFERS" WINDOW
  RETURN
ELSE
  WAIT "CHOFERS UTILIZAN CATEGORIA ["+codcat+"]" WINDOW TIMEOUT 1
ENDIF

DO V_SETUP WITH "V_HDR_CA.SPR", ""
DO LOC_SET

IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF

DO V_ONKEYS

SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA

SELECT V_HDR_HD
SET FILTER TO HH_EMPR=EMPRESA

SEEK EMPRESA
DO HDR_VER

STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_DOCS
ENDDO
DO FIN
RETURN

PROCEDURE LOC_SET
*****************
* INICIALIZA AMBIENTE
SELECT 1
USE V_HDR_HD ORDER CORRE
SELECT 2
USE V_HDR_LN ORDER CORRE 
SET RELATION TO LH_EMPR+LH_NRO INTO V_HDR_HD

SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
USE V_CORRE ORDER EMPRESA
SELECT 5
USE V_CLIENT ORDER CODIGO 
SELECT 6
USE V_VENDOR ORDER CODIGO
SELECT 7
USE V_TC ORDER FECHA
SELECT 8
USE V_DOC_HD ORDER NR
SET FILTER TO HD_EMPR=EMPRESA .AND. HD_TIPO="R" .AND. HD_ESTADO<>"A"
SELECT 9
USE V_DOC_LN ORDER NR

SELECT  V_HDR_HD
SET RELATION TO HH_EMPR 			INTO V_EMPR
SET RELATION TO HH_EMPR+HH_COD_CL  	INTO V_CLIENT ADDITIVE
SET RELATION TO HH_EMPR+HH_COD_VEN 	INTO V_VENDOR ADDITIVE

SELECT V_HDR_HD
DEFINE WINDOW W_HDR FROM 0,0 TO 24,79 DOUBLE
DEFINE WINDOW HDR_hd FROM 0, 0 TO 9,79 TITLE " HOJA DE RUTA " ;
 NOFLOAT DOUBLE 
DEFINE WINDOW HDR_ln FROM 10, 0 TO 21,79 TITLE "D E T A L L E" ;
 NOFLOAT DOUBLE 
DEFINE WINDOW HDR_MSG FROM 21, 0 TO 24,79 TITLE "MENSAJES" ;
 NOFLOAT DOUBLE 
 
*** Generamos Menu Principal
DO V_MANMU WITH .T.
 
RETURN

PROCEDURE HDR_VER
*****************
SELECT V_HDR_HD
DO V_HDR_V.SPR

***IMPRIME IMAGEN DEL MENU INFERIOR
DO V_MANMU WITH .F. 

SELECT V_HDR_LN
BROWSE KEY V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO  ;
       FIELDS LH_NR_NRO:H='NR.',LH_NR_CLI:H='CLIENTE':45,LH_NR_KG:H='KGS':P='999,999.99',LH_NR_IMPO:H='IMPORTE':P='9,999,999.99' WINDOW HDR_LN  ;
       NOEDIT noclear NOAPPEND nowait
USE
USE V_HDR_LN order CORRE
RETURN

PROCEDURE DOC_REVI
******************
SELECT V_HDR_HD
IF EOF()
  RETURN
ENDIF

IF HH_ESTADO$'AC' 
    MENSAJE1='LA HOJA DE RUTA NO PUEDE SER REVISADA' 
    MENSAJE2='!! YA FUE ANULADA O CERRADA !!'
    DO V_MENSAJ.SPR
    RETURN
ENDIF

SELECT V_HDR_LN
DEFINE WINDOW HDR_VERI ;
		FROM 9, 0 ;
		TO 24,79 ;
		DOUBLE 
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO  ;
       FIELDS LH_NR_NRO:H='NR.',LH_NR_CLI:H='CLIENTE':45,LH_NR_KG:H='KGS':P='999,999.99',LH_NR_IMPO:H='IMPORTE':P='9,999,999.99' ;
       WINDOW HDR_VERI  ;
       NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  TITLE 'DETALLE  -  [F10] PARA SALIR'

RELEASE WINDOW HDR_VERI
DO V_ONKEYS
DO HDR_VER
RETURN

******************
PROCEDURE DOC_SALE
******************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

*************
PROCEDURE FIN
*************
RELEASE MENU   M_DOCS
RELEASE WINDOW W_HDR
RELEASE WINDOW HDR_HD
RELEASE WINDOW HDR_LN
RELEASE WINDOW HDR_MSG
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE DOC_ADIC
******************
IF .NOT. V_USRAUT(USER,"HOJAS_DR",2)
    WAIT "NO ESTA AUTORIZADO PARA ESTA OPERACION !" WINDOW TIMEOUT 2 
    RETURN
ENDIF

SELECT V_HDR_HD
REG_ACTUAL=RECNO()
HH_FILE="../TMP/"+SYS(3)+".DBF"
COPY STRUCT TO (HH_FILE)

SELECT 0
USE (HH_FILE) ALIAS HH

APPEND BLANK
IF LOCK()
  REPLACE HH_EMPR    WITH EMPRESA
  REPLACE HH_FECHA   WITH DATE()
  REPLACE HH_USUARIO WITH USER
ENDIF
          
OKCANCEL=1
@ 10,0 CLEAR to 21,79

DO V_HDR_M.SPR
IF OKCANCEL=2 .OR. LASTKEY()=27
  SELECT HH
  USE
  DELETE FILE (HH_FILE)
  WAIT "NO SE HA CREADO EL DOCUMENTO!" WINDOW TIMEOUT 2
  SELECT V_HDR_HD
  GO TOP
  IF .NOT. EOF()
    GO REG_ACTUAL
  ENDIF
  DO HDR_VER
  RETURN
ENDIF

DO V_HDR_V.SPR

DEFINE WINDOW HDR_ADD FROM 9, 0	TO 24,79 DOUBLE 
SELECT V_HDR_LN
LH_FILE="../TMP/"+SYS(3)+".DBF"
COPY STRUCT TO (LH_FILE)
SELECT 0
USE (LH_FILE) ALIAS LH

FOR N=1 TO NROLINEAS
  APPEND BLANK  
  IF LOCK()
    REPLACE LH_EMPR   WITH HH.HH_EMPR
    REPLACE LH_FECHA  WITH HH.HH_FECHA
  ENDIF            
ENDFOR
GO TOP

ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE FIELDS LH_NR_NRO:H='N.R.':V=chk_NRO(LH_NR_NRO), ;
			  LH_NR_CLI:H='CLIENTE':R:40, ;
 	          LH_NR_KG:H='KG':P='999,999.99':R, ;
 	          LH_NR_IMPO:H='IMPORTE Bs.':P='9999999.99':R ;
 	   WINDOW HDR_ADD TITLE 'DETALLE  -  [F10]-GUARDAR [ESC]-CANCELAR'

IF OKCANCEL=2 .OR. LASTKEY()=27
  SELECT HH
  USE
  DELETE FILE (HH_FILE)
  SELECT LH
  USE
  DELETE FILE (LH_FILE)
  WAIT "NO SE HA CREADO EL DOCUMENTO!" WINDOW TIMEOUT 2
  SELECT V_HDR_HD
  GO TOP
  IF .NOT. EOF()
    GO REG_ACTUAL
  ENDIF
  DO HDR_VER
  RETURN
ENDIF

* GRABAR TODO
NUEVONRO=V_NEXT_N(EMPRESA,'HDR')

SELECT HH
REPLACE HH_NRO WITH NUEVONRO
USE

* ACTUALIZAR HD_NRO_HDR EN N.R.
SELECT LH
DELE FOR LH_NR_NRO=SPACE(6)
REPLACE ALL LH_NRO WITH NUEVONRO
GO TOP
DO WHILE .NOT. EOF()
  SELECT V_DOC_HD
  SEEK LH.LH_EMPR+"R"+LH.LH_NR_NRO
  IF FOUND()
    REPLACE HD_NRO_HDR WITH LH.LH_NRO
  ENDIF
  SELECT LH
  SKIP
ENDDO  
USE

SELECT V_HDR_HD
APPEND FROM (HH_FILE)
DELETE FILE (HH_FILE)

SELECT V_HDR_LN
APPEND FROM (LH_FILE)
DELETE FILE (LH_FILE)
GO TOP

SELECT V_HDR_HD
RELEASE WINDOW HDR_ADD
DO V_ONKEYS
DO HDR_VER
RETURN

PROCEDURE DOC_ANUL
******************
IF .NOT. V_USRAUT(USER,"HOJAS_DR",2)
  WAIT "NO ESTA AUTORIZADO PARA ESTA OPERACION !" WINDOW TIMEOUT 2 
  RETURN
ENDIF

SELECT V_HDR_HD
IF HH_ESTADO$'AC' 
    MENSAJE1='LA HOJA DE RUTA NO PUEDE SER ANULADA' 
    MENSAJE2='!! YA FUE ANULADA O CERRADA !!'
    DO V_MENSAJ.SPR
    RETURN
ENDIF

SELECT V_HDR_HD
DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTE DOCUMENTO ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

IF SINO='S'
  SELECT V_HDR_HD
  IF LOCK()
    REPLACE HH_ESTADO WITH 'A', ;
            HH_NOTAS1 WITH 'ANULADA', ;
            HH_NOTAS2 WITH '', ;
            HH_MARCA WITH '', HH_COLOR WITH '', ;
            HH_PLACA WITH '',  ;
            HH_COD_CL WITH SPACE(8) , HH_COD_VEN WITH SPACE(3)

    REPLACE HH_USUARIO WITH USER, HH_MODI WITH DATE()

    SELECT V_HDR_LN
    SEEK V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO
    DO WHILE .NOT. EOF() AND LH_EMPR+LH_NRO=V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO
      SELECT V_DOC_HD
      SEEK V_HDR_LN.LH_EMPR+"R"+V_HDR_LN.LH_NR_NRO
      IF FOUND()
        REPLACE HD_NRO_HDR WITH SPACE(6)
      ENDIF
      SELECT V_HDR_LN
      SKIP
    ENDDO  

    SELECT V_HDR_LN
    SEEK V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO
    IF .NOT. EOF() .AND. LOCK()
      REPLACE LH_ESTADO  WITH 'A', ;
      		  LH_NR_NRO  with '', ;
      		  LH_NR_KG   with 0, ;
      		  LH_NR_IMPO with 0, ;
      		  LH_NR_CLI  with ''
      SKIP             
      DELE REST WHILE V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO=LH_EMPR+LH_NRO FOR LOCK()
    ENDIF     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! EL DOCUMENTO HA SIDO ANULADO !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
DO HDR_VER  
RETURN

PROCEDURE HDR_MODI
*****************
IF .NOT. V_USRAUT(USER,"HOJAS_DR",2)
    WAIT "NO ESTA AUTORIZADO PARA ESTA OPERACION !" WINDOW TIMEOUT 2
    RETURN
ENDIF

SELECT V_HDR_HD
SCATTER MEMVAR
RECNUM=RECNO()
OKCANCEL=1
DO V_HDR_M.SPR

IF OKCANCEL=2 .OR. LASTKEY()=27
  WAIT "NO SE HA ALMACENADO EL DOCUMENTO!" WINDOW TIMEOUT 2
  GATHER MEMVAR
  GO RECNUM
  DO V_HDR_V.SPR
  RETURN
ENDIF

IF LOCK()
  REPLACE HH_ESTADO  WITH 'M'
  REPLACE HH_MODI    WITH DATE()
  REPLACE HH_USUARIO WITH USER
ENDIF

DO V_HDR_V.SPR

* ELIMINAMOS DATOS EN NR RELACIONADO A ESTA HDR        
MODARCH="..\TMP\"+SYS(3)+".DBF"
SELECT V_HDR_LN
SEEK V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO
COPY TO (MODARCH) REST WHILE .NOT. EOF() AND LH_EMPR+LH_NRO=V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO 
NUM_LN=0

SELECT 0
USE (MODARCH) ALIAS HDR_LN
DO WHILE .NOT. EOF() 
  NUM_LN=NUM_LN+1
  SELECT V_DOC_HD
  SEEK HDR_LN.LH_EMPR+"R"+HDR_LN.LH_NR_NRO
  IF FOUND()
    REPLACE HD_NRO_HDR WITH SPACE(6)
  ENDIF
  SELECT HDR_LN
  SKIP
ENDDO  

FOR N=1 TO NROLINEAS-NUM_LN
  APPEND BLANK
  IF LOCK()
    REPLACE LH_EMPR   WITH V_HDR_HD.HH_EMPR
    REPLACE LH_NRO    WITH V_HDR_HD.HH_NRO
    REPLACE LH_FECHA  WITH V_HDR_HD.HH_FECHA
  ENDIF            
ENDFOR  

GO TOP
		
DEFINE WINDOW HDR_ADD FROM 9, 0 TO 24,79 DOUBLE 
ON KEY LABEL F10 KEYBOARD CHR(23)

BROWSE FIELDS LH_NR_NRO:H='N.R.':V=CHK_NRO(LH_NR_NRO), ;
			  LH_NR_CLI:H='CLIENTE':R:40, ;
 	          LH_NR_KG:H='KG':P='999,999.99':R, ;
 	          LH_NR_IMPO:H='IMPORTE Bs.':P='9999999.99':R ;
 	   WINDOW HDR_ADD TITLE 'DETALLE  -  [F10] PARA GUARDAR Y SALIR'

DELETE FOR LH_NR_NRO=SPACE(6)

*ACTUALIZAMOS NR CON DATOS MODIFICADOS DE HDR
GO TOP
DO WHILE .NOT. EOF() 
  SELECT V_DOC_HD
  SEEK HDR_LN.LH_EMPR+"R"+HDR_LN.LH_NR_NRO
  IF FOUND()
    REPLACE HD_NRO_HDR WITH HDR_LN.LH_NRO
  ENDIF
  SELECT HDR_LN
  SKIP
ENDDO  
SELECT HDR_LN
USE

SELECT V_HDR_LN
SEEK V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO
DO WHILE .NOT. EOF() AND LH_EMPR+LH_NRO=V_HDR_HD.HH_EMPR+V_HDR_HD.HH_NRO 
  REPLACE LH_NR_NRO  WITH SPACE(6)
  REPLACE LH_NR_KG   WITH 0
  REPLACE LH_NR_IMPO WITH 0
  REPLACE LH_NR_CLI  WITH ""
  DELETE NEXT 1
  SKIP
ENDDO  
APPEND FROM (MODARCH)
DELE FILE (MODARCH)

SELECT V_HDR_HD
WAIT "SE HAN GUARDADO LOS CAMBIOS!" WINDOW TIMEOUT 2
RELEASE WINDOW HDR_ADD
DO V_ONKEYS
DO HDR_VER
RETURN

FUNCTION CHK_NRO
****************
PARAMETER NRO_NR
*NRO_NR  =NRO DE NR QUE SE BUSCAR

IF SPACE(6)=NRO_NR
  REPLACE LH_NR_NRO  WITH SPACE(6)
  REPLACE LH_NR_KG   WITH 0
  REPLACE LH_NR_IMPO WITH 0
  REPLACE LH_NR_CLI  WITH ""
  KEYBOARD chr(13)+chr(13)+chr(13)
  RETURN .T.
ENDIF

CURRENT=SELECT()
SELECT V_DOC_HD
SET FILTER TO HD_EMPR=EMPRESA AND HD_TIPO="R" AND HD_ESTADO<>"A" ;
    AND HD_NRO_HDR=SPACE(6)

SEEK EMPRESA+'R'+NRO_NR
IF .NOT. FOUND()
  NRO_NR=TGETNR()
ENDIF

BSTOT  =0
PESOTOT=0
CLIENTE=""

SELECT V_DOC_HD
SET FILTER TO HD_EMPR=EMPRESA AND HD_TIPO="R" AND HD_ESTADO<>"A"

SEEK EMPRESA+"R"+NRO_NR
IF FOUND()
  BSTOT  =hd_importe-ROUND((hd_importe* hd_dscto_p)/100,2)+ hd_ped_fle+ hd_ped_emb+ hd_ped_otr
  SELECT V_CLIENT
  SEEK V_DOC_HD.hd_empr+V_DOC_HD.hd_COD_CL
  IF FOUND()
	CLIENTE=CL_RAZON
  ENDIF
  SELECT V_DOC_LN
  SEEK V_DOC_HD.hd_empr+V_DOC_HD.hd_tipo+V_DOC_HD.hd_nro
  SUM LD_PESO TO PESOTOT WHILE LD_EMPR+LD_TIPO+LD_NRO=V_DOC_HD.hd_empr+V_DOC_HD.hd_tipo+V_DOC_HD.hd_nro
ENDIF

SELECT (CURRENT)
REPLACE LH_NR_NRO  WITH NRO_NR
REPLACE LH_NR_KG   WITH PESOTOT
REPLACE LH_NR_IMPO WITH BSTOT
REPLACE LH_NR_CLI  WITH CLIENTE

*BUSCA DUPLICADO
WRECNO=RECNO()
WCOUNT=0
COUNT TO WCOUNT FOR LH_NR_NRO=NRO_NR
GO WRECNO
IF WCOUNT>1
  WAIT "CODIGO DUPLICADO!" WINDOW TIMEOUT 2
  REPLACE LH_NR_NRO  WITH SPACE(6)
  REPLACE LH_NR_KG   WITH 0
  REPLACE LH_NR_IMPO WITH 0
  REPLACE LH_NR_CLI  WITH ""
  KEYBOARD chr(13)+chr(13)+chr(13)
ENDIF
RETURN .T.

***************
FUNCTION TGETNR
***************
TNROS="..\TMP\"+SYS(3)+".DBF"

SELECT HD_EMPR, HD_NRO, CL_COD_CL, CL_RAZON ;
       FROM V_DOC_HD, V_CLIENT WHERE HD_EMPR=EMPRESA AND HD_TIPO="R" AND HD_ESTADO<>'A' AND HD_NRO_HDR=SPACE(6) AND HD_EMPR=CL_EMPR AND hd_cod_cl=CL_COD_CL ;
       ORDER BY HD_NRO INTO DBF (TNROS) 

USE (TNROS)
GO TOP

IF .NOT. FOUND()
  *-  DEFINIR & ACTIVAR POPUP DE N.R. -*
  DEFINE POPUP GETNRONR FROM 8,10 TO 20,70 PROMPT ;
	  	 FIELD HD_NRO+"-"+CL_COD_CL+":"+SUBSTR(CL_RAZON,1,40) ;
 	   	 TITLE 'SELECCIONE N.R. CON [ENTER]' 
  ON SELECTION POPUP GETNRONR DEACTIVATE POPUP
  *
  ACTIVATE POPUP GETNRONR
  WNRO_NR =SUBSTR(PROMPT(),1,6)
  RELEASE POPUP GETNRONR
ELSE
  WNRO_NR=SPACE(6)
ENDIF
USE
DELE FILE (TNROS)
RETURN WNRO_NR

PROCEDURE DOC_BUSC
******************
DEFINE WINDOW HDR_BUSCA FROM 10,10 TO 19,70 DOUBLE TITLE 'BUSQUEDA DE N.R.' 

ACTIVATE WINDOW HDR_BUSCA
SELECT V_HDR_HD 
@ 1,1 GET choice FUNCTION '*RNV POR \<NRO;POR \<CHOFER' ;
	SIZE 1,10,1 DEFAULT 1 && COLOR B/G
MI_NRO=SPACE(6)
MI_CLI=SPACE(8)
@ 1,20 SAY 'NUMERO :' GET MI_NRO PICTURE '999999'   WHEN CHOICE=1
@ 3,20 SAY 'CHOFER :' GET MI_CLI PICTURE '99999999' WHEN CHOICE=2
@ 7,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G
READ CYCLE
SELECT V_HDR_HD
IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'
  ON KEY LABEL F12 DO V_BEXPOR WITH "HH_NRO, HH_FECHA, HH_COD_CL, V_CLIENT.CL_RAZON, HH_NOTAS1, HH_ESTADO", W_FILTRO
  ON KEY LABEL F10 KEYBOARD CHR(23)
  DO CASE
   CASE choice = 1
     W_FILTRO="ALLTRIM(MI_NRO)$HH_NRO"
   CASE choice = 2
     W_FILTRO="ALLTRIM(MI_CLI)$HH_COD_CL"
  ENDCASE

  BROWSE NOEDIT FOR EVAL(W_FILTRO) ;
     FIELDS HH_NRO:H='NRO.',HH_FECHA:H='FECHA',HH_cod_cl:H="COD.CHOFER",V_CLIENT.CL_RAZON:H="NOMBRE CHOFER", HH_NOTAS1:H='RUTA',HH_ESTADO:H='ESTADO' ;
     WINDOW W_HDR TITLE W_TITLE
ENDIF
RELEASE WINDOW HDR_BUSCA
DO V_ONKEYS
DO HDR_VER
RETURN

PROCEDURE DOC_MOVR
******************
PARAMETER DIRECCION
SELECT V_HDR_HD

IF DIRECCION="?"
    DO V_IR_REG.SPR
    W_TITLE="SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO"
    W_FILTRO=".T."
    ON KEY LABEL F10 KEYBOARD CHR(23)
    ON KEY LABEL F12 DO V_BEXPOR WITH "HH_NRO, HH_FECHA, HH_COD_CL, V_CLIENT.CL_RAZON, HH_NOTAS1, HH_ESTADO", W_FILTRO
    BROWSE FIELDS HH_NRO:H='NRO.',HH_FECHA:H='FECHA',HH_cod_cl:H="COD.CHOFER",V_CLIENT.CL_RAZON:H="NOMBRE CHOFER", HH_NOTAS1:H='RUTA',HH_ESTADO:H='ESTADO' ;
	   NOEDIT NODELETE WINDOW W_HDR TITLE W_TITLE
    DO V_ONKEYS 
ELSE
   DO V_CURMOV WITH "V_HDR_HD", DIRECCION
ENDIF   
DO HDR_VER
RETURN

PROCEDURE DOC_OPCI
******************
DO V_HDR_OP
RETURN


