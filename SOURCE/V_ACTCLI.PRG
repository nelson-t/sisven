******************
PROCEDURE V_ACTCLI
******************
CLEAR
SET TALK OFF
SET DELE ON
SET DATE TO FRENCH

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U"
  DO V_GETEMP
ENDIF

TC_INI=0.0
TC_HOY=0.0

FECHAF=DATE()
INI_GEST={  /  /  }
OPCION=1

DO V_ACTCLI.SPR
IF OPCION=2 .OR. LASTKEY()=27
  RETURN
ENDIF

CLEAR
  
SELECT 1
USE V_CLIENT ORDER CODIGO
SET FILTER TO CL_EMPR=EMPRESA

SELECT 2
USE V_FAC_HD ORDER ESTADOS
SET FILTER TO BETWEEN(HF_FECHA,INI_GEST,FECHAF)

SELECT 3
USE V_REC_HD ORDER ESTADOS
SET FILTER TO BETWEEN(HR_FECHA,INI_GEST,FECHAF)

SELECT 4
USE V_DOC_HD ORDER ESTADOS
SET FILTER TO BETWEEN(HD_FECHA,INI_GEST,FECHAF)

SELECT 5
USE V_TC ORDER FECHA
SEEK DTOS(INI_GEST)
IF EOF()
  CLOSE ALL
  WAIT "ERROR...NO EXISTE TC PARA "+DTOC(INI_GEST) WINDOW
  RETURN
ELSE
  TC_INI=TC_MONTO
ENDIF

SEEK DTOS(FECHAF)
IF EOF()
  CLOSE ALL
  WAIT "ERROR...NO EXISTE TC PARA "+DTOC(FECHAF) WINDOW
  RETURN
ELSE
  TC_HOY=TC_MONTO
ENDIF
    
SELECT V_CLIENT
GO TOP

DO WHILE .NOT. EOF()
DEBE =0
HABER=0
SALDO=0

DEBEU =0
HABERU=0
SALDOU=0

SELECT V_FAC_HD
SET RELATION TO DTOS(HF_FECHA) INTO V_TC

SEEK V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL

DO WHILE .NOT. EOF() .AND. V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL=HF_EMPR+HF_COD_CL
  IF HF_ESTADO<>'A' .AND. HF_TIPO<>'O'
    *REVISA SI EXISTE TC PARA LA FECHA
    IF V_TC.TC_MONTO=0
	  CLOSE ALL
  	  WAIT "ERROR...NO EXISTE TC PARA "+DTOC(V_TC.TC_FECHA) WINDOW
      RETURN
    ENDIF
    
    **SI POR ALGUN MOTIVO LA FACTURA NO TIENE SU TC, SE LO ASIGNA
    IF HF_TIPO='R' .AND. HF_FACTOR=0
      REPLACE HF_FACTOR WITH V_TC.TC_MONTO
    ENDIF

    DO CASE
      CASE HF_TIPO='R'  && FACTURA AL CREDITO
        DEBE =DEBE+hf_importe-HF_DSCTO_M-ROUND(hf_importe*(HF_DSCTO_P/100),2)+hf_fletes+hf_embala+hf_otros 
        DEBEU=DEBEU+ROUND((HF_importe-HF_DSCTO_M-ROUND(hf_importe*(HF_DSCTO_P/100),2)+hf_fletes+hf_embala+hf_otros)/HF_FACTOR,2)
      CASE HF_TIPO='X'  && NOTA DE CREDITO
        HABER =HABER+hf_importe
        HABERU=HABERU+ROUND(hf_importe/V_TC.TC_MONTO,2)
      CASE HF_TIPO='Y'  && NOTA DE DEBITO
        DEBE=DEBE+ hf_importe    
        DEBEU=DEBEU+ROUND(hf_importe/V_TC.TC_MONTO,2)
    ENDCASE
  ENDIF
  SELECT V_FAC_HD 
  SKIP
ENDDO

SELECT V_REC_HD
SET RELATION TO DTOS(HR_FECHA) INTO V_TC

SEEK V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL
DO WHILE .NOT. EOF() .AND. V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL=HR_EMPR+HR_COD_CL
  IF HR_ESTADO<>'A' 
    IF V_TC.TC_MONTO=0
	  CLOSE ALL
  	  WAIT "ERROR...NO EXISTE TC PARA "+DTOC(V_TC.TC_FECHA) WINDOW
      RETURN
    ENDIF

    HABER=HABER+HR_TOT_BS
    HABERU=HABERU+ROUND(HR_TOT_BS/V_TC.TC_MONTO,2)
  ENDIF
  SELECT V_REC_HD
  SKIP
ENDDO

*SE INCLUYE NR NO FACTURADAS
SELECT V_DOC_HD
SET RELATION TO DTOS(HD_FECHA) INTO V_TC

SEEK V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL
DO WHILE .NOT. EOF() .AND. V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL=HD_EMPR+HD_COD_CL
  IF (HD_ESTADO=' ' .AND. HD_TIPO='R')
     IF V_TC.TC_MONTO=0
	   CLOSE ALL
   	   WAIT "ERROR...NO EXISTE TC PARA "+DTOC(V_TC.TC_FECHA) WINDOW
       RETURN
     ENDIF
     DEBE=DEBE+ hd_importe    
     DEBEU=DEBEU+ROUND(hd_importe/V_TC.TC_MONTO,2)
  ENDIF
  SELECT V_DOC_HD
  SKIP
ENDDO

SELECT V_CLIENT 
IF LOCK()
  REPLACE CL_SALD_DB WITH DEBE, CL_SALD_HB WITH HABER, ;
          CL_SALD_AC WITH CL_SALD_AN+DEBE-HABER
  REPLACE CL_DIF_AC WITH ((CL_SALD_AN+CL_DIF_AN)/TC_INI+DEBEU-HABERU)*TC_HOY-CL_SALD_AC
ENDIF

@ 9, 23 SAY 'ACTUALIZACION DE SALDOS DE CLIENTES'
@ 11,3 TO 13,76 DOUBLE
@ 12,5 SAY  'CLIENTE: '+CL_COD_CL+'-  '+CL_RAZON+'  SALDO'+TRANSFORM(CL_SALD_AC,'99,999,999.99') COLOR B*/GR

SKIP
ENDDO
CLEAR
DO V_FINPRO.SPR        
CLOSE DATA
CLEAR
RETURN 

FUNCTION GET_EMPR
*****************
PARAMETER EMPR
SELECT 0
USE V_EMPR ORDER EMPR
SEEK EMPR
IF EOF()
  USE
  RETURN .F.
ELSE
  INI_GEST  =EM_FECHA
* @ 8,37 SAY MI_EMPRESA
  @ 11,35 SAY INI_GEST
  USE
  RETURN .T.
ENDIF
