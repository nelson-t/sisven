***************************************************************************
* MANTENIMIENTO DE NOTAS DE EMPAQUE
*
*IF NOT V_VALACC()
*  RETURN
*ENDIF  

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U"
  DO V_GETEMP
ENDIF

DO V_GETALM
* DEFINE ALM y MI_ALM

SET DELE ON
TIPO='E'
*                                          
NOTA='        NOTA DE EMPAQUE      '
INSAL='2'

DO SET_AMBI
DO SET_GRAL
DO V_ONKEYS

SELECT V_NE_HD
SET FILTER TO HD_EMPR=EMPRESA .AND. HD_TIPO=TIPO .AND. HD_ALM=ALM

SEEK EMPRESA
DO NR_VER
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
 ACTIVATE MENU M_NR
ENDDO

DO FIN

RETURN

*******************************************************************************

PROCEDURE SET_AMBI
******************
SET BLINK OFF
SET CLOCK ON
SET CONFIRM ON
SET EXCLU OFF
SET DATE TO DMY
SET DELE ON
SET TALK OFF
SET SAFETY OFF
SET ESCAPE OFF
SET EXACT OFF
RETURN

PROCEDURE SET_GRAL
******************
*MUESTRA PANTALLA INICIAL E INICIALIZA AMBIENTE CON 'SETUP'
SET CURSOR OFF
MENSAJE='ESPERE UN MOMENTO POR FAVOR !!'
CLEAR
DO V_NE_CA.SPR
S_INI=SECONDS()
DO SETUP
S_FIN=SECONDS()
IF S_FIN-S_INI<2
  @ 23,0  
  WAIT '' TIMEOUT 2-(S_FIN-S_INI)
ENDIF
SET CURSOR ON
CLEAR
RETURN

PROCEDURE SETUP
***************
* INICIALIZA AMBIENTE
SELECT 1
USE V_NE_HD ORDER CORRE
SELECT 2
USE V_NE_LN ORDER CORRE

SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4

USE V_ITEMS ORDER PART
SELECT 5
* TEMPORAL
SELECT 6
* TEMPORAL
SELECT 7
USE V_CORRE ORDER EMPRESA
SELECT 8
USE V_VENDOR ORDER CODIGO
SELECT 9
USE V_ALM ORDER ALM

SELECT 10
USE V_DOC_HD ORDER NR
SELECT 11
USE V_DOC_LN ORDER NR

SELECT 12
USE V_CLIENT ORDER CODIGO

SELECT V_NE_LN
SET RELATION TO LD_EMPR+LD_PART INTO V_ITEMS

SELECT  V_NE_HD
SET RELATION TO HD_EMPR INTO V_EMPR
SET RELATION TO HD_EMPR+HD_ALM INTO V_ALM ADDITIVE
SET RELATION TO HD_EMPR+HD_COD_CL INTO V_CLIENT ADDITIVE

SELECT V_NE_HD

DEFINE WINDOW W_NR FROM 0,0 TO 24,79 DOUBLE
DEFINE WINDOW NR_hd FROM 0, 0 TO 8,79 TITLE NOTA NOFLOAT DOUBLE 
DEFINE WINDOW NR_ln	FROM 9, 0 TO 21,79 TITLE "D E T A L L E" ;
	NOFLOAT DOUBLE 
DEFINE WINDOW NR_MSG	FROM 21, 0	TO 24,79	TITLE "MENSAJES" ;
	NOFLOAT DOUBLE 
		
DEFINE MENU M_NR 
DEFINE PAD PREV OF M_NR PROMPT '<- \<PREV'    AT 23,01 SKIP FOR BOF("V_NE_HD")
DEFINE PAD NEXT OF M_NR PROMPT '\<SIG ->'     AT 23,11 SKIP FOR EOF("V_NE_HD")
DEFINE PAD OJEAR OF M_NR PROMPT 'O\<JEA'      AT 23,20 
DEFINE PAD BUSCA OF M_NR PROMPT '\<BUSCA'     AT 23,27 
DEFINE PAD MIRA  OF M_NR PROMPT '\<VERIFICA'  AT 23,35 
DEFINE PAD ALTA  OF M_NR PROMPT '\<ALTA'      AT 23,46 
DEFINE PAD ANULA OF M_NR PROMPT 'AN\<UL'      AT 23,53 
DEFINE PAD OPCION OF M_NR PROMPT '\<OPCIONES' AT 23,60 
DEFINE PAD SALE  OF M_NR PROMPT 'SA\<LIR '    AT 23,71

*          1         2         3         4         5         6         7      
*01234567890123456789012345678901234567890123456789012345678901234567890123456789
*ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป
*บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ ANUL บ OPCIONES บ SALIR  บ
*ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ

ON SELECTION PAD PREV   OF M_NR  DO NR_PREV
ON SELECTION PAD NEXT   OF M_NR  DO NR_NEXT
ON SELECTION PAD OJEAR  OF M_NR  DO NR_OJEAR
ON SELECTION PAD BUSCA  OF M_NR  DO NR_BUSCA
ON SELECTION PAD MIRA   OF M_NR  DO NR_MIRA
ON SELECTION PAD ALTA   OF M_NR  DO NR_ALTA
ON SELECTION PAD ANULA  OF M_NR  DO NR_ANULA
ON SELECTION PAD OPCION OF M_NR  DO NR_OPC
ON SELECTION PAD SALE   OF M_NR  DO NR_SALE
RETURN

PROCEDURE NR_VER
****************
VENDEDOR=SPACE(20)

SELECT V_NE_HD
DO GET_VENDOR WITH HD_COD_VEN
SELECT V_NE_HD
DO V_NE_V.SPR
ACTIVATE SCREEN
@ 22,0 SAY "ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป"
@ 23,0 SAY "บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ BAJA บ OPCIONES บ SALIR  บ"
@ 24,0 SAY "ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ"
SELECT V_NE_LN
GO TOP
BROWSE KEY V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO  ;
 FIELDS LD_PART:H='PART.#', ;
 V_ITEMS.IT_DESCR:H='DESCRIPCION', ;
 LD_CANT  :H='CANTIDAD':P='9,999,999.99', ;
 V_ITEMS.IT_UNIDAD:H='UNI.', ;
 LD_PESO  :H='kg. APROX.':P='99,999.99', ;
 LD_ESTADO:H='ESTADO' WINDOW NR_LN  ;
 NOEDIT NOCLEAR NOAPPEND NOWAIT
USE
USE V_NE_LN order CORRE
SET RELATION TO LD_EMPR+LD_PART INTO V_ITEMS
RETURN

PROCEDURE NR_MIRA
*****************
SELECT V_NE_LN
DEFINE WINDOW NR_VERI FROM 9, 0 TO 24,79	DOUBLE 
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO  ;
 FIELDS LD_PART:H='PART.#', V_ITEMS.IT_DESCR:H='DESCRIPCION', ;
 LD_CANT:H='CANTIDAD':P='9,999,999.99', V_ITEMS.IT_UNIDAD:H='UNI.', ;
 LD_PESO:H='kg. APROX.':P='99,999.99' WINDOW NR_VERI  ;
 NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  ;
 TITLE 'DETALLE  -  [F10] PARA SALIR'

RELEASE WINDOW NR_VERI
DO V_ONKEYS
DO NR_VER
RETURN

PROCEDURE NR_NEXT
*****************
SELECT V_NE_HD
TRECNO=RECNO()
SKIP
IF EOF()
 GO TRECNO
ENDIF 
DO NR_VER
RETURN

PROCEDURE NR_PREV
*****************
SELECT V_NE_HD
IF .NOT. BOF()
 SKIP -1
ENDIF 
DO NR_VER
RETURN

PROCEDURE NR_ANULA
******************
IF .NOT. V_USRAUT(USER,"DOCS_ALM",3)  
   RETURN
ENDIF

SELECT V_NE_HD

DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR LA NOTA ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

IF SINO='S'

  SELECT V_DOC_HD
  SEEK v_ne_hd.HD_EMPR+v_ne_hd.HD_D_TIPO+v_ne_hd.HD_D_NRO

  IF .NOT. EOF()
    SELECT V_NE_LN
    SET RELATION TO v_ne_hd.HD_EMPR+v_ne_hd.HD_D_TIPO+v_ne_hd.HD_D_NRO+LD_PART INTO V_DOC_LN 
    SEEK V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO 
    DO WHILE .NOT. EOF() .AND. V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO=LD_EMPR+LD_ALM+LD_TIPO+LD_NRO
      IF LOCK('V_DOC_LN')
        REPLACE V_DOC_LN.LD_DESPACH WITH V_DOC_LN.LD_DESPACH-LD_CANT
      ENDIF
      SKIP
    ENDDO 
    SET RELATION OFF INTO V_DOC_LN
  ENDIF

  SELECT V_NE_HD
  IF LOCK()
    REPLACE HD_ESTADO WITH 'A', ;
            HD_OBS WITH '* * * A N U L A D A * * *'

    SELECT V_NE_LN
    SEEK V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO  
    IF .NOT. EOF() .AND. LOCK()
      REPLACE LD_ESTADO WITH 'A', ;
              LD_PART WITH SPACE(15), ;
              LD_CANT WITH 0, ;
              LD_PESO WITH 0
      SKIP             
      DO WHILE .NOT. EOF() .AND. V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO=LD_EMPR+LD_ALM+LD_TIPO+LD_NRO
        IF LOCK()
          DELETE
        ENDIF
        SKIP
      ENDDO
    ENDIF     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! EL DOCUMENTO HA SIDO ANULADO !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
  DO NR_VER  
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  

RETURN

PROCEDURE NR_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_NR
RELEASE WINDOW NR_HD
RELEASE WINDOW NR_LN
RELEASE WINDOW NR_MSG
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE NR_MODI
*****************
MENSAJE1='!! EL DOCUMENTO NO PUEDE SER MODIFICADO !!' 
MENSAJE2='   - LAS NEs SOLO PUEDEN SER ANULADAS -'
DO V_MENSAJ.SPR
RETURN

FUNCTION CHK_VENDOR
*******************
PARAMETER CODIGO
CURRENT=SELECT()
SELECT V_VENDOR
SEEK EMPRESA+CODIGO
IF FOUND()
  @ 3,16 SAY VE_NOMBRE
  SELECT (CURRENT)
  RETURN .T.
ELSE
  @ 3,16 SAY 'CODIGO DE VENDEDOR ERRADO'
  SELECT (CURRENT)
  RETURN .F.  
ENDIF    

*-----------------*
PROCEDURE GET_VENDOR
*-----------------*
PARAMETER CODIGO
CURRENT=SELECT()
SELECT V_VENDOR
SEEK EMPRESA+CODIGO
IF FOUND()
  VENDEDOR=VE_NOMBRE
ELSE
  VENDEDOR='VENDEDOR NO EXISTE'
ENDIF    
SELECT (CURRENT)
RETURN

PROCEDURE IT_BUSCA
******************
DEFINE WINDOW IT_BUSCA FROM 17,19 TO 24,79 DOUBLE TITLE 'BUSQUEDA DE ITEMS'
ACTIVATE WINDOW IT_BUSCA

@ 1,1 GET choice FUNCTION '*RNV POR \<CODIGO;POR \<DESCR.';
	SIZE 1,10,1 DEFAULT 1

MI_COD=SPACE(15)
MI_SELE=SPACE(20)
@ 1,20 SAY '         CODIGO:' GET MI_COD PICTURE REPLICATE('!',15) WHEN CHOICE=1
@ 3,20 SAY 'DESCR. A BUSCAR:' GET MI_SELE PICTURE REPLICATE('!',20) WHEN CHOICE=2
@ 5,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1
READ CYCLE

SELECT V_ITEMS

IF okcancel = 1
  DO CASE
   CASE choice = 1
     ULTIMO=RECNO()
     MI_COD=EMPRESA+MI_COD
     SEEK RTRIM(MI_COD)
     IF EOF()
       CLEAR
       @ 2,5
       WAIT '               CODIGO NO EXISTE .... !!' 
       GO ULTIMO
     ENDIF
   CASE choice = 2
    MI_SELE=ALLTRIM(MI_SELE)
    SET FILTER TO MI_SELE$IT_DESCR 
  ENDCASE
ENDIF
RELEASE WINDOW IT_BUSCA
RETURN

FUNCTION CALC_PESO
******************
PARAMETER WCANT, WPESO
IF LD_CANT<>0 
 REPLACE LD_PESO WITH WCANT*WPESO
ENDIF 
RETURN .T.

PROCEDURE NR_OPC
****************
OPCION=1

DO V_NE_O.SPR

DO CASE
  CASE LASTKEY()<>27 .AND. OPCION=1
    * IMPRIME PEDIDO
    DO NR_IMPR
  CASE LASTKEY()<>27 .AND. OPCION=2
    DO NR_MODI
  CASE LASTKEY()=27 .OR. OPCION=3
    * SALIR SIN HACER CAMBIOS  
ENDCASE

RETURN

PROCEDURE NR_IMPR
******************
SELECT V_NE_HD

LLAVE=HD_EMPR+HD_ALM+HD_TIPO+HD_NRO

ACTIVATE WINDOW NR_MSG
@ 0,0 SAY '   ESPERE UN MOMENTO POR FAVOR ..... IMPRIMIENDO'

TITULO='  NOTA DE EMPAQUE  '
SELECT V_NE_LN
SEEK LLAVE
IF FOUND()
  TEMP_FILE='..\TMP\'+SYS(3)+'.NE'
  _PADVANCE='LINEFEED'
  report form V_NE_FOR for LD_EMPR+LD_ALM+LD_TIPO+LD_NRO=LLAVE TO file &TEMP_FILE
  _PADVANCE='FORMFEED'
  *MODI COMM &TEMP_FILE
  ! COPY &TEMP_FILE lpt1:
  DELETE FILE (TEMP_FILE)
  HIDE WINDOW NR_MSG
  MENSAJE1='NOTA DE EMPAQUE NRO.:'+V_NE_HD.HD_NRO+' HA SIDO IMPRESA'
  MENSAJE2=''
  DO V_MENSAJ.SPR
ENDIF

SELECT V_NE_HD
DO NR_VER
RETURN

*****************
PROCEDURE NR_ALTA
*****************
IF .NOT. V_USRAUT(USER,"DOCS_ALM",2)  
   RETURN
ENDIF

MODO='ALTA'
OKCANCEL=1

MI_HD=SYS(3)+'.HD'
MI_LN=SYS(3)+'.LN'

SELECT V_NE_HD
COPY STRUC TO (MI_HD)

SELECT 5
USE (MI_HD) ALIAS CABEZA EXCLU

APPEND BLANK
IF LOCK()
  REPLACE HD_EMPR    WITH EMPRESA, ;
          HD_TIPO    WITH TIPO, ;
          HD_ALM     WITH ALM, ;
          HD_FECHA   WITH DATE(), ;
          HD_USUARIO WITH SYS(0)
ENDIF

HIDE MENU M_NR
ACTIVATE SCREEN
CLEAR
@ 13,25 TO 17,55 
@ 14,27 SAY 'DIGITE LOS DATOS POR FAVOR'
@ 16,27 SAY '     [ESC] PARA SALIR     '
ACTIVATE WINDOW NR_HD
CLEAR

D_TIPO=' '
SIDOC=.F.

DO V_NE_M.SPR

SELECT V_DOC_HD
SET ORDER TO NR
SEEK EMPRESA+CABEZA.HD_D_TIPO+CABEZA.HD_D_NRO

IF FOUND() AND V_DOC_HD.HD_ALM<>ALM
  WAIT "ERROR..DOCUMENTO NO PERTENECE AL MISMO ALMACEN!!" WINDOW TIMEOUT 3
ENDIF

IF OKCANCEL=2 .OR. (FOUND() .AND. V_DOC_HD.HD_ALM<>ALM) .OR. V_DOC_HD.HD_ESTADO='A'
  *CANCELAMOS OPERACION
  SELECT CABEZA
  USE
  DELETE FILE (MI_HD)
  SELECT V_NE_HD
  DO NR_VER
  RETURN
ENDIF

*CONTINUAMOS OPERACION

SELECT CABEZA

VENDEDOR=SPACE(20)

SELECT CABEZA
DO GET_VENDOR WITH HD_COD_VEN

SELECT CABEZA
DO V_NE_V.SPR

SELECT V_DOC_HD
SET ORDER TO NR
SEEK EMPRESA+CABEZA.HD_D_TIPO+CABEZA.HD_D_NRO

IF .NOT. EOF()
  SELECT CABEZA
  REPLACE HD_COD_CL with V_DOC_HD.HD_COD_CL
  REPLACE HD_AREA   with V_DOC_HD.HD_AREA
    
  SELECT 6
  USE V_NE_TMP
  COPY STRUCT TO (MI_LN)
  USE (MI_LN) ALIAS LINEAS EXCLU
  
  PORLOMENSOUNO=.F.
  
  SELECT V_DOC_LN
  SEEK V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO
  DO WHILE .NOT. EOF() .AND. LD_EMPR+LD_TIPO+LD_NRO=V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO
    IF V_DOC_LN.LD_DESPACH<V_DOC_LN.LD_CANT
      PORLOMENOSUNO=.T.    
      SCATTER MEMVAR
      SELECT LINEAS
      APPEND BLANK
      GATHER MEMVAR
      REPLACE LD_ESTADO WITH ' '

      *** TOT_DESPA=GET_DESPA(EMPRESA,CABEZA.HD_D_TIPO,CABEZA.HD_D_NRO,LD_PART)
      TOT_DESPA=V_DOC_LN.LD_DESPACH
    
      REPLACE LD_PEND WITH V_DOC_LN.LD_CANT-TOT_DESPA
      REPLACE LD_CANT WITH V_DOC_LN.LD_CANT-TOT_DESPA
    
      REPLACE LD_PESO WITH V_DOC_LN.LD_PESO/V_DOC_LN.LD_CANT*LD_PEND
      REPLACE LD_PESOPEN WITH LD_PESO
    
    ENDIF
    SELECT V_DOC_LN
    SKIP
  ENDDO

  SELECT LINEAS
  ON KEY LABEL F10 KEYBOARD CHR(23)
  SET RELATION TO LD_EMPR+LD_PART INTO V_ITEMS
  GO TOP
  BROWSE FIELDS LD_PART:H='PART.#':R, ;
         V_ITEMS.IT_DESCR:H='DESCRIPCION':W=.F., ;
         LD_PEND:H='PENDIENTE':P='9999999.99':W=.F., ;
         LD_CANT:H='DESPACHAR':P='9999999.99':V=LD_CANT<=LD_PEND .AND. CALC_PESO(LD_CANT,LD_PESOPEN/LD_PEND):E='NO PUEDE DESPACHAR ESA CANTIDAD', ;
         V_ITEMS.IT_UNIDAD:H='UNI.':W=.F., ;
         LD_PESO:H='kg. APROX.':P='99999.99':R ;
         WINDOW NR_LN NOAPPEND  ;
         TITLE '[F10] - TERMINA DETALLE   [ESC] - DESCARTA DOCUMENTO' 
  GO TOP

  SELECT LINEAS
  DO WHILE .NOT. EOF()
    *BORRAMOS LINEAS CON CANT 0, CANT NEGATIVA O CODIGO DE PARTE EN BLANCO 
    IF LD_CANT=0 .OR LD_CANT<0 .OR. LD_PART=SPACE(15)
        DELETE
    ENDIF
    SKIP
  ENDDO 
  GO TOP
ENDIF

SI_NUEVO=.F.

IF LASTKEY()<>27 .AND. .NOT. EOF() .AND PORLOMENOSUNO
  SI_NUEVO=.T.
  SELECT CABEZA
  REPLACE HD_NRO WITH V_NEXT_N(EMPRESA,'NE')
  SELECT LINEAS
  DO WHILE .NOT. EOF() 
    REPLACE LD_TIPO  WITH CABEZA.HD_TIPO
    REPLACE LD_NRO   WITH CABEZA.HD_NRO
    REPLACE LD_FECHA WITH CABEZA.HD_FECHA
    REPLACE LD_D_TIPO  WITH CABEZA.HD_D_TIPO
    REPLACE LD_D_NRO   WITH CABEZA.HD_D_NRO
    
    SKIP
  ENDDO 
  SELECT 5
  USE
  SELECT 6
  USE
  SELECT V_NE_HD
  APPEND FROM (MI_HD)
  SELECT V_NE_LN
  APPEND FROM (MI_LN)

  SELECT V_NE_LN
  SET RELATION TO V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO+LD_PART INTO V_DOC_LN ADDITIVE
  SEEK V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO 
  DO WHILE .NOT. EOF() .AND. V_NE_HD.HD_EMPR+V_NE_HD.HD_ALM+V_NE_HD.HD_TIPO+V_NE_HD.HD_NRO=LD_EMPR+LD_ALM+LD_TIPO+LD_NRO
    IF LOCK('V_DOC_LN')
      REPLACE V_DOC_LN.LD_DESPACH WITH V_DOC_LN.LD_DESPACH+LD_CANT
    ENDIF
    SKIP
  ENDDO 
  SET RELATION OFF INTO V_DOC_LN
  GO TOP
ELSE
  WAIT 'DOCUMENTO SE DESCARTO !!' NOWAIT WINDOW
ENDIF

SELECT 5
USE
SELECT 6
USE
DELETE FILE (MI_HD)
DELETE FILE (MI_LN)
FLUSH

DO V_ONKEYS
DO NR_VER

IF SI_NUEVO
  WAIT 'DOCUMENTO #:'+V_NE_HD.HD_NRO+' GENERADO !' NOWAIT WINDOW
  DO NR_OPC
ENDIF
RETURN


FUNCTION GET_DESPA
*******************
*Retorna total despachado (por NEs) de un ITEM perteneciente a una NR
*NO SE USARA AQUI, PUESTO QUE CADA OPERACION YA CALCULA LD_DESPACH

PARAMETER WEMPR,WTIPO,WNRO,WPART
CURR_DBF=SELECT()

TOT_D=0

SELECT V_NE_LN
CURR_IDX=ORDER()

SET ORDER TO NOTAR     &&EMPR+D_TIPO+D_NRO+PART

SEEK WEMPR+WTIPO+WNRO+WPART  && BUSCAMOS EN N.E. 

DO WHILE .NOT. EOF() .AND. LD_EMPR+LD_D_TIPO+LD_D_NRO+LD_PART=WEMPR+WTIPO+WNRO+WPART
  TOT_D=TOT_D+LD_CANT
  SKIP
ENDDO

SET ORDER TO (CURR_IDX)
SELECT (CURR_DBF)
WAIT WEMPR+WTIPO+WNRO+WPART+":"+str(TOT_D) WINDOW
RETURN TOT_D

PROCEDURE NR_OJEAR
******************
W_FILTRO=".T."
W_TITLE ="SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO"
ON KEY LABEL F10 KEYBOARD CHR(23)
ON KEY LABEL F12 DO V_BEXPOR WITH "HD_NRO, HD_ALM, HD_FECHA, HD_OBS, HD_ESTADO", W_FILTRO
SELECT V_NE_HD
DO V_IR_REG.SPR
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_OBS:H='OBSERVACIONES':P='@S30',HD_D_NRO:H='N.R.',HD_ESTADO:H='ESTADO' NOEDIT NOAPPEND NODELETE WINDOW W_NR ;
   TITLE W_TITLE
DO V_ONKEYS 
DO NR_VER
RETURN

PROCEDURE NR_BUSCA
******************
DEFINE WINDOW NR_BUSCA FROM 10,10 TO 19,70 DOUBLE TITLE 'BUSQUEDA DE N.I.' 
ACTIVATE WINDOW NR_BUSCA

MI_NRO='      '
MI_FECHA={  /  /  }
MI_TIPO='R'
MI_NOTAR='      '
@ 1,1 GET choice FUNCTION '*RNV POR \<NRO;POR \<FECHA;POR \<N.R.' ;
	SIZE 1,10,1 DEFAULT 1
@ 1,20 SAY 'NUMERO :' GET MI_NRO PICTURE '999999'   WHEN CHOICE=1
@ 3,20 SAY 'FECHA  :' GET MI_FECHA WHEN CHOICE=2
@ 5,20 SAY 'N.R.   :' GET MI_TIPO WHEN CHOICE=3
@ 5,30 SAY '-' GET MI_NOTAR WHEN CHOICE=3
@ 7,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G
READ CYCLE

SELECT V_NE_HD
IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE ="SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO"
  ON KEY LABEL F10 KEYBOARD CHR(23)
  ON KEY LABEL F12 DO V_BEXPOR WITH "HD_NRO, HD_ALM, HD_FECHA, HD_OBS, HD_ESTADO", W_FILTRO
  DO CASE
   CASE choice = 1
     W_FILTRO="ALLTRIM(MI_NRO)$HD_NRO"
   CASE choice = 2
     W_FILTRO="HD_FECHA=MI_FECHA"
   CASE choice = 3
     W_FILTRO="HD_D_NRO=MI_NOTAR"
  ENDCASE
  BROWSE NOEDIT FOR EVAL(W_FILTRO) ;
     FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_OBS:H='OBSERVACIONES':P='@S30',HD_D_NRO:H='N.R.',HD_ESTADO:H='ESTADO' ;
     WINDOW W_NR TITLE W_TITLE
ENDIF

RELEASE WINDOW NR_BUSCA
DO V_ONKEYS
DO NR_VER
RETURN
