* MANTENIMIENTO DE NOTAS DE REMISION

DO V_INIMAN WITH "NOTAS_REM", 1

*VERIFICA DOSIFICACION ACTIVA
IF V_GETNDO()=0
  RETURN
ENDIF

ALM_CON_NE=V_GETPAR("ALM_CON_NE")

TIPO='R'
*
DO V_SETUP WITH "V_NR_CA.SPR", ""
DO LOC_SET

IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF

SALDO_CTA=0 &&PARA GUARDAR SALDO CTA CLIENTE

DO NR_VER

STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_DOCS
ENDDO

DO FIN

RETURN

*******************************************************************************
PROCEDURE LOC_SET
*****************
* INICIALIZA AMBIENTE
SELECT 1
USE V_DOC_HD ORDER NR
SELECT 2
USE V_DOC_LN ORDER NR
SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
USE V_ITEMS ORDER PART
SELECT 5
USE V_PED_HD ORDER CORRE
SELECT 6
USE V_PED_LN ORDER CORRE
SELECT 7
USE V_CORRE ORDER EMPRESA
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
USE V_DESCTO ORDER PART
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11
USE V_CATEGO ORDER CATEGO
SELECT 12
USE V_NE_HD ORDER NOTAR
SELECT 13
USE V_NE_LN ORDER NR
SELECT 14
USE V_TC ORDER FECHA

*PARA CALCULAR SALDO CTA

SELECT 15

*USE V_FAC_HD ORDER ESTADOS
*SET RELATION TO DTOS(HF_FECHA) INTO V_TC 

SELECT 16
*USE V_REC_HD ORDER ESTADOS
*SET RELATION TO DTOS(HR_FECHA) INTO V_TC 

SELECT V_DOC_LN
SET RELATION TO LD_EMPR+LD_PART INTO V_ITEMS

SELECT V_DOC_HD
SET RELATION TO HD_EMPR INTO V_EMPR
SET RELATION TO HD_EMPR+HD_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HD_EMPR+HD_COD_VEN INTO V_VENDOR ADDITIVE
SET RELATION TO HD_EMPR+HD_PEDIDO  INTO V_PED_HD ADDITIVE
SET RELATION TO DTOS(HD_FECHA)     INTO V_TC     ADDITIVE

***FILTROS GENERALES
SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA

SELECT V_DOC_HD
SET FILTER TO HD_EMPR=EMPRESA .AND. HD_TIPO=TIPO
GO TOP

***WINDOWS
DEFINE WINDOW W_NR FROM 0,0 TO 24,79 DOUBLE
DEFINE WINDOW NR_hd FROM 0, 0 TO 8,79 TITLE " NOTA DE REMISION " ;
 NOFLOAT DOUBLE 
DEFINE WINDOW NR_ln FROM 9, 0 TO 21,79 	TITLE "D E T A L L E" ;
 NOFLOAT DOUBLE 
DEFINE WINDOW NR_MSG FROM 21, 0 TO 24,79 TITLE "MENSAJES" ;
 NOFLOAT DOUBLE 

***MENUS
SELECT V_ITEMS
DEFINE POPUP GETITEM FROM 8,15 TO 20,70 ;
	PROMPT FIELD IT_PART+'-'+IT_DESCR ;
	TITLE 'SELECCIONE ITEM CON [ENTER]' 
ON SELECTION POPUP GETITEM DEACTIVATE POPUP
SELECT V_CLIENT
DEFINE POPUP GETCLIEN FROM 8,20 TO 20,60 ;
	PROMPT FIELD CL_RAZON+'-'+CL_COD_CL ;
	TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
ON SELECTION POPUP GETCLIEN DEACTIVATE POPUP
SELECT V_DOC_HD

*** Generamos Menu Principal
DO V_MANMU WITH .T.
RETURN

PROCEDURE NR_VER
****************
ALMACEN =SPACE(20)
SELECT V_DOC_HD
DO GET_ALM WITH HD_ALM

SELECT V_DOC_HD
DO V_NR_V.SPR

***IMPRIME IMAGEN DEL MENU INFERIOR
DO V_MANMU WITH .F. 

SELECT V_DOC_LN
GO TOP
KEYBOARD CHR(27)
BROWSE KEY V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO  ;
       FIELDS LD_PART:H='PART.#':W=.F.,V_ITEMS.IT_DESCR:H='DESCRIPCION',LD_CANT:H='CANTIDAD':P='999,999.99':W=.F.,V_ITEMS.IT_UNIDAD:H='UNI.',LD_PESO:H='kg. TOT.':P='99,999.99':W=.F.,LD_ESTADO:H='ESTADO':W=.F. WINDOW NR_LN  ;
       NOEDIT NOCLEAR NOAPPEND COLOR SCHEME 10

SELECT V_DOC_HD
RETURN

PROCEDURE DOC_REVI
******************
SELECT V_DOC_LN

DEFINE WINDOW NR_VERI FROM 9, 0 TO 24,79 DOUBLE 
DEFINE WINDOW NE_VER  FROM 4,18 TO 7,78 TITLE 'DESPACHOS' NOCLOSE DOUBLE

*** SOLO ALMACENES QUE USAN N.E.
IF V_DOC_HD.HD_ALM$ALM_CON_NE
  ACTIVATE WINDOW NE_VER        
ENDIF          
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO  ;
       FIELDS LD_PART:H='PART.#',V_ITEMS.IT_DESCR:H='DESCRIPCION',LD_CANT:H='CANTIDAD':P='999,999.99', ;
              V_ITEMS.IT_UNIDAD:H='UNI.',LD_PESO:H='kg. TOT.':P='99,999.99' ;
       WINDOW NR_VERI NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300 ;
       TITLE 'DETALLE  -  [F10] PARA SALIR' COLOR SCHEME 10 ;
       WHEN VER_NE(LD_EMPR,LD_TIPO,LD_NRO,LD_PART)

RELEASE WINDOWS NR_VERI, NE_VER

SELECT V_DOC_LN
DO V_ONKEYS
DO NR_VER
RETURN

FUNCTION VER_NE
***************
PARAMETER EMPRESA, TIPO, NR, PART

*** SOLO ALMACENES QUE USAN N.E.
IF .NOT. LD_ALM$ALM_CON_NE
  RETURN .T.
ENDIF
CLEAR
ACTUAL=SELECT()
NE_STRING=''
DESPACH=0

SELECT V_NE_HD    && ORDER NOTAR
SEEK EMPRESA+TIPO+NR
IF .NOT. EOF()
  DO WHILE .NOT. EOF() .AND. EMPRESA+TIPO+NR=HD_EMPR+HD_D_TIPO+HD_D_NRO
    NE_TIPO=HD_TIPO
    NE_NRO =HD_NRO
    SELECT V_NE_LN
    SEEK EMPRESA+NE_TIPO+NE_NRO+PART
    DO WHILE .NOT. EOF() .AND. LD_EMPR+LD_TIPO+LD_NRO+LD_PART=EMPRESA+NE_TIPO+NE_NRO+PART
      DESPACH=DESPACH+LD_CANT
      NE_STRING=ALLTRIM(NE_STRING)+LD_NRO+' '
      SKIP
    ENDDO   
    SELECT V_NE_HD
    SKIP
  ENDDO
ENDIF
SELECT (ACTUAL)
@ 0,0  SAY ' DESPACHADO: '+TRANSFORM(DESPACH,'9,999,999.99') COLOR G+/B
@ 0,30 SAY '  PENDIENTE: ' +TRANSFORM(V_DOC_LN.LD_CANT-DESPACH,'9,999,999.99') COLOR R+/B
@ 1,0  SAY '   EMPAQUES: '+NE_STRING COLOR W+/B
RETURN .T.

PROCEDURE DOC_SALE
******************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
*************
RELEASE MENU M_DOCS
RELEASE WINDOW NR_HD
RELEASE WINDOW NR_LN
RELEASE WINDOW NR_MSG
RELEASE WINDOW W_NR
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE DOC_ADIC
******************
MENSAJE1='LAS NOTAS DE REMISION SOLO PUEDEN SER GENERADAS A PARTIR DEL PEDIDO' 
MENSAJE2='GENERE LA N.R. EN EL MODULO DE PEDIDOS. GRACIAS.'
DO V_MENSAJ.SPR
RETURN

FUNCTION EXISTE_NE
******************
ACTUAL=SELECT()
SELECT V_NE_HD    && ORDER NOTAR
SET FILTER TO HD_ESTADO<>"A"
SEEK V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO
IF EOF()
  SET FILTER TO
  SELECT (ACTUAL)
  RETURN "*"   
ENDIF
NES=''
DO WHILE .NOT. EOF() AND HD_EMPR+HD_D_TIPO+HD_D_NRO=V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO
  NES=NES+HD_NRO+" " 
  SKIP
ENDDO
SET FILTER TO
SELECT (ACTUAL)
RETURN NES

******************
PROCEDURE DOC_ANUL
******************
IF .NOT. V_USRAUT(USER,"NOTAS_REM",3)
  RETURN
ENDIF

SELECT V_DOC_HD
NOTAS_E=EXISTE_NE()
IF NOTAS_E<>"*"
    MENSAJE1='LA NOTA DE REMISION NO PUEDE SER ANULADA' 
    MENSAJE2='YA FUE DESPACHADA CON N.E.: '+alltrim(NOTAS_E)
    DO V_MENSAJ.SPR
    RETURN
ENDIF

SELECT V_DOC_HD
IF HD_ESTADO='F' .OR. HD_FACTURA<>SPACE(6)
    MENSAJE1='LA NOTA DE REMISION NO PUEDE SER ANULADA' 
    MENSAJE2='!! YA FUE FACTURADA !!'
    DO V_MENSAJ.SPR
    RETURN
ENDIF

DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTA NR ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

IF SINO='S'

  SELECT V_DOC_HD
  DO REPONE_PE
  
  SELECT V_DOC_HD
  IF LOCK()
    REPLACE HD_ESTADO WITH 'A', ;
            HD_PEDIDO WITH SPACE(6), ;
            HD_IMPORTE WITH 0, ;
            HD_OBS WITH '* * * A N U L A D A * * *', ;
            HD_IMPORTE WITH 0, ;
            HD_U_ANUL WITH USER, ;
            HD_F_ANUL WITH DATE(), ;
            HD_MIGRA WITH ' '

    SELECT V_DOC_LN
    SEEK V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO
    IF .NOT. EOF() .AND. LOCK()
      REPLACE LD_ESTADO WITH 'A', ;
              LD_PART WITH SPACE(15), ;
              LD_COD_CL WITH SPACE(8), ;
              LD_COD_VEN WITH SPACE(30), ; 
              LD_CANT WITH 0, ;
              LD_PESO WITH 0, ;
              LD_COD_CL WITH SPACE(8)
      SKIP             
      DO WHILE .NOT. EOF() .AND. V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO=LD_EMPR+LD_TIPO+LD_NRO
        IF LOCK()
          DELETE
        ENDIF
        SKIP
      ENDDO
    ENDIF     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! LA NR HA SIDO ANULADO !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
  DO NR_VER  
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
RETURN

PROCEDURE REPONE_PE
*******************
SELECT V_DOC_LN
SEEK V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO

DO WHILE .NOT. EOF() .AND. V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO=LD_EMPR+LD_TIPO+LD_NRO
  WCANT=LD_CANT
  SELECT V_PED_LN
  SEEK V_DOC_LN.LD_EMPR+V_DOC_HD.HD_PEDIDO+V_DOC_LN.LD_PART
  IF .NOT. EOF() .AND. LOCK()
    REPLACE V_PED_LN.LP_PEND WITH V_PED_LN.LP_PEND+WCANT
    DO CASE 
      CASE V_PED_LN.LP_PEND=V_PED_LN.LP_CANT
        REPLACE V_PED_LN.LP_ESTADO WITH 'P'
    CASE V_PED_LN.LP_PEND<V_PED_LN.LP_CANT .AND. V_PED_LN.LP_PEND<>0
        REPLACE V_PED_LN.LP_ESTADO WITH 'I'
    CASE V_PED_LN.LP_PEND=0
        REPLACE V_PED_LN.LP_ESTADO WITH 'D'
    ENDCASE        
  ENDIF
  SELECT V_DOC_LN
  SKIP
ENDDO

SELECT V_PED_LN
ESTADO='D'
SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
   IF LP_ESTADO='I'
     ESTADO='I'
     EXIT
   ENDIF
   IF LP_ESTADO='P'
     ESTADO='P'
   ENDIF
   SKIP
ENDDO 
SELECT V_PED_HD
IF LOCK()
  REPLACE HP_ESTADO WITH ESTADO
ENDIF  
RETURN

*-----------------*
FUNCTION GET_ALM
*-----------------*
PARAMETER ALM
SELECT 0 
USE V_ALM ORDER ALM
SEEK EMPRESA+ALM
IF FOUND()
  ALMACEN=SUBSTR(AL_DESCR,1,20)
  USE
ELSE
  ALMACEN='ALMACEN NO EXISTE'
  USE
ENDIF    

PROCEDURE DOC_BUSC
******************
DEFINE WINDOW NR_BUSCA FROM 05,10 TO 20,70 DOUBLE TITLE 'BUSQUEDA DE N.R.' 
ACTIVATE WINDOW NR_BUSCA

* VARIBLES PARA BUSQUEDAS                                          
MI_NIT=SPACE(16)
MI_NR  =SPACE(6)
MI_CLI =SPACE(8)
MI_NROF=SPACE(6)
MI_NROP=SPACE(6)
MI_HDR =SPACE(6)
MI_RSOC=SPACE(30)

@ 1,1 GET choice FUNCTION '*RNV POR \<NRO;POR \<CODIGO O NIT;POR \<FACTURA;POR \<PEDIDO;POR \<HOJA DE RUTA;POR \<RAZON SOCIAL' ;
	SIZE 1,10,1 DEFAULT 1 && COLOR B/G
@ 1,24 GET MI_NR   PICTURE '999999'   WHEN CHOICE=1

@  3,24 GET MI_CLI WHEN CHOICE=2 AND MI_NIT=SPACE(16) 
@  3,33 SAY '-' GET MI_NIT WHEN CHOICE=2 AND MI_CLI=SPACE(8) 

@ 5,24 GET MI_NROF PICTURE '999999'   WHEN CHOICE=3
@ 7,24 GET MI_NROP PICTURE '999999'   WHEN CHOICE=4
@ 9,24 GET MI_HDR  PICTURE '999999'   WHEN CHOICE=5
@ 11,24 GET MI_RSOC PICTURE REPLICATE('X', 30)   WHEN CHOICE=6
@ 13,24 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G
READ CYCLE

SELECT V_DOC_HD 
IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE ="SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO"
  ON KEY LABEL F10 KEYBOARD CHR(23)
  ON KEY LABEL F12 DO V_BEXPOR WITH "HD_NRO, HD_ALM, HD_FECHA, HD_COD_CL, HD_OBS, HD_PEDIDO, HD_NDOSIF, HD_FACTURA, IMPORTE_BS(), HD_ESTADO", W_FILTRO
  DO CASE
   CASE choice = 1
     W_FILTRO="HD_TIPO=TIPO .AND. ALLTRIM(MI_NR)$HD_NRO"
   CASE choice = 2

     W_FILTRO=".F."
     IF MI_CLI<>SPACE(8)   
       W_FILTRO="ALLTRIM(MI_CLI)$HD_COD_CL" 
     ENDIF
     IF MI_NIT<>SPACE(16)
       W_FILTRO="ALLTRIM(MI_NIT)$V_CLIENT.CL_RUC"
     ENDIF

   CASE choice = 3
     W_FILTRO="ALLTRIM(MI_NROF)$HD_FACTURA"
     SET ORDER TO FACTURA
   CASE choice = 4
     W_FILTRO="ALLTRIM(MI_NROP)$HD_PEDIDO"
     SET ORDER TO PEDIDO
   CASE choice = 5
     W_FILTRO="ALLTRIM(MI_HDR)$HD_NRO_HDR"
   CASE choice = 6
     W_FILTRO="UPPER(ALLTRIM(MI_RSOC))$UPPER(V_CLIENT.CL_RAZON) .OR. UPPER(ALLTRIM(MI_RSOC))$UPPER(HD_OBS)"        
  ENDCASE

  BROWSE NOEDIT FOR EVAL(W_FILTRO) ;
     FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_COD_CL:H='COD.CLIE.', ;
     V_CLIENT.CL_RUC:H="NIT":16, V_CLIENT.CL_RAZON:H='RAZON SOCIAL':P='@S30', HD_OBS:H='OBSERVACIONES':P='@S30',HD_PEDIDO:H='PEDIDO',HD_NDOSIF:H='# DOSIF.', HD_FACTURA:H='FACTURA', ;
     IMPORTE=hd_importe-ROUND((hd_importe* hd_dscto_p)/100,2)+ hd_ped_fle+ hd_ped_emb+ hd_ped_otr:H="IMPORTE Bs.":P="99,999,999.99", ;
     HD_ESTADO:H='ESTADO' ;
     WINDOW W_NR TITLE W_TITLE
  SET ORDER TO NR
ENDIF

RELEASE WINDOW NR_BUSCA
DO V_ONKEYS
DO NR_VER
RETURN

PROCEDURE DOC_MOVR
******************
PARAMETER DIRECCION
SELECT V_DOC_HD

IF DIRECCION="?"
    DO V_IR_REG.SPR
    W_TITLE="SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO"
    W_FILTRO=".T."
    ON KEY LABEL F10 KEYBOARD CHR(23)
    ON KEY LABEL F12 DO V_BEXPOR WITH "HD_NRO, HD_ALM, HD_FECHA, HD_COD_CL, HD_OBS, HD_PEDIDO, HD_NDOSIF, HD_FACTURA, IMPORTE_BS(), HD_ESTADO", W_FILTRO
    BROWSE FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA', ;
       HD_COD_CL:H='COD.CLIE.',V_CLIENT.CL_RUC:H="NIT":16,HD_OBS:H='OBSERVACIONES':P='@S30',HD_PEDIDO:H='PEDIDO',HD_FACTURA:H='FACTURA',HD_NRO_HDR:H='H.D.R.', ;
       IMPORTE=hd_importe-ROUND((hd_importe* hd_dscto_p)/100,2)+ hd_ped_fle+ hd_ped_emb+ hd_ped_otr:H="IMPORTE Bs.":P="99,999,999.99", ;
       HD_ESTADO:H='ESTADO' ;
       TITLE 'SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO' ;
       NODELETE NOEDIT WINDOW W_NR
    DO V_ONKEYS 
ELSE
   DO V_CURMOV WITH "V_DOC_HD", DIRECCION
ENDIF   
DO NR_VER
RETURN

PROCEDURE DOC_OPCI
******************
DO V_NR_OP
RETURN

FUNCTION IMPORTE_BS
*******************
RETURN hd_importe-ROUND((hd_importe* hd_dscto_p)/100,2)+ hd_ped_fle+ hd_ped_emb+ hd_ped_otr