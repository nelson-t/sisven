***************************************************************************
* MANTENIMIENTO DE PROFORMAS
SET REFRESH TO 1
EMPRESA=' '
* VARIBLES PARA BUSQUEDAS                                          
MI_PED='      '
MI_CLI='        '
MI_OBS=SPACE(40)
CHOICE=1
*
SET COLOR SET TO plasmar
DO SET_AMBI
DO ON_KEYS
DO SET_EMPR
IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF
SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA
SELECT V_PRO_HD
SET FILTER TO HP_EMPR=EMPRESA
SEEK EMPRESA
DO PE_VER
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_PEDIDO
ENDDO

DO FIN

RETURN

************************************************************************
PROCEDURE SET_AMBI
******************
SET BLINK OFF
SET CLOCK ON
SET CONFIRM ON
SET EXCLU OFF
SET DATE TO DMY
SET DELE ON
SET TALK OFF
SET SAFETY OFF
SET ESCAPE OFF
SET EXACT OFF
SET AUTOSAVE ON
RETURN

PROCEDURE SET_EMPR
******************
* MUESTRA PANTALLA INICIAL E INICIALIZA AMBIENTE CON 'DO SETUP'
SET CURSOR OFF
DO CARATULA WITH 'ESPERE UN MOMENTO POR FAVOR !!'
DO SETUP
SET CURSOR ON
CLEAR
SELECT V_EMPR
EMPRESA=EM_EMPR
RETURN

PROCEDURE SETUP
***************
* INICIALIZA AMBIENTE
SELECT 1
USE V_PRO_HD ORDER CORRE
SELECT 2
USE V_PRO_LN ORDER CORRE
SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
USE V_ITEMS ORDER PART
SELECT 5
* AREA RESERVADA PARA CABEZERAS TEMPORALES DE PEDS./PROFORMAS/NR.
SELECT 6
* AREA RESERVADA PARA LINEAS TEMPORALES PEDS./PROFORMAS/NR.
SELECT 7
USE V_CORRE ORDER EMPRESA 
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
USE V_DESCTO ORDER PART
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11
USE V_CATEGO ORDER CATEGO
SELECT 12
USE V_TC ORDER FECHA &&DTOS(FECHA)

****************************************************
* Cขdigo adicionado por :  Martกn Fabbri
* Descripciขn : Area de trabajo que permitir
*               trabajar con la tabla V_AREAS
*****************************************************
SELECT 17
USE V_AREAS ORDER CODIGO

DEFINE POPUP GETAREA FROM 4,50 TO 10,78;
	PROMPT FIELD A_COD+'-'+A_DES ;
	TITLE 'SELECCIONE CON [ENTER]' 
ON SELECTION POPUP GETAREA DEACTIVATE POPUP
*****************************************************

SELECT V_PRO_LN
SET RELATION TO LP_EMPR+LP_PART INTO V_ITEMS

SELECT  V_PRO_HD
SET RELATION TO HP_EMPR INTO V_EMPR
SET RELATION TO HP_EMPR+HP_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HP_EMPR+HP_COD_VEN INTO V_VENDOR ADDITIVE
SET RELATION TO DTOS(HP_FECHA)     INTO V_TC ADDITIVE

SELECT V_ITEMS

DEFINE POPUP GETITEM FROM 8,15 TO 20,70 ;
	PROMPT FIELD IT_PART+'-'+IT_DESCR TITLE 'SELECCIONE ITEM CON [ENTER]' 
ON SELECTION POPUP GETITEM DEACTIVATE POPUP

SELECT V_CLIENT

DEFINE POPUP GETCLIEN FROM 8,20 TO 20,60 PROMPT FIELD ;
CL_RAZON+'-'+CL_COD_CL TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
ON SELECTION POPUP GETCLIEN DEACTIVATE POPUP

SELECT V_PRO_HD

DEFINE WINDOW W_PEDIDO FROM 0,0 TO 49,79 DOUBLE
DEFINE WINDOW ped_hd FROM 0, 0 TO 8,79 TITLE " P R O F O R M A " NOFLOAT DOUBLE 
DEFINE WINDOW ped_ln FROM 9, 0	TO 21,79	TITLE "D E T A L L E" NOFLOAT DOUBLE
DEFINE WINDOW ped_MSG	FROM 21, 0 TO 24,79 TITLE "MENSAJES" NOFLOAT DOUBLE 
DEFINE MENU M_PEDIDO 
DEFINE PAD PREV  OF M_PEDIDO PROMPT '<-\<PREV'   AT 23,01 SKIP FOR BOF("V_PRO_HD")
DEFINE PAD NEXT  OF M_PEDIDO PROMPT '\<SIG->'    AT 23,10 SKIP FOR EOF("V_PRO_HD")
DEFINE PAD OJEAR OF M_PEDIDO PROMPT '\<OJEA'     AT 23,18
DEFINE PAD BUSCA OF M_PEDIDO PROMPT '\<BUSCA'    AT 23,25
DEFINE PAD MIRA  OF M_PEDIDO PROMPT '\<VERIF'    AT 23,33
DEFINE PAD INFO  OF M_PEDIDO PROMPT 'IN\<FO'     AT 23,41
DEFINE PAD ALTA  OF M_PEDIDO PROMPT '\<ALTA'     AT 23,48
DEFINE PAD ANULA OF M_PEDIDO PROMPT 'AN\<UL'    AT 23,55
DEFINE PAD OPCION OF M_PEDIDO PROMPT 'OP\<CIONES' AT 23,62
DEFINE PAD SALE  OF M_PEDIDO PROMPT 'SA\<LE'    AT 23,73

*          1         2         3         4         5         6         7      
*01234567890123456789012345678901234567890123456789012345678901234567890123456789
*ษออออออออหอออออออหออออออหอออออออหอออออออหออออออหออออออหออออออหออออออออออหออออออป
*บ <-PREV บ SIG-> บ OJEA บ BUSCA บ VERIF บ INFO บ ALTA บ ANUL บ OPCIONES บ SALE บ
*ศออออออออสอออออออสออออออสอออออออสอออออออสออออออสออออออสออออออสออออออออออสออออออผ

ON SELECTION PAD PREV   OF M_PEDIDO DO PE_PREV
ON SELECTION PAD NEXT   OF M_PEDIDO DO PE_NEXT
ON SELECTION PAD OJEAR  OF M_PEDIDO DO PE_OJEAR
ON SELECTION PAD BUSCA  OF M_PEDIDO DO PE_BUSCA
ON SELECTION PAD MIRA   OF M_PEDIDO DO PE_MIRA
ON SELECTION PAD INFO   OF M_PEDIDO DO V_PR_INF.SPR
ON SELECTION PAD ALTA   OF M_PEDIDO && DO V_PRO_AL
ON SELECTION PAD ANULA  OF M_PEDIDO && DO PE_ANULA
ON SELECTION PAD OPCION OF M_PEDIDO DO V_PRO_OP
ON SELECTION PAD SALE   OF M_PEDIDO DO PE_SALE
RETURN

PROCEDURE PE_VER
********************
SELECT V_PRO_HD
T_FLETES=HP_FLETES
T_EMBALA=HP_EMBALA
T_OTROS=HP_OTROS
DO V_PRO_V.SPR
ACTIVATE SCREEN

@ 22,0 SAY "ษออออออออหอออออออหออออออหอออออออหอออออออหออออออหออออออหออออออหออออออออออหออออออป" COLOR W/B
@ 23,0 SAY "บ <-PREV บ SIG-> บ OJEA บ BUSCA บ VERIF บ INFO บ ALTA บ ANUL บ OPCIONES บ SALE บ" COLOR W/B
@ 24,0 SAY "ศออออออออสอออออออสออออออสอออออออสอออออออสออออออสออออออสออออออสออออออออออสออออออผ" COLOR W/B
SELECT V_PRO_LN
GO TOP

KEYBOARD CHR(27)
BROWSE KEY V_PRO_HD.HP_EMPR+V_PRO_HD.HP_NRO  ;
  FIELDS LP_PART:H='PART.#':W=.F.:12, ;
  V_ITEMS.IT_DESCR:H='DESCRIPCION':20, ;
  LP_CANT:H='CANTIDAD':P='99,999,999.99':W=.F., ;
  V_ITEMS.IT_UNIDAD:H='UNI.', ;
  LP_PRECIO:H='PRECIO U.':P='9,999.9999':W=.F., ;
  MONTO=LP_CANT*LP_PRECIO:P='99,999,999.99', ;
  LP_ESTADO:H='ESTADO':W=.F. ;
  WINDOW PED_LN NOEDIT NOCLEAR NOAPPEND COLOR SCHEME 10
RETURN

PROCEDURE CARATULA
******************
PARAMETER MENSAJE
CLEAR
DO V_PRO_CA.SPR
RETURN

PROCEDURE PE_NEXT
*****************
SELECT V_PRO_HD
IF .NOT. EOF()
   SKIP
ENDIF 
DO PE_VER
RETURN

PROCEDURE PE_PREV
*****************
SELECT V_PRO_HD
IF .NOT. BOF()
   SKIP -1
ENDIF 
DO PE_VER
RETURN

PROCEDURE PE_OJEAR
******************
SELECT V_PRO_HD
DO V_IR_REG.SPR
set display to vga50
ON KEY LABEL ENTER KEYBOARD CHR(23)
BROWSE KEY EMPRESA FIELDS HP_NRO:H='NRO.',HP_FECHA:H='FECHA',;
HP_COD_CL:H='COD.CLIE.',HP_OBS:H='OBSERVACIONES':P='@S30',;
IMPORTE=HP_IMPORTE-ROUND((HP_IMPORTE*HP_DSCTO_P)/100,2)+;
HP_FLETES+HP_EMBALA+HP_OTROS:H='IMPORTE Bs.':P='99,999,999.99';
NOEDIT WINDOW W_PEDIDO TITLE 'SELECCIONE PROFORMA CON [ENTER]';
 COLOR SCHEME 10 NODELETE
set display to vga25
ON KEY
DO ON_KEYS
DO PE_VER
RETURN

PROCEDURE PE_ANULA
******************
SELECT V_PRO_HD
DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTA PROFORMA ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ
IF SINO='S'
  IF LOCK()
    REPLACE HP_ESTADO WITH 'A'
    REPLACE HP_OBS WITH '** ANULADA **'
    REPLACE HP_IMPORTE WITH 0, HP_DSCTO_P WITH 0, HP_ESTADO WITH 'A'
    REPLACE HP_FLETES WITH 0, HP_EMBALA WITH 0, HP_OTROS WITH 0, ;
            HP_LUGAR1 WITH '', HP_LUGAR2 WITH '', HP_FORPAG WITH '', ;
            HP_TIENTR WITH '', HP_VALIDEZ WITH '', HP_DIBU1 WITH '', ;
            HP_DIBU2  WITH ''
    SELECT V_PRO_LN
    SEEK V_PRO_HD.HP_EMPR+V_PRO_HD.HP_NRO
    DO WHILE .NOT. EOF() .AND. V_PRO_HD.HP_EMPR+V_PRO_HD.HP_NRO=LP_EMPR+LP_NRO
      IF LOCK()
        DELETE 
      ENDIF
      SKIP
    ENDDO     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! LA PROFORMA HA SIDO ANULADA !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
  DO PE_VER  
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
RETURN

PROCEDURE PE_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_PEDIDO
RELEASE WINDOW PED_HD
RELEASE WINDOW PED_LN
RELEASE WINDOW PED_MSG
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE PE_CALCULA
********************
SEEK V_PRO_HD.HP_EMPR+V_PRO_HD.HP_NRO
SUM LP_CANT TO TOT_CANT REST WHILE V_PRO_HD.HP_EMPR+V_PRO_HD.HP_NRO=LP_EMPR+LP_NRO
RETURN

PROCEDURE ON_KEYS
*****************
DEACTIVATE MENU _SYSMENU
ON KEY LABEL F1 KEYBOARD ''
ON KEY LABEL F2 KEYBOARD ''
ON KEY LABEL F3 KEYBOARD ''
ON KEY LABEL F4 KEYBOARD ''
ON KEY LABEL F5 KEYBOARD ''
ON KEY LABEL F6 KEYBOARD ''
ON KEY LABEL F7 KEYBOARD ''
ON KEY LABEL F8 KEYBOARD ''
ON KEY LABEL F9 KEYBOARD ''
ON KEY LABEL F10 KEYBOARD ''
ON KEY LABEL F11 keyboard ''
ON KEY LABEL F12 keyboard ''
RETURN

FUNCTION GET_SALDO
******************
PARAMETER L_EMPR,L_ALM,L_PART
SELECT 0
USE V_DOC_LN ORDER KARDEX
SAL_IT=V_SALDOS(L_EMPR,L_ALM,L_PART)
USE
SELECT V_PRO_LN
RETURN SAL_IT 

PROCEDURE PE_MIRA
*****************
SELECT 0
USE V_ALM ORDER ALM
T_ALM=SYS(3)
COPY TO (T_ALM) WITH CDX FIELDS AL_EMPR,AL_ALM,AL_DESCR,AL_CANT FOR AL_EMPR=EMPRESA
USE (T_ALM) ORDER ALM ALIAS ALMACEN EXCLU

SELECT 0
USE V_DOC_LN ORDER KARDEX
SET RELATION TO LD_EMPR+LD_ALM INTO ALMACEN

SELECT V_PRO_LN

DEFINE WINDOW SAL_ALM FROM 1,44 TO 8,79 COLOR SCHEME 5 DOUBLE TITLE "SALDOS DEL ITEM" NOCLOSE

ACTIVATE WINDOW SAL_ALM

DEFINE WINDOW ped_VERI ;
		FROM 9, 0 ;
		TO 24,79 ;
		DOUBLE 

REG_ACT=0

ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_PRO_HD.HP_EMPR+V_PRO_HD.HP_NRO  ;
       FIELDS LP_PART:H='PART.#':W=CHKREG():12, ;
       V_ITEMS.IT_DESCR:H='DESCRIPCION':W=CHKREG(), ;
       LP_CANT:H='CANTIDAD':P='9,999,999.99':W=CHKREG(), ;
       V_ITEMS.IT_UNIDAD:H='U.':W=CHKREG(), ;
       LP_PRECIO:H='PREC. U.':P='9,999.99':W=CHKREG(), ;
       IMPORTE=LP_CANT*LP_PRECIO:H='IMPORTE':P='99,999,999.99':W=CHKREG(), ;
       PARTE=LP_PART:H='PART.#':W=CHKREG() ;
       WINDOW PED_VERI  ;
       NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  TITLE 'DETALLE  -  [F10] PARA SALIR ';
       COLOR SCHEME 10

SELECT V_DOC_LN
USE

SELECT ALMACEN
USE

DELETE FILE (T_ALM+'.DBF')
DELETE FILE (T_ALM+'.CDX')

SELECT V_PRO_LN

RELEASE WINDOW PED_VERI
RELEASE WINDOW SAL_ALM
DO ON_KEYS
DO PE_VER
RETURN

FUNCTION CHKREG
***************
SELECT V_PRO_LN
IF RECNO()<>REG_ACT
  =ALM_SALDO()  
  REG_ACT=RECNO()
ENDIF
RETURN .T.

FUNCTION ALM_SALDO
******************
PARTE=LP_PART
CLEAR
@ 0,0 SAY 'CALCULANDO SALDO : '+PARTE COLOR GR+/W

SELECT ALMACEN
REPLACE AL_CANT WITH 0 ALL

SELECT V_DOC_LN
SEEK EMPRESA+PARTE
DO WHILE .NOT. EOF() .AND. LD_EMPR=EMPRESA .AND. LD_PART=PARTE
  DO CASE
    CASE LD_INSAL='1'
      REPLACE ALMACEN.AL_CANT WITH ALMACEN.AL_CANT+LD_CANT
    CASE LD_INSAL='2'
      REPLACE ALMACEN.AL_CANT WITH ALMACEN.AL_CANT-LD_CANT
  ENDCASE 
  SKIP
ENDDO

SELECT ALMACEN
GO TOP
SEEK EMPRESA
CLEAR
@ 0,0 SAY ' ALMACEN  SALDO CANT   '
@ 1,0 SAY '-----------------------'
LINEA=2
DO WHILE .NOT. EOF() .AND. AL_EMPR=EMPRESA
  IF AL_CANT>0
    @ LINEA,0 SAY '   '+AL_ALM+'   '+TRANSFORM(AL_CANT,'9,999,999.99') COLOR G+/W
  ELSE
    @ LINEA,0 SAY '   '+AL_ALM+'   '+TRANSFORM(AL_CANT,'9,999,999.99') COLOR R/W
  ENDIF
  LINEA=LINEA+1
  SKIP
ENDDO

SELECT V_PRO_LN
RETURN .T.


PROCEDURE PE_BUSCA
******************
DEFINE WINDOW PE_BUSCA FROM 06,10 TO 15,74 DOUBLE TITLE 'BUSQUEDA DE PROFORMAS' 

ACTIVATE WINDOW PE_BUSCA
SELECT V_PRO_HD 
@ 1,1 GET CHOICE FUNCTION '*RNV POR \<NRO;POR \<CLIENTE;POR \<OBSERVACION' ;
	SIZE 1,10,1 COLOR SCHEME 1

@ 1,20 SAY ':' GET MI_PED PICTURE '999999'   WHEN CHOICE=1
@ 3,20 SAY ':' GET MI_CLI PICTURE '99999999' WHEN CHOICE=2
@ 5,20 SAY ':' GET MI_OBS WHEN CHOICE=3

@ 7,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1  COLOR SCHEME 1

READ CYCLE

IF okcancel = 1
  DO CASE
   CASE choice = 1
     SEEK EMPRESA+MI_PED
     IF EOF()
       CLEAR
       @ 2,5 SAY '         !!!!!  PROFORMA NO EXISTE  !!!!!         '  && COLOR GR+*/N+
       READ
       GO TOP
     ENDIF
   CASE choice = 2
     ON KEY LABEL F10 KEYBOARD CHR(23)
     ON KEY LABEL ENTER KEYBOARD CHR(23)
     set display to vga50
     SELECT V_PRO_HD
     BROWSE NOEDIT FOR HP_COD_CL=ALLTRIM(MI_CLI) ;
     FIELDS HP_NRO:H='NRO.',HP_COD_CL:H='COD.CLI.',HP_FECHA:H='FECHA',HP_OBS:H='OBSERVACIONES':P='@S30',HP_IMPORTE:H='IMPORTE', HP_ESTADO:H='ESTADO' ;
     WINDOW W_PEDIDO TITLE 'SELECCIONE PROFORMA CON [ENTER]';
     COLOR SCHEME 10
     set display to vga25
     ON KEY
     DO ON_KEYS
   CASE choice = 3
     ON KEY LABEL F10 KEYBOARD CHR(23)
     ON KEY LABEL ENTER KEYBOARD CHR(23)
     set display to vga50
     SELECT V_PRO_HD
     BROWSE NOEDIT FOR ALLTRIM(MI_OBS)$HP_OBS ;
     FIELDS HP_NRO:H='NRO.',HP_COD_CL:H='COD.CLI.',HP_FECHA:H='FECHA',HP_OBS:H='OBSERVACIONES':P='@S30',HP_IMPORTE:H='IMPORTE', HP_ESTADO:H='ESTADO' ;
     WINDOW W_PEDIDO TITLE 'SELECCIONE PROFORMA CON [ENTER]';
     COLOR SCHEME 10
     set display to vga25
     ON KEY
     DO ON_KEYS
  ENDCASE
ENDIF

RELEASE WINDOW PE_BUSCA
DO ON_KEYS
DO PE_VER
RETURN


