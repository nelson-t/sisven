******************
PROCEDURE V_RESTCL
******************
CLOSE DATA
CLEAR

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U" OR TYPE("INI_GEST")="U"
  DO V_GETEMP
ENDIF

MONEDA=1
OPCION=1
QUE_CODS=1
CODS_CL=SPACE(8)
STOTAL=0
CATEGO_CL=' '

FECHAI=INI_GEST
FECHAF=date()

DO V_RESTCL.SPR

CLEAR
@ 0,0 SAY 'PROCESANDO CLIENTES .....'

IF OPCION = 3 .OR. LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF  

SELECT 1
USE V_EMPR
SET ORDER TO EMPR && .CDX

SELECT 0
USE V_TC ORDER FECHA
SEEK DTOS(INI_GEST)
IF .not. EOF()
  TC_INI=TC_MONTO
ELSE
  TC_INI=0
ENDIF
USE

SELECT 2
USE V_CATEGO
SET ORDER TO CATEGO && .CDX

SELECT 3
USE V_CLIENT
SET ORDER TO CODIGO  && .CDX
SET RELATION TO CL_EMPR INTO V_EMPR
SET RELATION TO CL_EMPR+CL_CATEGO INTO V_CATEGO ADDITIVE

DO CASE
  CASE QUE_CODS=1
    SET FILTER TO CL_EMPR=EMPRESA 
  CASE QUE_CODS=2
    SET FILTER TO CL_EMPR=EMPRESA .AND. CL_COD_CL<>ALLTRIM(CODS_CL)
  CASE QUE_CODS=3
    SET FILTER TO CL_EMPR=EMPRESA .AND. CL_COD_CL=ALLTRIM(CODS_CL)
  CASE QUE_CODS=4
    SET FILTER TO CL_EMPR=EMPRESA .AND. CL_CATEGO=ALLTRIM(CATEGO_CL)  
ENDCASE      

GO TOP

EST_ARCH=SYS(3)

SELECT 4
USE V_ESTCLI  
copy struc to (est_arch) with cdx
USE (EST_ARCH) ORDER FECHA ALIAS ESTADOS

SELECT 5
USE V_FAC_HD ORDER ESTADOS
SET FILTER TO BETWEEN(HF_FECHA,INI_GEST,FECHAF)

SELECT 6
USE V_REC_HD ORDER ESTADOS
SET FILTER TO BETWEEN(HR_FECHA,INI_GEST,FECHAF)

SELECT 7
USE V_DOC_HD ORDER ESTADOS
SET FILTER TO BETWEEN(HD_FECHA,INI_GEST,FECHAF)

SELECT 8
USE V_TC ORDER FECHA

DBF_TEMP=SYS(3)

SELECT 9
CREATE TABLE (DBF_TEMP) (EMPR C(1), COD_CL C(8), SINIC N(10,2))
INDEX ON EMPR+COD_CL TAG CODIGO
USE
USE (DBF_TEMP) ORDER CODIGO ALIAS T_CLIENT EXCLU

SELECT V_FAC_HD
SET RELATION TO DTOS(HF_FECHA) INTO V_TC

SELECT V_REC_HD
SET RELATION TO DTOS(HR_FECHA) INTO V_TC  

SELECT V_DOC_HD
SET RELATION TO DTOS(HD_FECHA) INTO V_TC

SELECT V_CLIENT
GO TOP

DO WHILE .NOT. EOF()

  CODIGO  =CL_COD_CL
  RAZON   =CL_RAZON
  IF MONEDA=1
    SALD_INI=CL_SALD_AN
  ELSE
    SALD_INI=CL_SALD_AN+CL_DIF_AN
  ENDIF
  
  ? CL_COD_CL+'-'+CL_RAZON

  SELECT T_CLIENT  
  APPEND BLANK
  REPLACE EMPR WITH V_CLIENT.CL_EMPR, ;
          COD_CL WITH CODIGO

  SELECT ESTADOS
  APPEND BLANK
  IF LOCK()
    REPLACE COD_CLIE WITH V_CLIENT.CL_COD_CL, ;
            OBS   WITH '                             SALDO INICIO DE GESTION', ;
            FECHA WITH INI_GEST, ;
            DEBE  WITH IIF(SALD_INI>=0,SALD_INI,0), ;
            HABER WITH IIF(SALD_INI<0 ,-SALD_INI,0), ;
            TC WITH TC_INI
  ENDIF          

  SELECT V_FAC_HD
  SEEK V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL

  DO WHILE .NOT. EOF() .AND. V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL=HF_EMPR+HF_COD_CL
    IF V_TC.TC_MONTO=0
      WAIT "TIPO DE CAMBIO PARA ESTA FECHA ES CERO O NO EXISTE:"+DTOC(HF_FECHA) WINDOW TIMEOUT 3
      DO SALIR
    ENDIF

    IF HF_ESTADO<>'A' .AND. HF_TIPO<>'O'
      DO CASE
      CASE HF_TIPO='R'  && FACTURA AL CREDITO
         TTIPO='F.CR.'
         TDEBE=hf_importe-HF_DSCTO_M-ROUND(hf_importe*(HF_DSCTO_P/100),2)+hf_fletes+hf_embala+hf_otros 
         THABER=0 
      CASE HF_TIPO='X'  && NOTA DE CREDITO
         TTIPO='N.C.'
         TDEBE=0
         THABER=hf_importe
      CASE HF_TIPO='Y'  && NOTA DE DEBITO
         TTIPO='N.D.'
         TDEBE=HF_IMPORTE
         THABER=0
      ENDCASE

      SELECT ESTADOS
      APPEND BLANK
      IF LOCK()
        	REPLACE COD_CLIE WITH V_CLIENT.CL_COD_CL, ;
                    TIPO     WITH TTIPO, ;
                    OBS      WITH V_FAC_HD.HF_OBS, ;
                    FECHA    WITH V_FAC_HD.HF_FECHA, NRO WITH V_FAC_HD.HF_NRO, ;
                    DEBE     WITH TDEBE, HABER WITH THABER
        	IF V_FAC_HD.HF_TIPO='R' 
          		REPLACE TC WITH V_FAC_HD.HF_FACTOR
        	ELSE
          		REPLACE TC WITH V_TC.TC_MONTO
        	ENDIF
      ENDIF          
    ENDIF
    SELECT V_FAC_HD 
    SKIP
  ENDDO

  SELECT V_REC_HD
  
  SEEK V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL
  DO WHILE .NOT. EOF() .AND. V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL=HR_EMPR+HR_COD_CL
    IF V_TC.TC_MONTO=0
      WAIT "TIPO DE CAMBIO PARA ESTA FECHA ES CERO O NO EXISTE:"+DTOC(HR_FECHA) WINDOW TIMEOUT 3
      DO SALIR
    ENDIF

    IF HR_ESTADO<>'A' 
      TDEBE=0
      THABER=HR_TOT_BS

      SELECT ESTADOS
      APPEND BLANK
      IF LOCK()
        REPLACE COD_CLIE WITH V_CLIENT.CL_COD_CL, ;
                TIPO WITH 'RCBO.', ;
                OBS WITH "* * *  ABONO A SU CUENTA  * * *", ;
                FECHA WITH V_REC_HD.HR_FECHA, NRO WITH V_REC_HD.HR_NRO, ;
                DEBE WITH TDEBE, HABER WITH THABER, ;
                TC WITH V_TC.TC_MONTO
      ENDIF          
    ENDIF
    SELECT V_REC_HD
    SKIP
  ENDDO

  * SE INCLUYE N.R.
  
  SELECT V_DOC_HD

  SEEK V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL
  DO WHILE .NOT. EOF() .AND. V_CLIENT.CL_EMPR+V_CLIENT.CL_COD_CL=HD_EMPR+HD_COD_CL
      IF V_TC.TC_MONTO=0
        WAIT "TIPO DE CAMBIO PARA ESTA FECHA ES CERO O NO EXISTE:"+DTOC(HD_FECHA) WINDOW TIMEOUT 3
        DO SALIR
      ENDIF

      IF (HD_ESTADO=' ' .AND. HD_TIPO='R')
    	  SELECT ESTADOS
    	  APPEND BLANK
    	  IF LOCK()
    	    REPLACE COD_CLIE WITH V_CLIENT.CL_COD_CL
      		REPLACE TIPO  WITH "N.R." 
      		REPLACE OBS   WITH V_DOC_HD.HD_OBS
      		REPLACE FECHA WITH V_DOC_HD.HD_FECHA
      		REPLACE NRO   WITH V_DOC_HD.HD_NRO
      		REPLACE DEBE  WITH V_DOC_HD.hd_importe-ROUND((V_DOC_HD.hd_importe* V_DOC_HD.hd_dscto_p)/100,2)+ V_DOC_HD.hd_ped_fle+ V_DOC_HD.hd_ped_emb+ V_DOC_HD.hd_ped_otr
       		REPLACE TC 	  WITH V_TC.TC_MONTO
    	  ENDIF
        SELECT V_DOC_HD
      ENDIF
      SKIP
  ENDDO
 
  SELECT V_CLIENT 
  SKIP

ENDDO
  
DEFINE WINDOW VER_CLIE FROM 0,0 TO 24,79 DOUBLE TITLE 'ESTADO DE CUENTA DEL CLIENTE - [ESC] PARA SALIR'
ACTIVATE WINDOW VER_CLIE

SELECT ESTADOS
SET RELATION TO EMPRESA+COD_CLIE INTO V_CLIENT
SET RELATION TO DTOS(FECHA)      INTO V_TC ADDITIVE
SET RELATION TO EMPRESA+COD_CLIE INTO T_CLIENT ADDITIVE

SET DELE OFF

SELECT ESTADOS
GO TOP
*SE CALCULA SALDO INICIAL EN BS o USD
DO WHILE .NOT. EOF()
  IF FECHA >= INI_GEST .AND. FECHA < FECHAI
    REPLACE T_CLIENT.SINIC WITH T_CLIENT.SINIC+((DEBE-HABER)/ ROUND(IIF(MONEDA=1,1,TC),2))
    DELETE
  ENDIF   
  SKIP
ENDDO

SET DELE ON

SELECT ESTADOS
SET RELATION OFF INTO T_CLIENT

SELECT T_CLIENT

GO TOP
DO WHILE .NOT. EOF()
  IF SINIC<>0
    SELECT ESTADOS
    APPEND BLANK
    REPLACE COD_CLIE WITH T_CLIENT.COD_CL, ;
            OBS   WITH '                             SALDO ANTERIOR', ;
            FECHA WITH {01/01/01}, ;
            DEBE  WITH IIF(T_CLIENT.SINIC>=0,T_CLIENT.SINIC,0), ;
            HABER WITH IIF(T_CLIENT.SINIC<0 ,-T_CLIENT.SINIC,0), ;
            TC WITH 1
  ENDIF
  SELECT T_CLIENT
  SKIP
ENDDO

SELECT ESTADOS
GO TOP

REP_ARCH='..\TMP\'+SYS(3)+'.TXT'

IF OPCION=2
  REPORT FORM V_RESTCL FOR DEBE+HABER<>0 .AND. (BETWEEN(FECHA,FECHAI,FECHAF) .OR. FECHA={01/01/01}) TO (REP_ARCH)
  ! COPY &REP_ARCH LPT1:
ELSE
  REPORT FORM V_RESTCL TO FILE (REP_ARCH) FOR DEBE+HABER<>0 .AND. (BETWEEN(FECHA,FECHAI,FECHAF) .OR. FECHA={01/01/01})
  CLEAR
  MODI COMM (REP_ARCH) NOEDIT WINDOW VER_CLIE 
ENDIF
RELEASE WINDOW VER_CLIE
WAIT "PROCESO CONCLUIDO...SALIENDO." TIMEOUT 2
DELETE FILE (REP_ARCH)
DO SALIR
RETURN

PROCEDURE SALIR
***************
CLOSE DATA
DELETE FILE (EST_ARCH+'.DBF') 
DELETE FILE (EST_ARCH+'.CDX') 
DELETE FILE (DBF_TEMP+'.DBF')
DELETE FILE (DBF_TEMP+'.CDX')
RETURN TO MASTER

