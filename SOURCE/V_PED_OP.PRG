******************
PROCEDURE V_PED_OP
******************
OPCION=1
CON_LOGO=.F.

IF V_PED_HD.HP_ESTADO="A"
  WAIT "COTIZACION ANULADA ... NO SE PUEDE CONTINUAR" WINDOW TIMEOUT 2
  RETURN
ENDIF
 
DO V_PED_OP.SPR

DO CASE
  CASE LASTKEY()<>27 .AND. OPCION=1
*   IMPRIME COTIZACION CON PRECIOS
    FORMATO=1
    COPIA=1
    DO V_PED_OF.SPR

    IF LASTKEY()<>27
  
      DO CASE
        CASE FORMATO=1
          DO PED_IMPR WITH 'C'
        CASE FORMATO=2
          DO V_PEDHTM WITH "NO_SETUP", EMPRESA, V_PED_HD.HP_NRO, "COTIZACION", IIF(COPIA=1,"COPIA","ORIGINAL"), EMPCIUD 
      ENDCASE
    ENDIF

  CASE LASTKEY()<>27 .AND. OPCION=2
*   IMPRIME COTIZACION -- DETALLE SIN PRECIOS
    DO PED_IMPR WITH 'D'

  CASE LASTKEY()<>27 .AND. OPCION=3
*   IMPRIME PEDIDO CON PRECIOS
	IF V_PED_HD.HP_ESTADO='C'
    	MENSAJE1='ESTE PEDIDO NO ESTA CONFIRMADO... IMPRIMA COTIZACION !'
	    MENSAJE2='PRESIONE <OK> PARA CONTINUAR'
    	DO V_MENSAJ.SPR
		SELECT V_PED_HD    	
		RETURN
    ENDIF

    FORMATO=1
    COPIA=1
    DO V_PED_OF.SPR
    IF LASTKEY()<>27
      DO CASE
        CASE FORMATO=1
          DO PED_IMPR WITH 'P'
        CASE FORMATO=2
          DO V_PEDHTM WITH "NO_SETUP", EMPRESA, V_PED_HD.HP_NRO, "PEDIDO", IIF(COPIA=1,"COPIA","ORIGINAL"), EMPCIUD 
      ENDCASE
    ENDIF
          
  CASE LASTKEY()<>27 .AND. OPCION=4
*   IMPRIME SOLO PENDIENTES DEL PEDIDO
    DO PED_IMPR WITH 'E'
    
  CASE LASTKEY()<>27 .AND. OPCION=5 .AND. V_USRAUT(USER,"PEDIDOS",2)
*   CONFIRMA COTIZACION y CONVIERTE A PEDIDO
    DO CONFIRMA
    
  CASE LASTKEY()<>27 .AND. OPCION=6 .AND. V_USRAUT(USER,"PEDIDOS",2)
*   EMITE NOTA DE REMISION
    SELECT V_PED_HD
    IF HP_ESTADO$'PI' .AND. LASTKEY()<>27
	   MENSAJE1='ESTA UD. SEGURO QUE HAY SALDO PARA HACER LA N.R.'
       MENSAJE2='PRESIONE [ESC] PARA SALIR AHORA, <OK> PARA CONTINUAR'
	   DO V_MENSAJ.SPR
       DO V_PED_NR WITH 'V.CR.'
       DO CHK_FLAGS
	ELSE
	   MENSAJE1='NO SE PUEDE PROSEGUIR.'
       MENSAJE2='DEBE CONFIRMAR LA COTIZACION PRIMERO!'
       DO V_MENSAJ.SPR
    ENDIF

  CASE LASTKEY()<>27 .AND. OPCION=7 .AND. V_USRAUT(USER,"PEDIDOS",2)
*   EMITE NOTA DE REMISION Y FACTURA AL CONTADO
    SELECT V_PED_HD
    IF HP_ESTADO$'PI' .AND. LASTKEY()<>27
       MENSAJE1='ESTA UD. SEGURO QUE HAY SALDO PARA HACER LA N.R.'
       MENSAJE2='PRESIONE [ESC] PARA SALIR AHORA, <OK> PARA CONTINUAR'
       DO V_MENSAJ.SPR
       DO V_PED_NR WITH 'V.CO.'
       DO CHK_FLAGS
	ELSE
	   MENSAJE1='NO SE PUEDE PROSEGUIR.'
       MENSAJE2='DEBE CONFIRMAR LA COTIZACION PRIMERO!'
       DO V_MENSAJ.SPR
    ENDIF

  CASE LASTKEY()<>27 .AND. OPCION=8 .AND. V_USRAUT(USER,"PEDIDOS",2)
*   MODIFICA PEDIDO
    SELECT V_PED_HD
    IF V_PED_HD.HP_ESTADO<>'C'
      MENSAJE1='OPCION INVALIDA !'
      MENSAJE2='ESTE NO ES UN PEDIDO CON ESTADO = COTIZACION'
      DO V_MENSAJ.SPR
      RETURN
    ENDIF
    DO V_PED_MO
    DO CHK_FLAGS
    
  CASE LASTKEY()<>27 .AND. OPCION=9 .AND. V_USRAUT(USER,"PEDIDOS",3)
*   COMPLETA EL PEDIDO
    DO COMPLETA
    DO CHK_FLAGS

  CASE LASTKEY()<>27 .AND. OPCION=10 .AND. V_USRAUT(USER,"PEDIDOS",2)
*   REASIGNA PRECIOS DE LISTA A LA COTIZACION
    DO V_PED_RE    
 
  CASE LASTKEY()=27 .OR. OPCION=11 
*   SALE DEL MENU DE OPCIONES
ENDCASE

DO PE_VER    

RETURN

PROCEDURE PED_IMPR
******************
PARAMETER CUAL

SELECT V_PED_LN
SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO     
TEMP_FILE='..\TMP\'+SYS(3)+'.TXT'
_PADVANCE='LINEFEED'

DO CASE
  CASE CUAL='C'
	REPORT FORM V_PE_FO2 NOCONSOLE REST WHILE V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO TO FILE (TEMP_FILE)
  CASE CUAL='D' 
	REPORT FORM V_PE_FO3 NOCONSOLE REST WHILE V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO TO FILE (TEMP_FILE)
  CASE CUAL='P'
	REPORT FORM V_PE_FOR NOCONSOLE REST WHILE V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO TO FILE (TEMP_FILE)
  CASE CUAL='E'
    REPORT FORM V_PE_PEN NOCONSOLE REST FOR LP_PEND>0 WHILE V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO TO FILE (TEMP_FILE)
ENDCASE

_PADVANCE='FORMFEED'
   *! COPY &TEMP_FILE lpt1:
   *! DOSPRINTER.EXE &TEMP_FILE
IF CUAL='E'
   DEFINE WINDOW VER_REP FROM 0,0 TO 24,79 DOUBLE TITLE '[ESC] - PARA SALIR'
   MODI COMM &TEMP_FILE WINDOW VER_REP NOEDIT
   RELEASE WINDOW VER_REP    
   MENSAJE1='AHORA SE IMPRIMIRA EL DOCUMENTO...'
   MENSAJE2='PRESIONE <OK> PARA CONTINUAR, [ESC] PARA SALIR'
   DO V_MENSAJ.SPR
ENDIF

IF LASTKEY()<>27
   ! TYPE &TEMP_FILE > LPT1
   WAIT WINDOW "Imprimiendo...." TIMEOUT 1
   MENSAJE1='COTIZACION / PEDIDO NRO.:'+V_PED_HD.HP_NRO+' HA SIDO IMPRESO'
   MENSAJE2=''
   DO V_MENSAJ.SPR
ENDIF

DELETE FILE (TEMP_FILE)
SELECT V_PED_HD

RETURN

PROCEDURE COMPLETA
******************
SELECT V_PED_HD
IF NOT V_PED_HD.HP_ESTADO$'PI'
  MENSAJE1='OPCION INVALIDAD !'
  MENSAJE2='ESTE NO ES UN PEDIDO PENDIENTE O INCOMPLETO'
  DO V_MENSAJ.SPR
  RETURN
ENDIF
   
MENSAJE1='! ESTA UD. SEGURO DE COMPLETAR EL PEDIDO !' 
MENSAJE2='CANTIDADES PENDIENTES SE ELIMINAN. [ESC] PARA SALIR'
DO V_MENSAJ.SPR

SELECT V_PED_HD
IF HP_ESTADO='A' .OR. LASTKEY()=27
  RETURN
ENDIF  

*PASSWORD=SPACE(15)
*DO V_PASSWD.SPR
*IF PASSWORD<>'X0ITN541' .AND. PASSWORD<>'COMPLETA0'
*   MENSAJE1='CLAVE ERRONEA ... OPERACION NO AUTORIZADA !' 
*   MENSAJE2='PRESIONE <OK> PARA CONTINUAR'
*   DO V_MENSAJ.SPR
*   RETURN
*ENDIF

IMPORTE=0
IMPORTE_INI=HP_IMPORTE

IF LOCK()
  REPLACE HP_ESTADO WITH 'D'
  REPLACE HP_U_COM WITH SYS(0), HP_F_COM WITH DATE()
  REPLACE HP_PSW_COM WITH PASSWORD
  SELECT V_PED_LN
  SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
  DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
    IF LOCK()
      REPLACE LP_ESTADO WITH 'D', LP_CANT WITH LP_CANT-LP_PEND, LP_PEND WITH 0
      IF LP_CANT=0
        REPLACE LP_PART WITH SPACE(15)
        DELETE
      ELSE
        IMPORTE=IMPORTE+ROUND(LP_CANT*LP_PRECIO,2)
      ENDIF
    ENDIF
    SKIP
  ENDDO 

  SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
  IF .NOT. EOF()
    SELECT V_PED_HD
    REPLACE HP_IMPORTE WITH IMPORTE
    REPLACE HP_FLETES WITH HP_FLETES*(IMPORTE/IMPORTE_INI)
    REPLACE HP_EMBALA WITH HP_EMBALA*(IMPORTE/IMPORTE_INI)
    REPLACE HP_OTROS  WITH HP_OTROS*(IMPORTE/IMPORTE_INI)        
  ELSE    
    SELECT V_PED_HD
    REPLACE HP_ESTADO WITH 'A'
    REPLACE HP_OBS WITH '*** ANULADO ***'
    REPLACE HP_IMPORTE WITH 0, HP_DSCTO_P WITH 0, HP_ESTADO WITH 'A'
  ENDIF
 
ENDIF
SELECT V_PED_HD
UNLOCK
RETURN 
 
PROCEDURE CHK_FLAGS
*******************
*Verifica el estado de las l¡neas del Pedido y actualiza ESTADO
SELECT V_PED_HD
IF HP_ESTADO<>'A' AND HP_ESTADO<>'C'
  SELECT V_PED_LN
  ESTADO='D'
  SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
  DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
    IF LOCK()
      DO CASE
        CASE LP_PEND=0
          REPLACE LP_ESTADO WITH 'D'
        CASE LP_PEND=LP_CANT
          REPLACE LP_ESTADO WITH 'P'
        CASE LP_PEND<>LP_CANT
          REPLACE LP_ESTADO WITH 'I'
      ENDCASE
      IF LP_ESTADO='I'
        ESTADO='I'
        EXIT
      ENDIF
      IF LP_ESTADO='P'
        ESTADO='P'
      ENDIF
    ENDIF
    SKIP
  ENDDO 

 SELECT V_PED_HD
 IF LOCK()
   REPLACE HP_ESTADO WITH ESTADO
 ENDIF  

ENDIF
DO PE_VER
RETURN .T.

PROCEDURE CONFIRMA
******************
*   CONFIRMA COTIZACION
MENSAJE1='ESTA UD. SEGURO ?'
MENSAJE2='PRESIONE [ESC] PARA SALIR AHORA, <OK> PARA CONTINUAR'
DO V_MENSAJ.SPR
    
SELECT V_PED_HD

IF HP_ESTADO='C' .AND. LASTKEY()<>27
     
      REPLACE HP_ESTADO WITH 'P'
      REPLACE HP_USUARIO WITH SYS(0)
      REPLACE HP_FECHA WITH DATE()

      SELECT V_PED_LN
      ESTADO='P'
      SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
      DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
        IF LOCK()
           IF LP_ESTADO='C'
             REPLACE LP_ESTADO WITH 'P'
             REPLACE LP_PEND WITH LP_CANT
             REPLACE LP_FECHA WITH V_PED_HD.HP_FECHA
           ENDIF
        ENDIF
        SKIP
      ENDDO 
      WAIT "SE PROCESO LA PETICION ... SE GENERO EL PEDIDO" WINDOW TIMEOUT 3      

    ELSE
      
      IF HP_ESTADO='P' .AND. LASTKEY()<>27

        SELECT V_PED_LN
        DESPACH=.F.
        **BUSCA SI POR LO MENOS UN ITEM FUE DESPACHADO
        SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
        DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
          IF LP_ESTADO$'D' .OR. LP_CANT<>LP_PEND
            DESPACH=.T.
          ENDIF
          SKIP
        ENDDO
         
        IF .NOT. DESPACH
        
          SELECT V_PED_HD		
          REPLACE HP_ESTADO WITH 'C'
          REPLACE HP_USUARIO WITH SYS(0)
          REPLACE HP_FECHA WITH DATE()

          SELECT V_PED_LN
          ESTADO='C'
          SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
          DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
            IF LOCK()
              REPLACE LP_ESTADO WITH 'C'
              REPLACE LP_PEND WITH 0
              REPLACE LP_FECHA WITH V_PED_HD.HP_FECHA
            ENDIF
            SKIP
          ENDDO 
		  WAIT "SE PROCESO LA PETICION ... SE RESTAURO COTIZACION" WINDOW TIMEOUT 3      
        ELSE
		  WAIT "NO SE PROCESO LA PETICION ... EL PEDIDO YA SE DESPACHO" WINDOW TIMEOUT 3      
        ENDIF 
      ELSE
        WAIT "NO SE PROCESO LA PETICION ... PEDIDO ANULADO O DESPACHADAO" WINDOW TIMEOUT 3      
      ENDIF
ENDIF
