* PROGRAMA PARA SUGERIR REQUERIMIENTOS DE PRODUCCION
* POR SECTORES
* EN BASE AL MODELO DE INVENTARIOS

SAVE SCREEN TO PANTA
PUBLIC NRO_RP,TR,Q
STORE 0 TO Q,TR
NRO_RP=SPACE(9)
SET DELE ON
DEFINE WINDOW Z FROM 8,0 TO 21,79 TITLE '> [F4] - ADICIONA REGISTRO  [F8] - BORRA REGISTRO  [F10] - TERMINAR <'
DEFINE WINDOW R FROM 0,0 TO 24,79 TITLE '> REQUERIMIENTO DE PRODUCCION -- [F10] PARA TERMINAR <'

DO NUMERO_RP WITH EMPRESA,DEPART    &&** CALCULA SIGUIENTE NUMERO DE R.P.

 SELECT V_ITEMS 
 SET ORDER TO LINEA
  IF DPTO<>10
     SET FILTER TO IT_LINEA=ALLTRIM(LINEA)
  ENDIF
SELECT V_ITEMS
SET ORDER TO PART
GO TOP
STORE 0 TO T,C,VALOR
IF DPTO<>10
   DO RV_MESSA.SPR
   DO WHILE .NOT. EOF()
      REC=RECNO()
      DO CASE
         CASE DPTO<>10 .AND. OPC=1
           SELECT V_ITEMS
           IF IT_ABC = 'A'
              DO CALCULO
           ENDIF      
         CASE DPTO<>10 .AND. OPC=2
           SELECT V_PED_LN
           SEEK V_ITEMS.IT_EMPR+'P'+V_ITEMS.IT_PART
           IF FOUND().AND.LP_REQ_PR<>'R'
              DO CALCULO
           ENDIF
         CASE DPTO<>10 .AND. OPC=3
           DO CALCULO
      ENDCASE      
      SELECT V_ITEMS      
      GO REC
      SKIP
   ENDDO
   SELECT V_ITEMS
   SET FILTER TO
   RELEASE WINDOW MENSAJE
*   =SONIDOS(2)
ENDIF  &&** SI DPTO<>10

SELECT V_ITEMS
SET ORDER TO PART
SELECT RP
IF RECCOUNT()>0 .OR. DPTO=10
   ACTIVATE WINDOW RP_HD
   CLEAR
   SET RELATION OFF INTO V_ITEMS
   SET RELATION TO RP_EMPR+RP_PART INTO V_ITEMS ADDITIVE
   SELECT RP
   DO WHILE LASTKEY()<>23
      ON KEY LABEL F4  KEYBOARD CHR(14)
      ON KEY LABEL F8  DO BORRAR &&KEYBOARD CHR(20)
      ON KEY LABEL F10 KEYBOARD CHR(23)
      SET CARRY TO RP_NRO,RP_EMPR
      BROW FIELDS RP_PART:H='PARTE':6:P='!!!999':v=getitem(),DESCR=V_ITEMS.IT_DESCR:H='DESCRIPCION':R , ;
            UNIDAD=V_ITEMS.IT_UNIDAD:H='UNI':R, RP_CANT:H='CANTIDAD':R, ;
            RP_CAN_STO:H='P/CLIENTE':V=RETORNO(),total=RP_CANT+RP_CAN_STO:H='TOTAL RP',  ;
            SALDO=V_ITEMS.IT_SALDRP:H='SALDO', PEND=V_ITEMS.IT_PENDIEN:H='T.PENDIENTE' WINDOW Z noclear           
   ENDDO            
   REPLACE RP_NRO WITH NRO_RP ALL
   USE
   SELECT V_REQ_PR
   SET FILTER TO
   APPEND FROM (REQ_PR) FOR .NOT. DELETED()
   SELECT 10
   USE (REQ_PR) ALIAS RP
   COUNT TO NRO_RPS FOR .NOT. DELETED()
   IF NRO_RPS > 0
      SELECT V_RP_HD
      APPEND BLANK
      REPLACE HR_EMPR WITH EMPRESA, HR_FECHA WITH DATE(), ;
              HR_NRO WITH NRO_RP  &&HR_FECHA_I WITH (DATE()+TR)
      DO V_RP_GET.SPR
   
      SELECT RP
      GO TOP
      DO WHILE .NOT. EOF()
         TOTAL_RP=0
         SELECT V_PED_LN
         SEEK RP.RP_EMPR+'P'+RP.RP_PART
         IF FOUND()
            DO WHILE .NOT. EOF() .AND. TOTAL_RP<=(RP.RP_CANT+RP.RP_CAN_STO)
               TOTAL_RP=TOTAL_RP+LP_PEND
               IF LOCK()
                 REPLACE LP_REQ_PR WITH 'R'
               ENDIF
               SKIP
            ENDDO
         ENDIF
         SELECT RP
         SKIP
      ENDDO      
   ENDIF 
   
ELSE
   ?? REPLICATE(CHR(7),3)
   WAIT '   EL SISTEMA NO SUGIERE R.P. PARA LA LINEA = '+LINEA+'   ' WINDOW
ENDIF  
SELECT RP
DELETE ALL
SELECT V_ITEMS
   SET FILTER TO
SELECT V_REQ_PR
   SET FILTER TO
SELECT V_DOC_LN
   SET FILTER TO
SELECT V_RP_HD    
   SET FILTER TO
SELECT V_ITEMS
RESTORE SCREEN FROM PANTA
RETURN

PROCEDURE BORRAR
****************
SELECT RP
   DELETE
   KEYBOARD "{ESC}"
RETURN

PROCEDURE CALCULO
*****************   
      STORE 0 TO SALDO,SALDO_ANT,T_ING,T_SAL,DIAS_NC,ING_AVG,DS_TV,NRO_ING
      OK=.F.
      SELECT V_ITEMS
      FECHA_ANT=IT_FECH_UA
      TOT_CANT = get_ing_sal(IT_EMPR,IT_PART,IT_FECH_UA)
      IF OK     
         SELECT V_ITEMS
         GO REC
         COST_VAR = costo_var(IT_EMPR,IT_PART,IT_FECH_UA)
         SELECT V_ITEMS
         GO REC
         PED_PEN= get_ped_pen(IT_EMPR,IT_PART,IT_FECH_UA)
         SELECT V_DIAS
         SEEK DTOS(DATE())
         D_DEM=240/DI_HABILES
         SELECT V_ITEMS
         IF LOCK()
             REPLACE IT_SALDRP WITH (IT_SALDRP+TOT_CANT), IT_INGRESO WITH (IT_INGRESO+T_ING), ;
             IT_SALIDA  WITH (IT_SALIDA+T_SAL), IT_TI WITH IIF(IT_TI>0,(IT_TI+ING_AVG)/2,ING_AVG), ;
             IT_DS_TV  WITH DS_TV, IT_DIAS_TV WITH (IT_DIAS_TV+DIAS_NC), ;
             IT_DEMANDA WITH (T_SAL*D_DEM), IT_COSTO WITH COST_VAR, ;
             IT_PENDIEN WITH PED_PEN, IT_FECH_UA WITH FECHAF
         ENDIF          
  
        IF (IT_COSTO<>0).AND.(IT_TI<>0).AND.(IT_DIAS_TV<>0)
           DO CASE
              CASE LINEA='A'.OR.LINEA='P'
                 COSTO_CTE=25
                 Q=ROUND(SQRT((2*COSTO_CTE*IT_DEMANDA)/(IT_COSTO*.15))/10,0)*10
                 PP=Q/IT_TI*(IT_DS_TV+(IT_SALIDA/IT_DIAS_TV))
              CASE LINEA='X'.OR.LINEA='V'   
                 COSTO_CTE=10
                 Q=ROUND(SQRT((2*COSTO_CTE*IT_DEMANDA)/(IT_COSTO*.15))/10,0)*10
                 PP=Q/IT_TI*(IT_DS_TV+(IT_SALIDA/IT_DIAS_TV))
              CASE LINEA='C'.OR.LINEA='F'
                 COSTO_CTE=25
                 Q=ROUND(SQRT((2*COSTO_CTE*IT_DEMANDA)/(IT_COSTO*.15))/100,0)*100
                 PP=Q/IT_TI*(IT_DS_TV+(IT_SALIDA/IT_DIAS_TV))
              CASE LINEA='O'
                 COSTO_CTE=40
                 Q=ROUND(SQRT((2*COSTO_CTE*IT_DEMANDA)/(IT_COSTO*.15))/50,0)*50
                 PP=Q/IT_TI*(IT_DS_TV+(IT_SALIDA/IT_DIAS_TV))
              CASE LINEA='T'
                 COSTO_CTE=40
                 Q=ROUND(SQRT((2*COSTO_CTE*IT_DEMANDA)/(IT_COSTO*.15))/300,0)*300
                 PP=Q/IT_TI*(IT_DS_TV+(IT_SALIDA/IT_DIAS_TV))
           ENDCASE          
           TR=Q/IT_TI           &&** TIEMPO DE REPOSICION
           IF IT_SALDRP <= PP
              SELECT RP
              IF Q<>0
                 IF DPTO<>10
                    APPEND BLANK
                 ENDIF   
                 IF LOCK()
                    REPLACE RP_EMPR WITH V_ITEMS.IT_EMPR, ;
                    RP_NRO WITH NRO_RP,RP_PART WITH V_ITEMS.IT_PART, ;
                    RP_UNIDAD WITH V_ITEMS.IT_UNIDAD, RP_FECHA WITH FECHAF, ;
                    RP_CANT WITH Q
                 ENDIF      
              ENDIF        
           ENDIF
        ENDIF
      ENDIF  
RETURN   

FUNCTION get_ING_SAL
********************
PARAMETERS EMPRESA,PARTE,ULT_FECHA
S_I=.F.            
SW1=.T.         
SELECT V_DOC_LN
IF ULT_FECHA=INI_GEST
   OPERADOR = '>='
ELSE
   OPERADOR = '>'
ENDIF      
IF DPTO<>10
   SET FILTER TO LD_EMPR=EMPRESA .AND. LD_PART=PARTE ;
              .AND. LD_ESTADO<>'A' .AND. LD_FECHA &OPERADOR ULT_FECHA              
ELSE
   SET FILTER TO LD_EMPR=EMPRESA .AND. LD_PART=PARTE .AND. LD_ESTADO<>'A'
ENDIF
COUNT TO FILTRADO

IF FILTRADO > 0 &&.AND. VERIFICA()
   OK=.T.
   SEEK EMPRESA+PARTE
   IF FOUND()
      REG=RECNO()
      CALCULATE STD(LD_CANT),SUM(LD_CANT) WHILE LD_EMPR=EMPRESA .AND. ;
                LD_PART=PARTE FOR LD_INSAL='2'.AND.LD_TIPO<>'A'.AND.  ;
                LD_FECHA > ULT_FECHA TO DS_TV,T_SAL
      GO REG
      CALCULATE AVG(LD_CANT),SUM(LD_CANT) WHILE LD_EMPR=EMPRESA .AND. ;
                LD_PART=PARTE FOR LD_INSAL='1'.AND.LD_TIPO<>'A'.AND.  ;
                LD_FECHA > ULT_FECHA TO ING_AVG,T_ING
      GO REG
      DO WHILE .NOT. EOF() .AND. LD_EMPR=EMPRESA .AND. LD_PART=PARTE
         IF LD_FECHA &OPERADOR ULT_FECHA         
            IF LD_ALM=ALLTRIM(ALMACEN)
               IF LD_INSAL='1'
                  SALDO = SALDO + LD_CANT
               ELSE
                  SALDO = SALDO - LD_CANT
               ENDIF      
            ENDIF     
            DO FECHAS WITH LD_FECHA
         ENDIF
         SKIP 
      ENDDO
      SKIP -1
      DO FECHAS WITH FECHAF
   ENDIF
ENDIF
RETURN SALDO

PROCEDURE FECHAS
****************
PARAMETERS FECHA_CAL
   IF SW1
      SALDO_ANT=SALDO
      SW1=.F.
   ENDIF
   IF (SALDO >= 0) 
      IF SALDO_ANT=0
         FECHA_ANT=FECHA_CAL
      ENDIF   
      IF FECHA_CAL >= FECHA_ANT .AND. SALDO_ANT<>0
         SELECT V_DIAS
         SEEK DTOS(FECHA_ANT)
            DH1=DI_HABILES    
         SEEK DTOS(FECHA_CAL) 
            DH2=DI_HABILES
         DIAS_NC=DIAS_NC+(DH2-DH1)
      ENDIF
   ENDIF
   FECHA_ANT=FECHA_CAL
   SALDO_ANT=SALDO
SELECT V_DOC_LN
RETURN    

FUNCTION COSTO_VAR
******************
PARAMETERS EMPRESA,PARTE,ULT_FECHA
STORE 0 TO COSTO
SELECT V_FORMUL
SEEK EMPRESA+PARTE
DO WHILE .NOT. EOF() .AND. FO_EMPR=EMPRESA .AND. FO_PART=PARTE
	 COSTO=COSTO+((V_MATER.MA_PRECIO*V_FORMUL.FO_PORCEN*V_ITEMS.IT_PESO_T)/100)
     SKIP
ENDDO
SELECT V_ITEMS
RETURN COSTO

FUNCTION GET_PED_PEN
********************
PARAMETERS EMPRESA,PARTE,ULT_FECHA
STORE 0 TO PENDIENTE
SELECT V_PED_LN
SEEK EMPRESA+'P'+PARTE
IF FOUND()
  CALCULATE SUM(LP_PEND) WHILE LP_EMPR=EMPRESA .AND. LP_PART=PARTE FOR ;
            LP_ESTADO='I'.OR. LP_ESTADO='P' TO PENDIENTE  
ENDIF
RETURN PENDIENTE

PROCEDURE NUMERO_RP
*******************
PARAMETERS EMPRESA,GRUPO
SELECT V_RP_HD
SEEK EMPRESA+GRUPO
   IF FOUND()
      SET FILTER TO HR_EMPR=EMPRESA .AND. HR_NRO=ALLTRIM(GRUPO)
      GO BOTT
      LAST_RP=HR_NRO
      NRO_RP=ALLTRIM(STR(VAL(SUBSTR(LAST_RP,4,3))+1))
      ANO_RP=SUBSTR(LAST_RP,8,2)
      NEW_YEAR=SUBSTR(DTOC(DATE()),7,2)
      DO CASE
        CASE LEN(NRO_RP)=1
           NRO_RP=GRUPO+'-'+'00'+NRO_RP+'/'+ANO_RP
         CASE LEN(NRO_RP)=2
           NRO_RP=GRUPO+'-'+'0'+NRO_RP+'/'+ANO_RP
         CASE LEN(NRO_RP)=3
           NRO_RP=GRUPO+'-'+NRO_RP+'/'+ANO_RP
     ENDCASE
   ELSE
      ANO_RP=SUBSTR(DTOC(DATE()),7,2)
      NEW_YEAR=SUBSTR(DTOC(DATE()),7,2)
      NRO_RP=GRUPO+'-'+'001'+'/'+ANO_RP          
   ENDIF  
   IF ANO_RP<>NEW_YEAR
      NRO_RP=GRUPO+'-'+'001'+'/'+NEW_YEAR
   ENDIF      
RETURN

FUNCTION ganth
**************
PARAMETERS VALOR
col=(SCOL()-52)/2
IF valor <=1
  PUBLIC PANTALLA 
  SAVE SCREEN TO PANTALLA
  @ 21,COL TO 23,66 COLOR W+/B
ELSE
  @22,col+1 SAY REPLICATE(CHR(219),valor/2) COLOR GR+/B
ENDIF
RETURN .T.

FUNCTION GETITEM
****************
REPLACE RP.RP_EMPR WITH EMPRESA
SELECT V_ITEMS
SEEK rp.rp_empr+rp.rp_part
IF .NOT. FOUND()
   ? CHR(7)
   WAIT 'ITEM NO ENCONTRADO !!!' WINDOW NOWAIT
   SELECT RP
   RETURN (.F.)
ELSE
   REC=RECNO()
   IF DPTO=10
      DO CASE
         CASE SUBSTR(RP.RP_PART,1,2)='A'
            DEPART='MM'
         CASE SUBSTR(RP.RP_PART,1,2)='C'
            DEPART='CO'
         CASE SUBSTR(RP.RP_PART,1,2)='F'
            DEPART='FB'
         CASE SUBSTR(RP.RP_PART,1,2)='P'
            DEPART='PS'
         CASE SUBSTR(RP.RP_PART,1,2)='TP'
            DEPART='PO'
         CASE SUBSTR(RP.RP_PART,1,2)='T'
            DEPART='TU'
         CASE SUBSTR(RP.RP_PART,1,2)='X'
            DEPART='AC'
         CASE SUBSTR(RP.RP_PART,1,2)='V'
            DEPART='VA'
         CASE SUBSTR(RP.RP_PART,1,2)='V'
            DEPART='VA'
      ENDCASE
      DO NUMERO_RP WITH RP.RP_EMPR,DEPART
   ENDIF   
   DO RV_MESSA.SPR
   RELEASE WINDOW mensaje
ENDIF   
SELECT RP
RETURN (.T.)

FUNCTION RETORNO
****************
KEYBOARD "{BACKTAB}"
KEYBOARD "{DNARROW}" 
RETURN

FUNCTION VERIFICA
*****************
   SELECT V_REQ_PR
   SET FILTER TO RP_PART=V_ITEMS.IT_PART
   COUNT TO NUM_RPS
   IF NUM_RPS > 0
      GO BOTT
      ULT_RP=RP_NRO
      SELECT V_DOC_LN
      SET ORDER TO RP
      SEEK EMPRESA+NRO_PARTE+ULT_RP
      IF FOUND()
         SET ORDER TO KARDEX
         RETURN (.T.)
      ELSE 
         SET ORDER TO KARDEX
         RETURN (.F.)
      ENDIF
   ELSE
      RETURN (.T.)
   ENDIF