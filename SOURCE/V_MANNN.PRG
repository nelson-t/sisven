***************************************************************************
* MANTENIMIENTO DE NOTAS DE REMISION
MI_EMPRESA=SPACE(30)
TIPO='N'
EMPRESA=' '
*                                          
SET COLOR SET TO plasmar
DO SET_AMBI
DO ON_KEYS
DO SET_EMPR
IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF

SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA
SELECT V_DOC_HD
SET FILTER TO HD_EMPR=EMPRESA .AND. HD_TIPO=TIPO

SEEK EMPRESA
DO NR_VER
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
   ACTIVATE MENU M_NR
ENDDO

DO FIN

RETURN

*******************************************************************************

PROCEDURE SET_AMBI
******************
SET BLINK OFF
SET CLOCK ON
SET CONFIRM ON
SET EXCLU OFF
SET DATE TO DMY
SET DELE ON
SET TALK OFF
SET SAFETY OFF
SET ESCAPE OFF
SET EXACT OFF
RETURN

PROCEDURE SET_EMPR
******************
*MUESTRA PANTALLA INICIAL E INICIALIZA AMBIENTE CON 'DO SETUP'
SET CURSOR OFF
DO CARATULA WITH 'ESPERE UN MOMENTO POR FAVOR !!'
S_INI=SECONDS()
DO SETUP
S_FIN=SECONDS()
IF S_FIN-S_INI<2
  @ 23,0  
  WAIT '' TIMEOUT 2-(S_FIN-S_INI)
ENDIF
SET CURSOR ON
CLEAR
SELECT V_EMPR
COUNT TO COUNTER
IF COUNTER=1
  GO TOP
  EMPRESA=EM_EMPR
ELSE
  * MUESTRA EMPRESAS
  @ 6,5,19,78 BOX REPLICATE("ฐ",8)+"ฐ"
  @ 6,5 FILL TO 19,78 COLOR N+/N
  DEFINE WINDOW MI_EMPR FROM 5,3 TO 18,76 DOUBLE FILL CHR(177) TITLE '[ESC] PARA SALIR' 
  ACTIVATE WINDOW MI_EMPR
  @ 3,0
  GO TOP
  COUNTER=0
  DO WHILE .NOT. EOF()
    @ 3+counter,33 SAY ' '+EM_EMPR+'-'+EM_DESCR COLOR W/B
    counter=counter+1
    SKIP
  ENDDO 
  @ 2,32 TO 3+COUNTER,65 DOUBLE COLOR W/B
  
  * PIDE Y VERIFICA EMPRESA
  DO WHILE .T. .AND. LASTKEY()<>27
    @ 3, 7 SAY ' SELECCIONE EMPRESA:' GET EMPRESA PICTURE '9' 	COLOR W/B
    READ
    SEEK EMPRESA  
    IF .NOT. EOF()
      EXIT
    ENDIF
  ENDDO
  RELEASE WINDOW MI_EMPR  
  CLEAR
ENDIF  
RETURN

PROCEDURE SETUP
***************
* INICIALIZA AMBIENTE
SELECT 1
USE V_DOC_HD ORDER NR
SELECT 2
USE V_DOC_LN ORDER NR
SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
USE V_ITEMS ORDER PART
SELECT 5
USE V_PED_HD ORDER CORRE
SELECT 6
USE V_PED_LN ORDER CORRE
SELECT 7
USE V_CORRE ORDER EMPRESA
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
USE V_DESCTO ORDER PART
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11
USE V_CATEGO ORDER CATEGO

SELECT 15
* AREA RESERVADA PARA USO TEMPORAL DE CABEZAS DE FACT. 
SELECT 16
* AREA RESERVADA PARA USO TEMPORAL DE LINEAS DE FACT. 

SELECT 17
USE V_TC ORDER FECHA

SELECT V_DOC_LN
SET RELATION TO LD_EMPR+LD_PART INTO V_ITEMS
SET RELATION TO LD_EMPR+LD_NRO+LD_PART INTO V_PED_LN ADDITIVE

SELECT  V_DOC_HD
SET RELATION TO HD_EMPR INTO V_EMPR
SET RELATION TO HD_EMPR+HD_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HD_EMPR+HD_COD_VEN INTO V_VENDOR ADDITIVE
SET RELATION TO HD_EMPR+HD_PEDIDO  INTO V_PED_HD ADDITIVE

SELECT V_ITEMS

DEFINE POPUP GETITEM FROM 8,15 TO 20,70 ;
	PROMPT FIELD IT_PART+'-'+IT_DESCR ;
	TITLE 'SELECCIONE ITEM CON [ENTER]' 
ON SELECTION POPUP GETITEM DEACTIVATE POPUP

SELECT V_CLIENT

DEFINE POPUP GETCLIEN FROM 8,20 TO 20,60 ;
	PROMPT FIELD CL_RAZON+'-'+CL_COD_CL ;
	TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
ON SELECTION POPUP GETCLIEN DEACTIVATE POPUP

SELECT V_DOC_HD

DEFINE WINDOW W_NR FROM 0,0 TO 49,79 DOUBLE

DEFINE WINDOW NR_hd ;
		FROM 0, 0 ;
		TO 8,79 ;
		TITLE " NOTA DE REMISION " ;
		NOFLOAT ;
		DOUBLE 

DEFINE WINDOW NR_ln ;
		FROM 9, 0 ;
		TO 21,79 ;
		TITLE "D E T A L L E" ;
		NOFLOAT ;
		DOUBLE 
		
DEFINE WINDOW NR_MSG ;
		FROM 21, 0 ;
		TO 24,79 ;
		TITLE "MENSAJES" ;
		NOFLOAT ;
		DOUBLE 
	
DEFINE MENU M_NR 

DEFINE PAD PREV OF M_NR PROMPT '<- \<PREV'     AT 23,01 SKIP FOR BOF("V_DOC_HD")
DEFINE PAD NEXT OF M_NR PROMPT '\<SIG ->'      AT 23,11 SKIP FOR EOF("V_DOC_HD")
DEFINE PAD OJEAR OF M_NR PROMPT '\<OJEA'       AT 23,20
DEFINE PAD BUSCA OF M_NR PROMPT '\<BUSCA'      AT 23,27
DEFINE PAD MIRA  OF M_NR PROMPT '\<VERIFICA'   AT 23,35
DEFINE PAD ALTA  OF M_NR PROMPT '\<ALTA'       AT 23,46 
DEFINE PAD ANULA OF M_NR PROMPT 'AN\<UL'       AT 23,53
DEFINE PAD OPCION OF M_NR PROMPT 'OP\<CIONES'  AT 23,60
DEFINE PAD SALE  OF M_NR PROMPT 'SA\<LIR '     AT 23,71

*          1         2         3         4         5         6         7      
*01234567890123456789012345678901234567890123456789012345678901234567890123456789
*ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป
*บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ ANUL บ OPCIONES บ SALIR  บ
*ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ

ON SELECTION PAD PREV   OF M_NR DO NR_PREV
ON SELECTION PAD NEXT   OF M_NR DO NR_NEXT
ON SELECTION PAD OJEAR  OF M_NR DO NR_OJEAR
ON SELECTION PAD BUSCA  OF M_NR DO NR_BUSCA
ON SELECTION PAD MIRA   OF M_NR DO NR_MIRA
ON SELECTION PAD ALTA   OF M_NR DO NR_ALTA
ON SELECTION PAD ANULA  OF M_NR DO NR_ANULA
ON SELECTION PAD OPCION OF M_NR 
ON SELECTION PAD SALE   OF M_NR DO NR_SALE
RETURN

PROCEDURE NR_VER
********************
ALMACEN =SPACE(20)
SELECT V_DOC_HD
DO GET_ALM    WITH HD_ALM
SELECT V_DOC_HD
DO V_NR_V.SPR
ACTIVATE SCREEN
@ 22,0 SAY "ษอออออออออหออออออออหออออออหอออออออหออออออออออหออออออหออออออหออออออออออหออออออออป"
@ 23,0 SAY "บ <- PREV บ SIG -> บ OJEA บ BUSCA บ VERIFICA บ ALTA บ BAJA บ OPCIONES บ SALIR  บ"
@ 24,0 SAY "ศอออออออออสออออออออสออออออสอออออออสออออออออออสออออออสออออออสออออออออออสออออออออผ"
SELECT V_DOC_LN
GO TOP
KEYBOARD CHR(27)
BROWSE KEY V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO  ;
       FIELDS LD_PART:H='PART.#':W=.F.,V_ITEMS.IT_DESCR:H='DESCRIPCION',LD_CANT:H='CANTIDAD':P='999,999.99':W=.F.,V_ITEMS.IT_UNIDAD:H='UNI.',LD_PESO:H='kg. TOT.':P='99,999.99':W=.F.,LD_ESTADO:H='ESTADO':W=.F. WINDOW NR_LN  ;
       NOEDIT NOCLEAR NOAPPEND COLOR SCHEME 10

RETURN

PROCEDURE NR_MIRA
*****************
SELECT V_DOC_LN
DEFINE WINDOW NR_VERI ;
		FROM 9, 0 ;
		TO 24,79 ;
		DOUBLE 
		
ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO  ;
       FIELDS LD_PART:H='PART.#',V_ITEMS.IT_DESCR:H='DESCRIPCION',LD_CANT:H='CANTIDAD':P='999,999.99',V_ITEMS.IT_UNIDAD:H='UNI.',LD_PESO:H='kg. TOT.':P='99,999.99' WINDOW NR_VERI  ;
       NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  TITLE 'DETALLE  -  [F10] PARA SALIR' ;
       COLOR SCHEME 10
RELEASE WINDOW NR_VERI
DO ON_KEYS
DO NR_VER
RETURN

PROCEDURE CARATULA
******************
PARAMETER MENSAJE
CLEAR
DO V_NR_CA.SPR
RETURN

PROCEDURE NR_NEXT
*****************
SELECT V_DOC_HD
IF .NOT. EOF()
   SKIP
ENDIF 
DO NR_VER
RETURN

PROCEDURE NR_PREV
*****************
SELECT V_DOC_HD
IF .NOT. BOF()
   SKIP -1
ENDIF 
DO NR_VER
RETURN

PROCEDURE NR_OJEAR
******************
SELECT V_DOC_HD
DO V_IR_REG.SPR
set display to vga50
ON KEY LABEL ENTER KEYBOARD CHR(23)
BROWSE KEY EMPRESA FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_COD_CL:H='COD.CLIE.',HD_OBS:H='OBSERVACIONES':P='@S30',HD_PEDIDO:H='PEDIDO',HD_FACTURA:H='FACTURA',HD_ESTADO:H='ESTADO' NOEDIT WINDOW W_NR TITLE 'SELECCIONE N.R. CON [ENTER]' ;
       COLOR SCHEME 10
set display to vga25
ON KEY
DO ON_KEYS
DO NR_VER
RETURN

PROCEDURE NR_BUSCA
******************
DEFINE WINDOW NR_BUSCA FROM 08,10 TO 19,70 DOUBLE TITLE 'BUSQUEDA DE N.R.' 

ACTIVATE WINDOW NR_BUSCA
SELECT V_DOC_HD 
@ 1,1 GET choice FUNCTION '*RNV POR \<NRO;POR \<CLIENTE;POR \<FACTURA;POR \<PEDIDO' ;
	SIZE 1,10,1 DEFAULT 1 && COLOR B/G

MI_NR  ='      '
MI_CLI ='        '
MI_NROF='      '
MI_NROP='      '

@ 1,20 GET MI_NR   PICTURE '999999'   WHEN CHOICE=1
@ 3,20 GET MI_CLI  PICTURE '99999999' WHEN CHOICE=2
@ 5,20 GET MI_NROF PICTURE '999999'   WHEN CHOICE=3
@ 7,20 GET MI_NROP PICTURE '999999'   WHEN CHOICE=4
@ 9,20 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
	SIZE 1,10,1 && COLOR B/G

READ CYCLE

IF okcancel = 1
  DO CASE
   CASE choice = 1
     MI_NR=EMPRESA+TIPO+MI_NR
     SEEK MI_NR
     IF EOF()
       CLEAR
       @ 2,5 SAY '         !!!!!  NR NO EXISTE  !!!!!         '  && COLOR GR+*/N+
       READ
       GO TOP
     ENDIF
   CASE choice = 2
     ON KEY LABEL F10 KEYBOARD CHR(23)
     ON KEY LABEL ENTER KEYBOARD CHR(23)
     set display to vga50
     SELECT V_DOC_HD
     BROWSE NOEDIT FOR HD_COD_CL=MI_CLI ;
     FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_COD_CL:H='COD.CLIE.',HD_OBS:H='OBSERVACIONES':P='@S30',HD_PEDIDO:H='PEDIDO',HD_FACTURA:H='FACTURA',HD_ESTADO:H='ESTADO' ;
     WINDOW W_NR TITLE 'SELECCIONE NR CON [ENTER]'
     set display to vga25
     ON KEY
   CASE choice = 3
     ON KEY LABEL F10 KEYBOARD CHR(23)
     ON KEY LABEL ENTER KEYBOARD CHR(23)
     set display to vga50
     
     SELECT V_DOC_HD
     SET ORDER TO FACTURA
     GO TOP
     BROWSE NOEDIT KEY EMPRESA+MI_NROF ;
     FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_COD_CL:H='COD.CLIE.',HD_OBS:H='OBSERVACIONES':P='@S30',HD_PEDIDO:H='PEDIDO',HD_FACTURA:H='FACTURA',HD_ESTADO:H='ESTADO' ;
     WINDOW W_NR TITLE 'SELECCIONE NR CON [ENTER]'
     SET ORDER TO CORRE
     set display to vga25
     ON KEY
   CASE choice = 4
     ON KEY LABEL F10 KEYBOARD CHR(23)
     ON KEY LABEL ENTER KEYBOARD CHR(23)
     set display to vga50
     SELECT V_DOC_HD
     SET ORDER TO PEDIDO
     GO TOP
     BROWSE NOEDIT KEY EMPRESA+MI_NROP ;
     FIELDS HD_NRO:H='NRO.',HD_ALM:H='ALM',HD_FECHA:H='FECHA',HD_COD_CL:H='COD.CLIE.',HD_OBS:H='OBSERVACIONES':P='@S30',HD_PEDIDO:H='PEDIDO',HD_FACTURA:H='FACTURA',HD_ESTADO:H='ESTADO' ;
     WINDOW W_NR TITLE 'SELECCIONE NR CON [ENTER]'
     SET ORDER TO CORRE
     set display to vga25
     ON KEY
  ENDCASE
ENDIF

RELEASE WINDOW NR_BUSCA
DO ON_KEYS
DO NR_VER
RETURN

PROCEDURE NR_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_NR
RELEASE WINDOW NR_HD
RELEASE WINDOW NR_LN
RELEASE WINDOW NR_MSG
RELEASE WINDOW W_NR
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE ON_KEYS
*****************
DEACTIVATE MENU _SYSMENU
ON KEY LABEL F1 KEYBOARD ''
ON KEY LABEL F2 KEYBOARD ''
ON KEY LABEL F3 KEYBOARD ''
ON KEY LABEL F4 KEYBOARD ''
ON KEY LABEL F5 KEYBOARD ''
ON KEY LABEL F6 KEYBOARD ''
ON KEY LABEL F7 KEYBOARD ''
ON KEY LABEL F8 KEYBOARD ''
ON KEY LABEL F9 KEYBOARD ''
ON KEY LABEL F10 KEYBOARD ''
ON KEY LABEL F11 keyboard ''
ON KEY LABEL F12 keyboard ''
RETURN

PROCEDURE NR_ALTA
*****************
MENSAJE1='LAS NOTAS DE REMISION SOLO PUEDEN SER GENERADAS A PARTIR DEL PEDIDO' 
MENSAJE2='GENERE LA N.R. EN EL MODULO DE PEDIDOS. GRACIAS.'
DO V_MENSAJ.SPR
RETURN

PROCEDURE NR_ANULA
******************
SELECT V_DOC_HD
IF HD_ESTADO='F' .OR. HD_FACTURA<>SPACE(6)
    MENSAJE1='LA NOTA DE REMISION NO PUEDE SER ANULADA' 
    MENSAJE2='!! YA FUE FACTURADA !!'
    DO V_MENSAJ.SPR
    RETURN
ENDIF
DEFINE WINDOW ANULA_WI FROM 8,20 TO 10,60 DOUBLE TITLE 'DESEA ANULAR ESTA NR ?' && COLOR B/G,,B/G,B/G,N+/G
ACTIVATE WINDOW ANULA_WI
SINO='N'
@ 0,5 SAY '          (S/N):' GET SINO PICTURE '!' VALID SINO$'SN'
READ

IF SINO='S'

  SELECT V_DOC_HD
  DO REPONE_PE
  
  SELECT V_DOC_HD
  IF LOCK()
    REPLACE HD_ESTADO WITH 'A', ;
            HD_PEDIDO WITH SPACE(6), ;
            HD_OBS WITH '* * * A N U L A D A * * *', ;
            HD_MIGRA WITH ' '

    SELECT V_DOC_LN
    SEEK V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO
    IF .NOT. EOF() .AND. LOCK()
      REPLACE LD_ESTADO WITH 'A', ;
              LD_PART WITH SPACE(15), ;
              LD_COD_CL WITH SPACE(8), ;
              LD_COD_VEN WITH SPACE(30), ; 
              LD_CANT WITH 0, ;
              LD_PESO WITH 0, ;
              LD_COD_CL WITH SPACE(8)
      SKIP             
      DO WHILE .NOT. EOF() .AND. V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO=LD_EMPR+LD_TIPO+LD_NRO
        IF LOCK()
          DELETE
        ENDIF
        SKIP
      ENDDO
    ENDIF     
  ENDIF
  RELEASE WINDOW ANULA_WI  
  MENSAJE1='!!!! LA NR HA SIDO ANULADO !!!!' 
  MENSAJE2=''
  DO V_MENSAJ.SPR
  DO NR_VER  
ELSE
  RELEASE WINDOW ANULA_WI  
ENDIF  
RETURN


PROCEDURE REPONE_PE
*******************
SELECT V_DOC_LN
SEEK V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO

DO WHILE .NOT. EOF() .AND. V_DOC_HD.HD_EMPR+V_DOC_HD.HD_TIPO+V_DOC_HD.HD_NRO=LD_EMPR+LD_TIPO+LD_NRO

  IF LOCK('v_ped_ln')
    REPLACE V_PED_LN.LP_PEND WITH V_PED_LN.LP_PEND+LD_CANT
  ENDIF

  DO CASE 
    CASE V_PED_LN.LP_PEND=V_PED_LN.LP_CANT
      IF LOCK('v_ped_ln')
        REPLACE V_PED_LN.LP_ESTADO WITH 'P'
      ENDIF
    CASE V_PED_LN.LP_PEND<V_PED_LN.LP_CANT .AND. V_PED_LN.LP_PEND<>0
      IF LOCK('v_ped_ln')
        REPLACE V_PED_LN.LP_ESTADO WITH 'I'
      ENDIF
    CASE V_PED_LN.LP_PEND=0
      IF LOCK('v_ped_ln')
        REPLACE V_PED_LN.LP_ESTADO WITH 'D'
      ENDIF
  ENDCASE        
  SKIP
ENDDO

SELECT V_PED_LN
ESTADO='D'
SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
DO WHILE .NOT. EOF() .AND. V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
   IF LP_ESTADO='I'
     ESTADO='I'
     EXIT
   ENDIF
   IF LP_ESTADO='P'
     ESTADO='P'
   ENDIF
   SKIP
ENDDO 
SELECT V_PED_HD
IF LOCK()
  REPLACE HP_ESTADO WITH ESTADO
ENDIF  
RETURN

*-----------------*
FUNCTION GET_ALM
*-----------------*
PARAMETER ALM
SELECT 0 
USE V_ALM ORDER ALM
SEEK EMPRESA+ALM
IF FOUND()
  ALMACEN=SUBSTR(AL_DESCR,1,20)
  USE
ELSE
  ALMACEN='ALMACEN NO EXISTE'
  USE
ENDIF    
