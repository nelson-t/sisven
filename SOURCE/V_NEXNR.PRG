PROCEDURE V_NEXNR
*****************
SET PRINT OFF
SET ESCAPE OFF
CLEAR
CLOSE DATA
SET EXCLU OFF
SET DELE ON
SET DATE TO FRENCH
SET CONFIRM ON
SET TALK OFF

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U" ;
OR TYPE("INI_GEST")="U"
  DO V_GETEMP
ENDIF

OPCION=1
TIPOR=1
ALM='01'
WNRO_NR=SPACE(6)
WCOD_CL=SPACE(8)
FECHAI={  /  /  }
FECHAF={  /  /  }
TODAS=1
ORDEN=1

DO V_NEXNR.SPR

IF OPCION=3 .OR. LASTKEY()=27
  RETURN
ENDIF

ARCH=SYS(3)

SELECT 1
USE V_NEXNR
COPY STRUC TO (ARCH)
USE (ARCH) ALIAS DESPA EXCLU

SELECT 2
USE V_NE_HD ORDER NOTAR
SELECT 3
USE V_NE_LN ORDER NR
SELECT 4
USE V_DOC_HD ORDER NR
SELECT 5
USE V_DOC_LN ORDER NR

DO CASE
  CASE TIPOR=1
    SET FILTER TO LD_TIPO='R' .AND. LD_ESTADO<>'A' .AND. LD_ALM=ALM
    GO TOP
  CASE TIPOR=2
    SEEK EMPRESA+'R'+WNRO_NR  
  CASE TIPOR=3
    SET FILTER TO LD_TIPO='R' .AND. LD_ESTADO<>'A'.AND. LD_COD_CL=WCOD_CL .AND. LD_ALM=ALM
    GO TOP
  CASE TIPOR=4
    SET FILTER TO LD_TIPO='R' .AND. LD_ESTADO<>'A'.AND. BETWEEN(LD_FECHA,FECHAI,FECHAF) .AND. LD_ALM=ALM
    GO TOP
ENDCASE

DO WHILE .NOT. EOF() .AND. IIF(TIPOR=2,LD_EMPR+LD_TIPO+LD_NRO=EMPRESA+'R'+WNRO_NR,.T.)  
   NRO_EMP=0
   NE_STRING=SPACE(40)
   CANT_DES=GETDESPACH(LD_EMPR,LD_TIPO,LD_NRO,LD_PART)
   IF TODAS=1 
     IF LD_CANT<>CANT_DES
       SELECT DESPA
       APPEN BLANK
       REPLACE NRO_NR   WITH V_DOC_LN.LD_NRO, ;
               PART     WITH V_DOC_LN.LD_PART, ;
               FECHA    WITH V_DOC_LN.LD_FECHA, ;
               CANT     WITH V_DOC_LN.LD_CANT, ;
               COUNT_NE WITH NRO_EMP, ;
               DESP     WITH CANT_DES, ;
               NES      WITH NE_STRING, ;
               COD_CL   WITH V_DOC_LN.LD_COD_CL, ;
               ESTADO   WITH V_DOC_LN.LD_ESTADO
     ENDIF
   ELSE
       SELECT DESPA
       APPEN BLANK
       REPLACE NRO_NR   WITH V_DOC_LN.LD_NRO, ;
               PART     WITH V_DOC_LN.LD_PART, ;
               FECHA    WITH V_DOC_LN.LD_FECHA, ;
               CANT     WITH V_DOC_LN.LD_CANT, ;
               COUNT_NE WITH NRO_EMP, ;
               DESP     WITH CANT_DES, ;
               NES      WITH NE_STRING, ;
               COD_CL   WITH V_DOC_LN.LD_COD_CL, ;
               ESTADO   WITH V_DOC_LN.LD_ESTADO
   ENDIF

   SELECT V_DOC_LN
   SKIP
ENDDO

SELECT 0
USE V_CLIENT ORDER CODIGO
SELECT 0
USE V_ITEMS ORDER PART

SELECT DESPA
SET RELATION TO EMPRESA+COD_CL INTO V_CLIENT
SET RELATION TO EMPRESA+PART INTO V_ITEMS ADDITIVE
DO CASE
  CASE ORDEN=1
    INDEX ON COD_CL+NRO_NR TAG A
  CASE ORDEN=2
    INDEX ON NRO_NR TAG A
  CASE ORDEN=3
    INDEX ON PART+NRO_NR TAG A
ENDCASE

IF OPCION=1
  CLEAR
  DEFINE WINDOW VER_NE FROM 0,0 TO 22,79
  BROWSE FIELDS COD_CL:H="COD.CL.",NRO_NR:h="N.R.",FECHA,PART:H='CODIGO':10,V_ITEMS.IT_DESCR:H="DESCRIPCION":20,CANT:P='9999999.99',PENDIENTE=CANT-DESP:P='9,999,999.99', NES:H='NOTAS DE EMPAQUE' WINDOW VER_NE TITLE '[ESC] PARA SALIR' WHEN MUESTRA()
  RELEASE WINDOW VER_NE
ELSE
  ARCH_TXT='..\TMP\'+ARCH+'.TXT'
  REPORT FORM V_NEX TO (ARCH_TXT)
  ! COPY &ARCH_TXT lpt1:
ENDIF
CLOSE ALL
DELE FILE (ARCH+'.DBF')
DELE FILE (ARCH+'.CDX')
DELE FILE (ARCH+'.TXT')
RETURN   

FUNCTION GETDESPACH
*******************
PARAMETER EMPRESA, TIPO, NR, PART

ACTUAL=SELECT()
DESPACH=0

SELECT V_NE_HD    && ORDER NOTAR

SEEK EMPRESA+TIPO+NR
IF EOF()
  SELECT (ACTUAL)
  RETURN DESPACH
ENDIF

DO WHILE .NOT. EOF() .AND. EMPRESA+TIPO+NR=HD_EMPR+HD_D_TIPO+HD_D_NRO

  NE_TIPO=HD_TIPO
  NE_NRO =HD_NRO

  SELECT V_NE_LN
  SEEK EMPRESA+NE_TIPO+NE_NRO+PART

  DO WHILE .NOT. EOF() .AND. LD_EMPR+LD_TIPO+LD_NRO+LD_PART=EMPRESA+NE_TIPO+NE_NRO+PART
    NRO_EMP=NRO_EMP+1
    DESPACH=DESPACH+LD_CANT
    NE_STRING=ALLTRIM(NE_STRING)+'['+LD_NRO+']'
    SKIP
  ENDDO   
  SELECT V_NE_HD
  SKIP

ENDDO

SELECT (ACTUAL)
RETURN DESPACH

FUNCTION MUESTRA
****************
@ 24,0 SAY 'CLIENTE:'+V_CLIENT.CL_RAZON
RETURN .T.