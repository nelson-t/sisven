PROCEDURE V_GSALDO
******************
SET PRINT OFF
SET ESCAPE OFF
CLEAR
CLOSE DATA
SET EXCLU OFF
SET DELE ON
SET DATE TO FRENCH
SET CONFIRM ON
SET TALK OFF

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U" ;
OR TYPE("INI_GEST")="U"
  DO V_GETEMP
ENDIF

FECHAI=INI_GEST
FECHAF=DATE()

PUBLIC tot_kg

MI_GLOSA=SPACE(30)
MI_GLOSA1=SPACE(30)

ALMACEN='  '
LINEA='  '
ORDEN=1
VALORADO=1

TOT_SALIDAS=0
TOT_SALDO=0
FECHAI=INI_GEST
FECHAF=DATE()
OPCION=1
TIPOR=1

REP_ARCH='..\TMP\'+SYS(3)+'.TXT'

DO V_GSALDO.SPR
CLEAR

IF  OPCION=3 .OR. LASTKEY()=27
  CLEAR
  RETURN
ENDIF

SELECT 1
USE V_ALM ORDE ALM

MI_IDX=SYS(3)+'.IDX'

SELECT 2
USE V_ITEMS ORDER PART

SELECT 3
USE V_DOC_LN 

@ 08,10 SAY '  SELECCIONANDO PRODUCTOS ........  ' COLOR B*/GR
INDEX ON LD_EMPR+LD_PART TO (MI_IDX) UNIQUE

@ 08,10 SAY 'û' COLOR GR+/b
@ 08,11 SAY ' SELECCIONANDO PRODUCTOS ........  '

SET FILTER TO LD_EMPR=EMPRESA .AND. LD_PART<>SPACE(15) .AND. LD_ESTADO<>'A' ;
.AND. SUBSTR(V_ITEMS.IT_LINEA,1,1) = ALLTRIM(LINEA) 

SET RELATION TO LD_EMPR+LD_PART INTO V_ITEMS
SELECT 4
USE V_LINEAS ORDER LINEA

SELECT V_ITEMS 
SET RELATION TO IT_EMPR+SUBSTR(IT_LINEA,1,2)+' ' INTO V_LINEAS

SELECT 5
USE V_SALDOS 

T_SALD=SYS(3)+'.SAL'
T_IDX =SYS(3)+'.IDX'

COPY STRUC TO (T_SALD)
USE (T_SALD) ALIAS SALDOS

@ 10,10 SAY '  CREANDO REGISTROS DE SALDOS ........  ' COLOR B*/GR

  SELECT V_DOC_LN
  GO TOP
  DO WHILE .NOT. EOF() 
      SELECT SALDOS
      APPEND BLANK
      REPLACE SA_EMPR WITH EMPRESA
      REPLACE SA_ALM WITH ALMACEN
      REPLACE SA_LINEA WITH V_ITEMS.IT_LINEA
      REPLACE SA_PART WITH V_DOC_LN.LD_PART
      REPLACE SA_FECHA WITH FECHAF
    SELECT V_DOC_LN
    SKIP
  ENDDO
@ 10,10 SAY 'û' COLOR GR+/b
@ 10,11 SAY ' CREANDO REGISTROS DE SALDOS ........  '

@ 12,10 SAY ' CALCULANDO SALDOS PARA PRODUCTOS ........  ' COLOR B*/GR

SELECT V_DOC_LN
SET RELATION OFF INTO V_ITEMS
SET FILTER TO 
SET FILTER TO LD_ESTADO<>'A' .AND. between(ld_fecha,fechai,fechaf)

SELECT SALDOS

GO TOP
DO WHILE .NOT. EOF()
  
  T_INV=0
  T_ING=0
  T_SAL=0

  T_INVK=0
  T_INGK=0
  T_SALK=0

  T_INVB=0
  T_INGB=0
  T_SALB=0
  
  TOT_BS=0
  TOT_KG=0
  TOT_CANT=get_sal(SA_EMPR, SA_ALM, SA_PART)
  SELECT SALDOS        
  @ 14 , 20 SAY sa_part+" CANT="+TRANSFORM(tot_cant,'99,999,999.99')+" PESO="+TRANSFORM(tot_kg,'99,999,999.99')
  REPLACE SA_SALDO   WITH TOT_CANT, ;
          SA_SALDOK  WITH TOT_KG, ;
          SA_SALDOB  WITH TOT_BS, ;
          SA_SALE    WITH T_SAL,  ;
          SA_SALEK   WITH T_SALK, ;
          SA_SALEB   WITH T_SALB, ;
          SA_INGK    WITH T_INGK, ;
          SA_INGB    WITH T_INGB, ;
          SA_IMPRE   WITH .F.
  DO CASE
    CASE TIPOR=1
     IF ORDEN=1 .AND. (TOT_CANT<>0 .OR. TOT_KG<>0)
       REPLACE SA_IMPRE WITH .T.
     ELSE
       IF (ORDEN=2 .OR. ORDEN=3) .AND. T_SAL<>0 
          REPLACE SA_IMPRE WITH .T.
       ENDIF
     ENDIF
    CASE TIPOR=2
     IF T_INV<>0 .AND. T_ING=0 .AND. T_SAL=0
       REPLACE SA_IMPRE WITH .T.
     ENDIF
    CASE TIPOR=3
     IF T_INV+T_ING>0 .AND. T_SAL=0
       REPLACE SA_IMPRE WITH .T.
     ENDIF
    CASE TIPOR=4
     IF TOT_CANT<=0 .AND. T_ING+T_SAL>0
       REPLACE SA_IMPRE WITH .T.
     ENDIF
    CASE TIPOR=5
     IF TOT_CANT>0 
       REPLACE SA_IMPRE WITH .T.
     ENDIF
    CASE TIPOR=6
     IF TOT_CANT<0 
       REPLACE SA_IMPRE WITH .T.
     ENDIF
  ENDCASE
  SKIP
ENDDO

@ 12,10 SAY 'û' COLOR GR+/b

@ 12,11 SAY ' CALCULANDO SALDOS PARA PRODUCTOS ........  ' 

@ 14,0 CLEAR

@ 14,10 SAY ' ORDENANDO PRODUCTOS ........  ' COLOR B*/GR

SELECT SALDOS

IF ORDEN=1
  INDEX ON SA_EMPR+SA_LINEA+SA_PART TO (T_IDX)
ELSE
  INDEX ON 100000000-SA_SALDOK TO (T_IDX)   &&**********

  DO ABC

  DO CASE
    CASE ORDEN=2
      T_ABC=' '
    CASE ORDEN=3
      T_ABC='A'
  ENDCASE
ENDIF

SET RELATION TO SA_EMPR+SA_PART INTO V_ITEMS 

@ 14,10 SAY 'û' COLOR GR+/b
@ 14,11 SAY ' ORDENANDO PRODUCTOS ........  '

DO CASE
  CASE TIPOR=1
    MI_TITULO=''
  CASE TIPOR=2
    MI_TITULO='SOLO LOS PRODUCTOS SIN MOVIMIENTO'
  CASE TIPOR=3
    MI_TITULO='SOLO LOS PRODUCTOS SIN SALIDAS'
  CASE TIPOR=4
    MI_TITULO='SOLO LOS PRODUCTOS CON MOVIMIENTO Y SALDO=0'
  CASE TIPOR=5
    MI_TITULO='SOLO LOS PRODUCTOS SALDOS POSITIVOS'
  CASE TIPOR=6
    MI_TITULO='SOLO LOS PRODUCTOS SALDOS NEGATIVOS'
ENDCASE

IF LASTKEY()<>27 .OR. OPCION=3
    IF TIPOR=1 .AND. ORDEN<>1
      REPORT FORM V_GSALD1 FOR SA_IMPRE .AND. SA_ABC=ALLTRIM(T_ABC) TO FILE (REP_ARCH)
    ELSE
      IF VALORADO=1
        REPORT FORM V_GSALDO FOR SA_IMPRE TO FILE (REP_ARCH)
      ELSE
        REPORT FORM V_GSALD2 FOR SA_IMPRE TO FILE (REP_ARCH)
      ENDIF
    ENDIF      
    *PANTALLA
    IF OPCION=1 
      DEFINE WINDOW VER_REP FROM 0,0 TO 24,79 DOUBLE TITLE '[ESC] - PARA SALIR'
      MODI COMM (REP_ARCH) WINDOW VER_REP NOEDIT
      RELEASE WINDOW VER_REP    
    ELSE
      ! COPY &REP_ARCH lpt1:
    ENDIF
	WAIT "PROCESO CONCLUIDO...SALIENDO." TIMEOUT 2
	DELETE FILE (REP_ARCH)
ENDIF
  
CLEAR
CLOSE DATA
DELETE FILE (T_SALD)
DELETE FILE (T_IDX)
DELETE FILE (MI_IDX)

RETURN


FUNCTION get_SAL
****************
PARAMETER EMPRESA,ALM,PARTE
SALDO=0
SELECT V_DOC_LN
SET ORDER TO KARDEX
SEEK EMPRESA+PARTE
DO WHILE .NOT. EOF() .AND. LD_EMPR=EMPRESA .AND. LD_PART=PARTE
 IF LD_ALM=ALLTRIM(ALMACEN)
   DO CASE
     CASE LD_INSAL='1'
       IF LD_TIPO='A'
         T_INV=T_INV+LD_CANT
         T_INVK=T_INVK+LD_PESO
         T_INVB=T_INVB+(LD_COSTO*LD_CANT)
       ELSE
         T_ING=T_ING+LD_CANT
         T_INGK=T_INGK+LD_PESO
         T_INGB=T_INGB+(LD_COSTO*LD_CANT)
       ENDIF
       SALDO =SALDO +LD_CANT
       TOT_KG=TOT_KG+LD_PESO
       TOT_BS=TOT_BS+(LD_COSTO*LD_CANT)
       
     CASE LD_INSAL='2'
       IF LD_TIPO='A'
         T_INV=T_INV-LD_CANT
         T_INVK=T_INVK-LD_PESO
         T_INVB=T_INVB-(LD_COSTO*LD_CANT)
       ELSE
         T_SAL=T_SAL+LD_CANT
         T_SALK=T_SALK+LD_PESO
         T_SALB=T_SALB+(LD_COSTO*LD_CANT)
       ENDIF
      SALDO =SALDO-LD_CANT
      TOT_KG=TOT_KG-LD_PESO
      TOT_BS=TOT_BS-(LD_COSTO*LD_CANT)
      
   ENDCASE 
 ENDIF
 SKIP
ENDDO
RETURN SALDO

FUNCTION GLOSA
**************
MI_GLOSA=V_LINEAS.LI_DESCR
RETURN ' '

FUNCTION GLOSA1
**************
SELECT V_ITEMS
SET RELATION TO IT_EMPR+SUBSTR(IT_LINEA,1,1)+'  ' INTO V_LINEAS
MI_GLOSA1=V_LINEAS.LI_DESCR
SET RELATION TO IT_EMPR+SUBSTR(IT_LINEA,1,2)+' ' INTO V_LINEAS
SELECT SALDOS
RETURN ' '

FUNCTION GET_EMPR
*****************
PARAMETER EMPR
SELECT 0
USE V_EMPR ORDER EMPR
SEEK EMPR
IF EOF()
  USE
  RETURN .F.
ELSE
  MI_EMPRESA=EM_DESCR
  @ 8,37 SAY MI_EMPRESA
  USE
  RETURN .T.
ENDIF

PROCEDURE ABC
*************
SELECT SALDOS
SUM SA_SALDOK TO TOT_SALDO FOR SA_IMPRE
GO TOP
MI_RESTA=0
DO WHILE .NOT. EOF()  
  IF SA_IMPRE
    PORCEN=ROUND(MI_RESTA/TOT_SALDO*100,2)
    DO CASE
      CASE PORCEN<=80
        REPLACE SA_ABC WITH  'A'
      CASE PORCEN<=95
        REPLACE SA_ABC WITH  'B'
      OTHERWISE
        REPLACE SA_ABC WITH  'C'
    ENDCASE  
    MI_RESTA=MI_RESTA+SA_SALDOK
  ENDIF
  SKIP
ENDDO
GO TOP
RETURN
