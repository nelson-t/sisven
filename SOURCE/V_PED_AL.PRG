PROCEDURE V_PED_AL
******************
IF .NOT. V_USRAUT(USER,"PEDIDOS",2)
  RETURN
ENDIF

SELECT V_TC
SEEK DTOS(DATE())
IF EOF()
  MENSAJE1='!!! TIPO DE CAMBIO DEL DIA, NO HA SIDO ACTUALIZADO !!!' 
  MENSAJE2='ACTUALICE EL TIPO DE CAMBIO ANTES DE INTENTAR DE NUEVO'
  DO V_MENSAJ.SPR
  RETURN
ENDIF

SELECT V_PED_HD
AREC=RECNO()

IF V_VNDUPL("PED",0)
	SELECT V_PED_HD
	GO AREC
    RETURN
ENDIF
GO AREC

ACTIVATE SCREEN
CLEAR

ON KEY LABEL F10 KEYBOARD CHR(23)
q_pend=0
OKCANCEL=1

*NROLINEAS es definido desde rutina principal

END_MENU=.F.
TPRECIO="Base"

P_CABEZA=SYS(3)
SELECT V_PED_HD
*MANTENRE ESTOS RELATIONS OFF, DE OTR FORMA LOS PUNTEROS SE DISTORSIONAN
SET RELATION OFF INTO V_EMPR
SET RELATION OFF INTO V_CLIENT
SET RELATION OFF INTO V_VENDOR 

COPY STRUCTURE TO (P_CABEZA)

SELECT 5 && E   && Cabecera temporal
USE (P_CABEZA) EXCLUSIVE        &&ALIAS CABEZA EXCLUSIVE
SET RELATION TO HP_EMPR INTO V_EMPR
SET RELATION TO HP_EMPR+HP_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HP_EMPR+HP_COD_VEN INTO V_VENDOR ADDITIVE

P_LINEAS=SYS(3)
SELECT V_PED_LN
SET RELATION OFF INTO V_ITEMS

COPY STRUCTURE TO (P_LINEAS)

SELECT 6 && F
USE (P_LINEAS) EXCLUSIVE &&ALIAS LINEAS EXCLUSIVE
SET RELATION TO LP_EMPR+LP_PART INTO V_ITEMS

TOT_CANT=0
TOT_PESO=0

SELECT E  && Cabecera temporal
APPEND BLANK
REPLACE HP_EMPR    WITH EMPRESA
REPLACE HP_FECHA   WITH DATE()
REPLACE HP_F_COTIZ WITH DATE()
REPLACE HP_U_COTIZ WITH USER
REPLACE HP_ESTADO  WITH 'C'

HIDE MENU M_PEDIDO
ACTIVATE SCREEN
CLEAR
@ 13,25 TO 17,55 
@ 14,27 SAY 'DIGITE LOS DATOS POR FAVOR'
@ 16,27 SAY '     [ESC] PARA SALIR     '
@ 19,0 TO 24,79 DOUBLE
@ 19, 5 SAY 'SALDOS DEL ITEM'
@ 19,60 SAY 'TOTALES'
@ 22,55 SAY 'DSCTO US$='+TRANSFORM(0,'9,999,999.99') 
@ 23,55 SAY 'TOTAL US$='+TRANSFORM(0,'9,999,999.99') 
ACTIVATE WINDOW PED_HD
CLEAR

DO V_PED_A.SPR

IF LASTKEY()<>27 .AND. OKCANCEL=1
  SELECT E  && Cabecera temporal
  DO V_PED_V.SPR
  ACTIVATE SCREEN
  SELECT F   && lineas temporal
  FOR N=1 TO NROLINEAS
    APPEND BLANK
    REPLACE LP_EMPR    WITH E.HP_EMPR
    REPLACE LP_FECHA   WITH E.HP_FECHA
    REPLACE LP_F_COTIZ WITH E.HP_F_COTIZ    
    REPLACE LP_COD_CL  WITH E.HP_COD_CL
    REPLACE LP_COD_VEN WITH E.HP_COD_VEN
    REPLACE LP_ESTADO  WITH E.HP_ESTADO
  ENDFOR

  DEFINE WINDOW ped_AL  FROM 9, 0 TO 18,79 TITLE "D E T A L L E" ;
         NOFLOAT DOUBLE 
******** PARA CALCULO DE SALDOS ************
  SELECT 0
  USE V_ALM ORDER ALM
  T_ALM=SYS(3)
  COPY TO (T_ALM) WITH CDX FIELDS AL_EMPR,AL_ALM,AL_DESCR,AL_CANT,AL_PESO,AL_PENDIEN FOR AL_EMPR=EMPRESA
  USE (T_ALM) ORDER ALM ALIAS ALMACEN EXCLU

  SELECT 0
  USE V_DOC_LN ORDER KARDEX
  SET RELATION TO LD_EMPR+LD_ALM INTO ALMACEN
************************************************
  SELECT F  && Lineas temporal
  GO TOP
  REG_ACT=0
  COD_ACT=SPACE(15)
  BROWSE NOCLEAR FIELDS LP_PART:H='CODIGO':P='@S8 !!!!!!!!!!!!!!!':V=CHK_PART(LP_PART) ;
   .AND. GET_PRECIO(LP_PART):E='CODIGO ERRONEO O YA EXISTE':W=GET_TOT('W'), ;
   DESCR=V_ITEMS.IT_DESCR:W=.F., V_ITEMS.IT_UNIDAD:H='UNI.':W=.F., ;
   LP_CANT:H='CANTIDAD':V=GET_TOT('V'):W=GET_TOT('W') .AND. ;
   LP_PART<>SPACE(15):P='9999999.99':F, ;
   LP_PRECIO:H='PRECIO U.':P='999999.99':V=GET_TOT('V'):W=GET_TOT('W') .AND. E.HP_DSCTO_P=0 ;
   .AND. LP_PART<>SPACE(15), ;
   IMPORTE=ROUND(LP_PRECIO*LP_CANT,2):P='999,999.99':W=.F. ;
   WINDOW PED_AL TITLE '[F10]-TERMINA [ESC]-DESCARTA PEDIDO [*]-FILTRO' 
  
  SELECT V_DOC_LN
  USE

  SELECT ALMACEN
  USE

  DELETE FILE (T_ALM+'.DBF')
  DELETE FILE (T_ALM+'.CDX')

  SELECT F   && lineas temporal
  
  IF LASTKEY()<>27
    GO TOP
    IMPORTE=0
    DO WHILE .NOT. EOF() 
      IF LP_CANT=0 .OR. LP_PART=SPACE(15)
        DELETE
      ELSE
        REPLACE LP_C_COTIZ WITH LP_CANT
        IMPORTE=IMPORTE+ROUND(LP_CANT*LP_PRECIO,2)
      ENDIF
      SKIP
    ENDDO
    
    SELECT E  && Cabecera temporal
    REPLACE HP_IMPORTE WITH IMPORTE

    ACTIVATE SCREEN

    @ 22,55 SAY 'DSCTO US$='+TRANSFORM(ROUND(HP_IMPORTE*E.HP_DSCTO_P/100,2) ,'9,999,999.99') 
    @ 23,55 SAY 'TOTAL US$='+TRANSFORM(HP_IMPORTE-ROUND(HP_IMPORTE*E.HP_DSCTO_P/100,2),'9,999,999.99') 

    DO PED_COTIZ

    OPCION=1
    DO V_PE_CNF.SPR

    DO CASE
      CASE OPCION=1
        * ALMACENA COTIZACION  
        SELECT E  && Cabecera temporal
        REPLACE HP_NRO WITH V_NEXT_N(EMPRESA,'PED')
        REPLACE HP_OBS_COM WITH TPRECIO
        SELECT F    && lineas temporal
        GO TOP
        DO WHILE .NOT. EOF() 
          REPLACE LP_NRO WITH E.HP_NRO
          SKIP
        ENDDO

        SELECT 5
        USE
        SELECT 6
        USE
  
        SELECT V_PED_HD
        APPEND FROM (P_CABEZA)

        SELECT V_PED_LN
        APPEND FROM (P_LINEAS)
        GO TOP
        END_MENU=.T.

      CASE OPCION=2
*       CONFIRMA PEDIDO
        SELECT E  && Cabecera temporal
        REPLACE HP_ESTADO WITH 'P'
        REPLACE HP_NRO WITH V_NEXT_N(EMPRESA,'PED')
        REPLACE HP_USUARIO WITH USER
                
        SELECT F    && lineas temporal
        GO TOP
        DO WHILE .NOT. EOF() 
          REPLACE LP_NRO    WITH E.HP_NRO
          REPLACE LP_ESTADO WITH E.HP_ESTADO
          REPLACE LP_PEND   WITH LP_CANT          
          SKIP
        ENDDO

        SELECT 5
        USE
        SELECT 6
        USE
  
        SELECT V_PED_HD
        APPEND FROM (P_CABEZA)

        SELECT V_PED_LN
        APPEND FROM (P_LINEAS)
        GO TOP
        END_MENU=.T.

      CASE OPCION=3
*       ANULA NUMERO DE PEDIDO      
        END_MENU=.F.
    ENDCASE
  ELSE
	SELECT V_PED_HD
  	GO AREC
  ENDIF
ELSE
 SELECT V_PED_HD
 GO AREC
ENDIF

SELECT 5
USE
SELECT 6
USE

SELECT V_PED_HD
*MANTENER, NO QUITAR
SET RELATION TO HP_EMPR INTO V_EMPR
SET RELATION TO HP_EMPR+HP_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HP_EMPR+HP_COD_VEN INTO V_VENDOR ADDITIVE

SELECT V_PED_LN
SET RELATION TO LP_EMPR+LP_PART INTO V_ITEMS

FLUSH

DELETE FILE (P_CABEZA+'.DBF')
DELETE FILE (P_LINEAS+'.DBF')

DO V_ONKEYS
DO PE_VER
IF LASTKEY()<>27 .AND. END_MENU
  DO V_PED_OP
ENDIF
RETURN

FUNCTION CHK_PART
*****************
*modificado por NT 28/12/67 - correccion verificacion duplicados
PARAMETER PART

IF substr(PART,1,1)="*"
  SELECT V_ITEMS
  set filter to (upper(alltrim(SUBSTR(PART,2,15)))$upper(it_part) .OR. upper(alltrim(SUBSTR(PART,2,15)))$upper(it_descr))
  ACTIVATE POPUP GETITEM
  PART=SUBSTR(PROMPT(),1,15)
  set filter to
  SELECT F   && Lineas temporal
  REPLACE LP_PART WITH PART
ELSE
  SELECT V_ITEMS
  SEEK EMPRESA+PART
  IF .NOT. FOUND()
    ACTIVATE POPUP GETITEM
    PART=SUBSTR(PROMPT(),1,15)
    SELECT F   && Lineas temporal
    REPLACE LP_PART WITH PART
  ENDIF
ENDIF

SELECT V_ITEMS
SEEK EMPRESA+PART

  SELECT F    && Lineas temporal
  IF PART<>SPACE(15)
    REG_NUM=RECNO()
    REPLACE LP_PART WITH 'XXXXXXXXXXXXXXX'
    LOCATE FOR LP_PART=PART
     
    IF FOUND()
      * CODIGO DUPLICADO
      GO REG_NUM
      REPLACE LP_PART WITH SPACE(15)
      REPLACE lp_sus_kg WITH 0
      REPLACE lp_peso_t WITH 0
      RETURN .F.
    ELSE
      GO REG_NUM
      REPLACE LP_PART WITH PART
      REPLACE lp_sus_kg WITH v_items.it_sus_kg
      REPLACE lp_peso_t WITH v_items.it_peso_t
    ENDIF
  ENDIF
  RETURN .T.

FUNCTION GET_PRECIO
*-----------------*
PARAMETER MI_PART

DESCTO_IT=0

*VERIFICA SI HAY DESCUENTO PARA ESES ITEM Y CATEGORIA DE CLIENTE
*NADA TIENE QUE VER CON EL DESCUENTO GENERAL QUE SE ASIGNA AL PEDIDO/COTIZ.

SELECT V_DESCTO
SEEK EMPRESA+F.LP_PART
DO WHILE .NOT. EOF() .AND. DE_EMPR+DE_PART=EMPRESA+F.LP_PART
    IF BETWEEN(E.HP_FECHA,DE_FECHAI,DE_FECHAF)
      IF V_CLIENT.CL_CATEGO$DE_CATEGO .OR. DE_CATEGO=SPACE(25)
        DESCTO_IT=DE_DESCTO/100
      ENDIF
      EXIT  
    ENDIF
    SKIP
ENDDO   

**PRECIOS
SELPRECIO=0	
DO CASE
	CASE ALLTRIM(TPRECIO)='Base'
		SELPRECIO=V_ITEMS.IT_PRECIO	
	CASE ALLTRIM(TPRECIO)='Lista 1'
		SELPRECIO=V_ITEMS.IT_PRECIO1	
	CASE ALLTRIM(TPRECIO)='Lista 2'
		SELPRECIO=V_ITEMS.IT_PRECIO2	
	CASE ALLTRIM(TPRECIO)='Lista 3'
		SELPRECIO=V_ITEMS.IT_PRECIO3	
	CASE ALLTRIM(TPRECIO)='Lista 4'
		SELPRECIO=V_ITEMS.IT_PRECIO4	
	CASE ALLTRIM(TPRECIO)='Lista 5'
		SELPRECIO=V_ITEMS.IT_PRECIO5	
ENDCASE

SELECT F   && Lineas temporal
REPLACE LP_LISTA   WITH ROUND((SELPRECIO-(SELPRECIO*DESCTO_IT)),2)
REPLACE LP_PRECIO  WITH ROUND((SELPRECIO-(SELPRECIO*DESCTO_IT)),2)

RETURN .T.

FUNCTION CHK_AREA
*****************
PARAMETER CODIGO

SELECT V_AREAS

IF (LEN(ALLTRIM(CODIGO)) = 0)
	ACTIVATE POPUP GETAREA
  	CODIGO = SUBSTR(PROMPT(),1,1)
  	SELECT E  && Cabecera temporal
  	REPLACE HP_AREA WITH CODIGO
	RESULT = .T.	
ELSE
	IF (LEN(ALLTRIM(CODIGO)) <> 0)
		SEEK CODIGO
		IF FOUND()
  			SELECT E  && Cabecera temporal
  			RESULT = .T.
		ELSE
  			ACTIVATE POPUP GETAREA
  			CODIGO = SUBSTR(PROMPT(),1,1)
  			SELECT E  && Cabecera temporal
  			REPLACE HP_AREA WITH CODIGO
  			RESULT = .T.
		ENDIF    
	ENDIF
ENDIF

IF (LEN(ALLTRIM(HP_AREA)) = 0)
	RESULT = .F.	
ENDIF

RETURN RESULT


FUNCTION CHK_VENDOR
*******************
PARAMETER CODIGO
SELECT V_VENDOR
SEEK EMPRESA+CODIGO
IF FOUND()
  @ 2,17 SAY VE_NOMBRE
  SELECT E  && Cabecera temporal
  RETURN .T.
ELSE
  @ 2,17 SAY 'CODIGO DE VENDEDOR ERRADO'
  SELECT E    && Cabecera temporal
  RETURN .F.  
ENDIF    

FUNCTION GET_TOT
****************
PARAMETER DEDONDE

MI_COD=LP_PART
MI_REC=RECNO()
q_pend=0
CALCULA=.F.

IF LP_PART=SPACE(15)
  CLEAR
  SUM LP_CANT,ROUND(LP_CANT*LP_PRECIO,2) TO TOT_CANT,TOT_IMPORT
  GO MI_REC
  @ 20,65 SAY (lp_precio)*.87/v_items.it_peso_t PICTURE "999.9999"    
  @ 22,55 SAY 'DSCTO US$='+TRANSFORM( ROUND(TOT_IMPORT*E.HP_DSCTO_P/100,2) ,'9,999,999.99') 
  @ 23,55 SAY 'TOTAL US$='+TRANSFORM(TOT_IMPORT-ROUND(TOT_IMPORT*E.HP_DSCTO_P/100,2),'9,999,999.99') 
  @ 19,0 TO 24,79 DOUBLE COLOR G+/B
  @ 19, 5 SAY 'SALDOS DEL ITEM'
  @ 19,60 SAY 'TOTALES'
  REG_ACT=0
  COD_ACT=SPACE(15)
  RETURN .T.
ENDIF

IF DEDONDE='V'   && Si el item es el mismo no calcula
  SUM LP_CANT,ROUND(LP_CANT*LP_PRECIO,2) TO TOT_CANT,TOT_IMPORT
  GO MI_REC
  @ 20,65 SAY (lp_precio)*.87/v_items.it_peso_t PICTURE "999.9999"  
  @ 22,55 SAY 'DSCTO US$='+TRANSFORM( ROUND(TOT_IMPORT*E.HP_DSCTO_P/100,2) ,'9,999,999.99') 
  @ 23,55 SAY 'TOTAL US$='+TRANSFORM(TOT_IMPORT-ROUND(TOT_IMPORT*E.HP_DSCTO_P/100,2),'9,999,999.99') 
ELSE   && Si item es diferente calcula
  IF MI_REC<>REG_ACT .OR. MI_COD<>COD_ACT
    CALCULA=.T.
    REG_ACT=MI_REC
    COD_ACT=MI_COD
  ENDIF  

  IF CALCULA
    SUM LP_CANT,ROUND(LP_CANT*LP_PRECIO,2) TO TOT_CANT,TOT_IMPORT
    GO MI_REC
    CLEAR
    @ 19,0 TO 24,79 DOUBLE COLOR G+/B
    @ 19, 5 SAY 'SALDOS DEL ITEM'
    @ 19,60 SAY 'TOTALES'
    @ 22,14 SAY 'CALCULANDO SALDO DEL PRODUCTO ...' COLOR R/GR*
    PARTE=LP_PART

    SELECT ALMACEN
    REPLACE almacen.AL_CANT WITH 0 ALL
    REPLACE almacen.AL_PESO WITH 0 ALL
    REPLACE almacen.al_pendient WITH 0 ALL

* Calculo de Saldos del Item
    SELECT V_DOC_LN
    ld_cant=0
    ld_peso=0
    SEEK EMPRESA+PARTE
    DO WHILE .NOT. EOF() .AND. LD_EMPR=EMPRESA .AND. LD_PART=PARTE
      DO CASE
        CASE LD_INSAL='1'
           REPLACE ALMACEN.AL_CANT WITH ALMACEN.AL_CANT+LD_CANT
           REPLACE ALMACEN.AL_PESO WITH ALMACEN.AL_PESO+LD_PESO
        CASE LD_INSAL='2'
           REPLACE ALMACEN.AL_CANT WITH ALMACEN.AL_CANT-LD_CANT
           REPLACE ALMACEN.AL_PESO WITH ALMACEN.AL_PESO-LD_PESO
      ENDCASE 
      SKIP
    ENDDO
    
    * Calculo de pendientes
    anterior=ALIAS()
    SELECT v_ped_ln
    SUM lp_pend FOR lp_part=parte .AND. lp_estado<>'D' TO q_pend
    SELECT ALMACEN
    REPLACE almacen.al_pendien WITH q_pend
    SELECT (anterior)
    * Fin de calculo de pendientes
    SELECT almacen
    GO TOP
    SEEK EMPRESA
    CLEAR
    @ 19,0
    ? ' [ALMACEN] [  S A L D O  ]   [PEND.]    [D I S P.]'
    DO WHILE .NOT. EOF() .AND. AL_EMPR=EMPRESA
      IF AL_CANT>0
        SET COLOR TO G+/B
      ENDIF
      ? '    '+AL_ALM+'  '+TRANSFORM(AL_CANT,'99,999,999.99')+' '+V_ITEMS.IT_UNIDAD+;
      ' '+TRANS(al_pendien,'999,999.99')+' '+TRANSFORM(AL_CANT-AL_PENDIEN,'99,999,999.99')
      SET COLOR TO
      SKIP
    ENDDO
    SELECT F   && Lineas temporal
    @ 20,65 SAY (lp_precio)*.87/v_items.it_peso_t PICTURE "999.9999"    
    @ 22,55 SAY 'DSCTO US$='+TRANSFORM( ROUND(TOT_IMPORT*E.HP_DSCTO_P/100,2) ,'9,999,999.99') 
    @ 23,55 SAY 'TOTAL US$='+TRANSFORM(TOT_IMPORT-ROUND(TOT_IMPORT*E.HP_DSCTO_P/100,2),'9,999,999.99') 
    @ 19,0 TO 24,79 DOUBLE COLOR G+/B
    @ 19, 5 SAY 'SALDOS DEL ITEM'
    @ 20,55 SAY '$us./Kg.:'
    @ 19,60 SAY 'TOTALES'
    SELECT F   && Lineas temporal
  ENDIF
ENDIF

RETURN .T.

FUNCTION GET_DSCTO
*******************
SELECT V_CATEGO
SEEK EMPRESA+V_CLIENT.CL_CATEGO
IF FOUND()
  SELECT E    && Cabecera temporal
  REPLACE HP_DSCTO_P WITH V_CATEGO.CA_DSCTO 
ENDIF
SELECT E  && Cabecera temporal
RETURN .T.


PROCEDURE PED_COTIZ
*******************
DEFINE WINDOW PED_COTIZ ;
		FROM 0, 0 ;
		TO 8,79 ;
		TITLE " DATOS COTIZACION " ;
		NOFLOAT ;
		DOUBLE 
ACTIVATE WINDOW ped_cotiz

T_FLETES=0.0
T_EMBALA=0.0
T_OTROS =0.0

SELECT E  && Cabecera temporal PEDIDO

DO V_PED_V2.SPR

DO V_PED_CO.SPR

REPLACE HP_FLETES WITH T_FLETES 
REPLACE HP_EMBALA WITH T_EMBALA 
REPLACE HP_OTROS  WITH T_OTROS

DO V_PE_IN2.SPR

RELEASE WINDOW PED_COTIZ

RETURN

FUNCTION SUM_TOT
****************
@10,21 SAY TRANSFORM(T_FLETES+T_EMBALA+T_OTROS,'99,999,999.99')
@12,21 SAY TRANSFORM(HP_IMPORTE-round((hp_importe*hp_dscto_p)/100,2)+T_FLETES+T_EMBALA+T_OTROS,'99,999,999.99')
RETURN .T.
