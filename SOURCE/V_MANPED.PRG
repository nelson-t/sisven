***************************************************************************
* MANTENIMIENTO DE PEDIDOS
***************************************************************************
*DEBE SER LLAMADO DESDE MAIN (V_MUMAIN.PRG)
*IF NOT V_VALACC()
*  RETURN
*ENDIF  

NROLINEAS=V_GETPAR("PED_NROLINEA")

IF NROLINEAS="*"
   NROLINEAS=35
ELSE
   NROLINEAS=val(NROLINEAS)
ENDIF    

IF TYPE("EMPRESA")="U" OR TYPE("MI_EMPRESA")="U"
  DO V_GETEMP
ENDIF

IF TYPE("USER")="U" OR TYPE("MACHINE")="U"
  PUBLIC USER, MACHINE
  USER   =v_getuin("U") 
  MACHINE=v_getuin("M")
ENDIF

IF .NOT. V_USRAUT(USER,"PEDIDOS",1)  
   RETURN
ENDIF

* VERIFICA DOSIFICACION S.I.N.
IF V_GETNDO()=0
  RETURN
ENDIF

SET REFRESH TO 1

* VARIBLES PARA BUSQUEDAS                                          
MI_PED=SPACE(8)
MI_CLI=SPACE(8)
MI_NIT=SPACE(16)
MI_OBS=SPACE(40)
CHOICE=1
*
DO V_SETUP WITH "V_PED_CA.SPR", ""
DO LOC_SET

IF LASTKEY()=27
  CLOSE DATA
  RETURN
ENDIF

DO V_ONKEYS

SELECT V_CLIENT
SET FILTER TO CL_EMPR=EMPRESA

SELECT V_PED_HD
SET FILTER TO HP_EMPR=EMPRESA
SEEK EMPRESA
*
DO PE_VER
*
STORE .T. TO MU_LOOP
DO WHILE MU_LOOP
 ACTIVATE MENU M_PEDIDO
ENDDO

DO FIN

RETURN

PROCEDURE LOC_SET
*****************
* INICIALIZA AMBIENTE
SELECT 1
USE V_PED_HD ORDER CORRE
SELECT 2
USE V_PED_LN ORDER CORRE
SELECT 3
USE V_EMPR ORDER EMPR
SELECT 4
USE V_ITEMS ORDER PART
SELECT 5
* AREA RESERVADA PARA CABEZERAS TEMPORALES DE PEDS./PROFORMAS/NR.
SELECT 6
* AREA RESERVADA PARA LINEAS TEMPORALES PEDS./PROFORMAS/NR.
SELECT 7
USE V_CORRE ORDER EMPRESA 
SELECT 8
USE V_CLIENT ORDER CODIGO
SELECT 9
USE V_DESCTO ORDER PART
SELECT 10
USE V_VENDOR ORDER CODIGO
SELECT 11
USE V_CATEGO ORDER CATEGO
SELECT 12
USE V_TC ORDER FECHA &&DTOS(FECHA)

SELECT 15
* AREA RESERVADA PARA USO TEMPORAL DE CABEZAS DE FACT. 
SELECT 16
* AREA RESERVADA PARA USO TEMPORAL DE LINEAS DE FACT. 
SELECT 17
USE V_AREAS ORDER CODIGO

DEFINE POPUP GETAREA FROM 4,50 TO 10,78;
	PROMPT FIELD A_COD+'-'+A_DES ;
	TITLE 'SELECCIONE CON [ENTER]' 
ON SELECTION POPUP GETAREA DEACTIVATE POPUP
*****************************************************
                
SELECT V_PED_LN
SET RELATION TO LP_EMPR+LP_PART INTO V_ITEMS

SELECT  V_PED_HD
SET RELATION TO HP_EMPR INTO V_EMPR
SET RELATION TO HP_EMPR+HP_COD_CL  INTO V_CLIENT ADDITIVE
SET RELATION TO HP_EMPR+HP_COD_VEN INTO V_VENDOR ADDITIVE
SET RELATION TO DTOS(HP_FECHA) INTO V_TC ADDITIVE

SELECT V_ITEMS

DEFINE POPUP GETITEM FROM 8,15 TO 20,70 ;
	PROMPT FIELD IT_PART+'-'+IT_DESCR ;
	TITLE 'SELECCIONE ITEM CON [ENTER]' 
ON SELECTION POPUP GETITEM DEACTIVATE POPUP

SELECT V_CLIENT

DEFINE POPUP GETCLIEN FROM 8,20 TO 20,60 ;
	PROMPT FIELD SUBSTR(CL_RAZON,1,30)+'-'+CL_COD_CL ;
	TITLE 'SELECCIONE CLIENTE CON [ENTER]' 
ON SELECTION POPUP GETCLIEN DEACTIVATE POPUP

SELECT V_PED_HD

DEFINE WINDOW W_PEDIDO FROM 0,0 TO 24,79 DOUBLE

DEFINE WINDOW ped_hd FROM 0, 0 TO 8,79 TITLE " P E D I D O / C O T I Z A C I O N " ;
 NOFLOAT DOUBLE 

DEFINE WINDOW ped_ln FROM 9, 0 TO 21,79 TITLE "D E T A L L E" ;
 NOFLOAT DOUBLE 

DEFINE WINDOW ped_MSG FROM 21, 0 TO 24,79 TITLE "MENSAJES" ;
 NOFLOAT DOUBLE 
		
DEFINE MENU M_PEDIDO 
***
DO V_PED_MU
***
RETURN

PROCEDURE PE_VER
****************
SELECT V_PED_HD
DO V_PED_V.SPR
ACTIVATE SCREEN
@ 22,0 SAY "ษออออออออหอออออออหออออออหอออออออหอออออออหออออออหออออออหออออออหออออออออออหออออออป" COLOR W/B
@ 23,0 SAY "บ <-PREV บ SIG-> บ OJEA บ BUSCA บ VERIF บ INFO บ ALTA บ ANUL บ OPCIONES บ SALE บ" COLOR W/B
@ 24,0 SAY "ศออออออออสอออออออสออออออสอออออออสอออออออสออออออสออออออสออออออสออออออออออสออออออผ" COLOR W/B
SELECT V_PED_LN
GO TOP

KEYBOARD CHR(27)
BROWSE KEY V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO  ;
       FIELDS LP_PART:H='PART.#':W=.F.:10,V_ITEMS.IT_DESCR:20:H='DESCRIPCION',LP_CANT:H='CANTIDAD':P='9,999,999.99':W=.F.,V_ITEMS.IT_UNIDAD:H='UNI.',LP_PRECIO:H='PRECIO U.':P='999,999.99':W=.F.,LP_PEND:H='PENDIENTE':P='99,999,999.99':W=.F.,LP_ESTADO:H='E.':W=.F. WINDOW PED_LN  ;
       NOEDIT NOCLEAR NOAPPEND COLOR SCHEME 10
@ 12,34 FILL TO 20,45 COLOR W+/B       

@ 12,63 FILL TO 20,75 COLOR GR+/B

@ 12,77 FILL TO 20,77 COLOR W+/B
RETURN

PROCEDURE PE_NEXT
*****************
SELECT V_PED_HD
TREC=RECNO()
SKIP
IF EOF()
  GO TREC
ENDIF
DO PE_VER
RETURN

PROCEDURE PE_PREV
*****************
SELECT V_PED_HD
IF .NOT. BOF()
   SKIP -1
ENDIF 
DO PE_VER
RETURN

PROCEDURE PE_OJEAR
******************
SELECT V_PED_HD
DO V_IR_REG.SPR
IF LASTKEY()<>27
  W_FILTRO=".T."
  ON KEY LABEL F10 KEYBOARD CHR(23)
  ON KEY LABEL F12 DO V_BEXPOR WITH "HP_NRO, HP_FECHA, HP_COD_CL, HP_OBS, HP_IMPORTE, HP_ESTADO", W_FILTRO
  BROWSE KEY EMPRESA FIELDS HP_NRO:H='NRO.':R, HP_FECHA:H='FECHA':R, HP_COD_CL:H='COD.CLIE.':R,HP_OBS:H='OBSERVACIONES':P='@S30',HP_IMPORTE:H='IMPORTE':R,HP_ESTADO:H='ESTADO':R NODELETE WINDOW W_PEDIDO ;
         TITLE 'SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO';
         COLOR SCHEME 10
  DO V_ONKEYS
  DO PE_VER
ENDIF  
RETURN

PROCEDURE PE_SALE
*****************
STORE .F. TO MU_LOOP
DEACTIVATE MENU
RETURN

PROCEDURE FIN
**************
RELEASE MENU   M_PEDIDO
RELEASE WINDOW PED_HD
RELEASE WINDOW PED_LN
RELEASE WINDOW PED_MSG
RELEASE WINDOW W_PEDIDO
CLOSE DATA
ACTIVATE SCREEN
ON KEY
SET ESCAPE ON
CLEAR
RETURN

PROCEDURE PE_CALCULA
********************
SEEK V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO
SUM LP_CANT TO TOT_CANT REST WHILE V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO=LP_EMPR+LP_NRO
RETURN

FUNCTION GET_SALDO
******************
PARAMETER L_EMPR,L_ALM,L_PART
SELECT 0
USE V_DOC_LN ORDER KARDEX
SAL_IT=V_SALDOS(L_EMPR,L_ALM,L_PART)
USE
SELECT V_PED_LN
RETURN SAL_IT 

PROCEDURE PE_MIRA
*****************
SELECT 0
USE V_ALM ORDER ALM
T_ALM=SYS(3)
COPY TO (T_ALM) WITH CDX FIELDS AL_EMPR,AL_ALM,AL_DESCR,AL_CANT FOR AL_EMPR=EMPRESA
USE (T_ALM) ORDER ALM ALIAS ALMACEN EXCLU

SELECT 0
USE V_DOC_LN ORDER KARDEX
SET RELATION TO LD_EMPR+LD_ALM INTO ALMACEN

SELECT V_PED_LN

DEFINE WINDOW SAL_ALM FROM 0,44 TO 8,79 COLOR SCHEME 5 DOUBLE TITLE "SALDOS DEL ITEM" NOCLOSE

ACTIVATE WINDOW SAL_ALM

DEFINE WINDOW ped_VERI FROM 9, 0 TO 24,79 DOUBLE 

REG_ACT=0

ON KEY LABEL F10 KEYBOARD CHR(23)
BROWSE KEY V_PED_HD.HP_EMPR+V_PED_HD.HP_NRO  ;
       FIELDS LP_PART:H='PART.#':W=CHKREG(), ;
       V_ITEMS.IT_DESCR:H='DESCRIPCION':W=CHKREG(), ;
       LP_CANT:H='CANTIDAD':P='9,999,999.99':W=CHKREG(), ;
       V_ITEMS.IT_UNIDAD:H='UNI.':W=CHKREG(), ;
       LP_PRECIO:H='PRECIO U.':P='999,999.99':W=CHKREG(), ;
       LP_ESTADO:H='ESTADO':W=CHKREG(), ;
       IMPORTE=ROUND(LP_PRECIO*LP_CANT,2):H='IMPORTE':P='99,999,999.99':W=CHKREG(), ;
       LP_PEND:H='PEND.':P='99,999,999.99':W=CHKREG(), ;
       PART=LP_PART:H='PART.#':W=CHKREG() ;
       WINDOW PED_VERI  ;
       NOCLEAR NOEDIT NODELETE NOAPPEND TIMEOUT 300  TITLE 'DETALLE  -  [F10] PARA SALIR ';
       COLOR SCHEME 10

SELECT V_DOC_LN
USE

SELECT ALMACEN
USE

DELETE FILE (T_ALM+'.DBF')
DELETE FILE (T_ALM+'.CDX')

SELECT V_PED_LN

RELEASE WINDOW PED_VERI
RELEASE WINDOW SAL_ALM
DO V_ONKEYS
DO PE_VER
RETURN

FUNCTION CHKREG
***************
SELECT V_PED_LN
IF RECNO()<>REG_ACT
  =ALM_SALDO()  
  REG_ACT=RECNO()
ENDIF
RETURN .T.

FUNCTION ALM_SALDO
******************
PARTE=LP_PART
CLEAR
@ 0,0 SAY 'CALCULANDO SALDO : '+PARTE COLOR GR+/W

SELECT ALMACEN
REPLACE AL_CANT WITH 0 ALL

SELECT V_DOC_LN
SEEK EMPRESA+PARTE
DO WHILE !EOF() .AND. LD_EMPR=EMPRESA .AND. LD_PART=PARTE
  DO CASE
    CASE LD_INSAL='1'
      REPLACE ALMACEN.AL_CANT WITH ALMACEN.AL_CANT+LD_CANT
    CASE LD_INSAL='2'
      REPLACE ALMACEN.AL_CANT WITH ALMACEN.AL_CANT-LD_CANT
  ENDCASE 
  SKIP
ENDDO

SELECT ALMACEN
GO TOP
SEEK EMPRESA
CLEAR
@ 0,0 SAY ' ALMACEN  SALDO CANT   '
@ 1,0 SAY '-----------------------'
LINEA=2
DO WHILE !EOF() .AND. AL_EMPR=EMPRESA
  IF AL_CANT>0
    @ LINEA,0 SAY '   '+AL_ALM+'   '+TRANSFORM(AL_CANT,'9,999,999.99') COLOR G+/W
  ELSE
    @ LINEA,0 SAY '   '+AL_ALM+'   '+TRANSFORM(AL_CANT,'9,999,999.99') COLOR R/W
  ENDIF
  LINEA=LINEA+1
  SKIP
ENDDO
SELECT V_PED_LN
RETURN .T.

PROCEDURE PE_BUSCA
******************
DEFINE WINDOW PE_BUSCA FROM 06,8 TO 22,74 DOUBLE TITLE 'BUSQUEDA DE PEDIDOS' 
ACTIVATE WINDOW PE_BUSCA
@  1, 1 GET CHOICE FUNCTION '*RNV POR \<NRO;POR \<CODIGO O NIT;POR \<PENDIENTES;POR \<DESPACHADOS;POR COTI\<ZACIONES;POR \<OBSERVACION' ;
	    SIZE 1,10,1 COLOR SCHEME 1
@  1,22 SAY ':' GET MI_PED PICTURE '999999' WHEN CHOICE=1
@  3,22 SAY ':' GET MI_CLI WHEN CHOICE=2 AND MI_NIT=SPACE(16)
@  3,33 SAY '-' GET MI_NIT WHEN CHOICE=2 AND MI_CLI=SPACE(8)
@ 11,22 SAY ':' GET MI_OBS WHEN CHOICE=6
@ 13,22 GET okcancel FUNCTION '*TH \!\<OK;\?\<Cancelar' DEFAULT 1;
        SIZE 1,10,1  COLOR SCHEME 1
READ CYCLE

SELECT V_PED_HD
IF okcancel = 1
  W_FILTRO=".T."
  W_TITLE='SELECCIONE CON [F10] - [F12] GENERA ARCHIVO TEXTO'
  ON KEY LABEL F12 DO V_BEXPOR WITH "HP_NRO, HP_COD_CL, HP_FECHA, HP_OBS, HP_IMPORTE, HP_ESTADO", W_FILTRO
  ON KEY LABEL F10 KEYBOARD CHR(23)
  DO CASE
   CASE choice = 1
     W_FILTRO="ALLTRIM(MI_PED)$HP_NRO"
   CASE choice = 2
     W_FILTRO=".F."
     IF MI_CLI<>SPACE(8)   
       W_FILTRO="ALLTRIM(MI_CLI)$HP_COD_CL" 
     ENDIF
     IF MI_NIT<>SPACE(16)
       W_FILTRO="ALLTRIM(MI_NIT)$V_CLIENT.CL_RUC"
     ENDIF
   CASE choice = 3
     W_FILTRO="HP_ESTADO$'PI'"
   CASE choice = 4
     W_FILTRO="HP_ESTADO='D'"
   CASE choice = 5
     W_FILTRO="HP_ESTADO='C'"
   CASE choice = 6
     W_FILTRO="ALLTRIM(UPPER(MI_OBS))$UPPER(HP_OBS)"
  ENDCASE
  ***
  BROWSE NOEDIT FOR EVAL(W_FILTRO) ;
     FIELDS HP_NRO:H='NRO.',HP_COD_CL:H='COD.CLI.',V_CLIENT.CL_RUC:H="NIT":16, HP_FECHA:H='FECHA',HP_OBS:H='OBSERVACIONES':P='@S30',HP_IMPORTE:H='IMPORTE', HP_ESTADO:H='ESTADO' ;
     WINDOW W_PEDIDO TITLE W_TITLE COLOR SCHEME 10
ENDIF
RELEASE WINDOW PE_BUSCA
DO V_ONKEYS
DO PE_VER
RETURN
